library ObserverMsgs initializer onInit requires Observers, LegacyTriggers
globals
    constant real ob_TextTime = 10
endglobals

function replayToNoticeObservers takes nothing returns nothing
    if IsPlayerObserver(GetEnumPlayer()) == true and obs_notices[GetPlayerId(GetEnumPlayer())] then
        call DisplayTimedTextToPlayer(GetEnumPlayer(), 0, 0, bj_cineFadeContinueTrans, bj_lastPlayedMusic)
    endif
endfunction

function displayTimedTextToNoticeObservers takes real duration, string message returns nothing
    local real r = bj_cineFadeContinueTrans
    local string s = bj_lastPlayedMusic

    set bj_cineFadeContinueTrans = duration
    set bj_lastPlayedMusic = message

    call ForForce(bj_FORCE_ALL_PLAYERS,function replayToNoticeObservers)

    set bj_cineFadeContinueTrans = r
    set bj_lastPlayedMusic = s
endfunction

function replayToObservers takes nothing returns nothing
    if IsPlayerObserver(GetEnumPlayer()) == true then
        call DisplayTimedTextToPlayer(GetEnumPlayer(), 0, 0, bj_cineFadeContinueTrans, bj_lastPlayedMusic)
    endif
endfunction

function displayTimedTextToObservers takes real duration, string message returns nothing
    local real r = bj_cineFadeContinueTrans
    local string s = bj_lastPlayedMusic

    set bj_cineFadeContinueTrans = duration
    set bj_lastPlayedMusic = message

    call ForForce(bj_FORCE_ALL_PLAYERS,function replayToObservers)

    set bj_cineFadeContinueTrans = r
    set bj_lastPlayedMusic = s
endfunction

// displayTimedTextToObservers
function relayObserverEventsShow takes nothing returns nothing
    local eventid e = GetTriggerEventId()
    local player p = GetTriggerPlayer()
    local integer id = GetPlayerId(p)
    local unit u = GetTriggerUnit()
    if e == EVENT_PLAYER_HERO_SKILL then
        call displayTimedTextToNoticeObservers(ob_TextTime, udg_RealNames[id]+"["+GetUnitName(u)+"] has learned: "+GetObjectName(GetLearnedSkill())+" - Level "+I2S(GetLearnedSkillLevel()))
    elseif e == EVENT_PLAYER_HERO_LEVEL and id < 12 then
        call displayTimedTextToNoticeObservers(ob_TextTime, udg_RealNames[id]+"["+GetUnitName(u)+"] has leveled to: Level "+I2S(GetUnitLevel(u)))
    elseif e == EVENT_PLAYER_UNIT_PAWN_ITEM then
        // u => seller (troll)
        // solditem => solditem
        // buying unit => buyer (boat/merc)
        call displayTimedTextToNoticeObservers(ob_TextTime, udg_RealNames[id]+"["+GetUnitName(u)+"] has sold: "+GetItemName(GetSoldItem())+" to "+GetUnitName(GetBuyingUnit()))
    elseif e == EVENT_PLAYER_UNIT_SELL_ITEM then
        // u => seller (boat/merc)
        set p = GetOwningPlayer(GetBuyingUnit())
        set id = GetPlayerId(p)
        call displayTimedTextToNoticeObservers(ob_TextTime, udg_RealNames[id]+"["+GetUnitName(udg_PUnits[id])+"] has bought: "+GetItemName(GetSoldItem())+" from "+GetUnitName(u))
    elseif e == EVENT_PLAYER_UNIT_CONSTRUCT_FINISH then
        call displayTimedTextToNoticeObservers(ob_TextTime, udg_RealNames[id]+"["+GetUnitName(udg_PUnits[id])+"] has built: "+GetUnitName(u))
    elseif e == EVENT_PLAYER_HERO_REVIVE_FINISH then
        call displayTimedTextToNoticeObservers(ob_TextTime, udg_RealNames[id]+"["+GetUnitName(u)+"] has revived")
    endif
endfunction

//===========================================================================
function onInit takes nothing returns nothing
    local integer i = 0
    set gg_trg_observer_msgs = CreateTrigger(  )
    loop
        exitwhen i > 15
        call TriggerRegisterPlayerUnitEvent(gg_trg_observer_msgs, Player(i), EVENT_PLAYER_HERO_SKILL, null)
        call TriggerRegisterPlayerUnitEvent(gg_trg_observer_msgs, Player(i), EVENT_PLAYER_HERO_LEVEL, null)
        call TriggerRegisterPlayerUnitEvent(gg_trg_observer_msgs, Player(i), EVENT_PLAYER_UNIT_PAWN_ITEM, null)
        call TriggerRegisterPlayerUnitEvent(gg_trg_observer_msgs, Player(i), EVENT_PLAYER_UNIT_SELL_ITEM, null)
        call TriggerRegisterPlayerUnitEvent(gg_trg_observer_msgs, Player(i), EVENT_PLAYER_UNIT_CONSTRUCT_FINISH, null)
        call TriggerRegisterPlayerUnitEvent(gg_trg_observer_msgs, Player(i), EVENT_PLAYER_HERO_REVIVE_FINISH, null)
        set i = i + 1
    endloop
    call TriggerAddAction(gg_trg_observer_msgs, function relayObserverEventsShow )
endfunction
//===========================================================================
