package PlayerExtensions
import initlater Tribe
import PublicLibrary
import ForceExtensions
import HashSet

let newObs = new HashSet<player>()

public function player.getTribe() returns Tribe
    return Tribe.ofPlayer(this)

public function player.isPlaying() returns boolean
    return this.getController() == MAP_CONTROL_USER and this.getSlotState() == PLAYER_SLOT_STATE_PLAYING

public function player.subLumber(int val)
    this.subState(PLAYER_STATE_RESOURCE_LUMBER, val)

public function player.getAllies() returns force
    return GetPlayersAllies(this)

public function player.setHandicapXP(real handicap)
    SetPlayerHandicapXP(this, handicap)

public function player.setColor(playercolor color, bool changeExisting)
    SetPlayerColorBJ(this, color, changeExisting)

public function player.isObserver() returns bool
    return IsPlayerObserver(this) or newObs.has(this)

public function player.getTroll() returns unit
    return GetPlayerTroll(this)

public function player.makeObserver()
    this.setState(PLAYER_STATE_OBSERVER, 1)
    this.setupObs()

public function player.makeObserverNew()
    newObs.add(this)
    this.setupObs()

public function player.isObserverNew() returns boolean
    return newObs.has(this)

public function player.setupObs()
    let iter = bj_FORCE_ALL_PLAYERS.iterator()
    for otherPlayer from iter
        SetPlayerAllianceStateBJ(otherPlayer, this, bj_ALLIANCE_NEUTRAL_VISION)

    SetPlayerAllianceStateBJ(this, players[PLAYER_NEUTRAL_AGGRESSIVE], bj_ALLIANCE_NEUTRAL)
