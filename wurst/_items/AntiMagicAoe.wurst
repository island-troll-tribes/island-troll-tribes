package AntiMagicAoe
import AbilityObjEditing
import ClosureEvents
import ClosureForGroups
import DummyCaster
import Orders
import Assets

let ABILITY_ANTI_MAGIC_SHELL = 'A0F8'
let ABILITY_ANTI_MAGIC_DUMMY = 'XAMG'
let COOLDOWN = 20.
let MANACOST = 20
let AOE = 800.

@compiletime function createDummySpell()
    new AbilityDefinitionIllidanChannel(ABILITY_ANTI_MAGIC_DUMMY)
    ..setArtTarget("")
    ..setArtCaster("")
    ..setArtSpecial("")
    ..setArtEffect("")
    ..setAnimationNames("")
    ..setLevels(1)
    ..presetFollowThroughTime(lvl -> 0)
    ..presetAreaofEffect(lvl -> 0)
    ..presetCastRange(lvl -> 1)
    ..presetDurationHero(lvl -> 0.01)
    ..presetDurationNormal(lvl -> 0.01)
    ..presetManaCost(lvl -> MANACOST)
    ..presetCooldown(lvl -> COOLDOWN)
    ..setHeroAbility(false)
    ..setItemAbility(false)
    ..presetOptions(lvl -> 1)
    ..presetDisableOtherAbilities(lvl -> false)
    ..presetTargetType(lvl -> 0)
    ..setHotkeyNormal("Q")
    ..setName("Anti-magic Shell")
    ..presetTooltipNormal(lvl -> "[|cffffcc00Q|r] Anti-magic Shell")
    ..presetTooltipNormalExtended(lvl -> "For every ally in 700 radius, grants an anti-magic barrier that absorbs 100 magic damage and blocks them from being ensnared by hunters or ensnare traps. Lasts 10 seconds.")
    ..setIconNormal(Icons.bTNAntiMagicShell)
    ..setIconTurnOff(Icons.bTNAntiMagicShell)
    ..setIconResearch(Icons.bTNAntiMagicShell)

function onCast()
    let caster = GetSpellAbilityUnit()
    forUnitsInRange(caster.getPos(), AOE) u ->
        if u.isAlive() and u.getOwner().isAllyOf(caster.getOwner())
            new DummyCaster()
                ..owner(caster.getOwner())
                ..origin(u.getPos() + vec2(100, 0))
                ..delay(1)
                ..castTarget(ABILITY_ANTI_MAGIC_SHELL, 1, Orders.antimagicshell, u)

init
    registerSpellEffectEvent(ABILITY_ANTI_MAGIC_DUMMY) ->
        onCast()
