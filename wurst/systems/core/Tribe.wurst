package Tribe
import LinkedList
import HashMap
import TribeUtils
import Lodash
import LodashExtensions
import PlayerExtensions
import LegacyColors

public class Tribe
    static let playerToTribeMap = new HashMap<player, Tribe>()
    static let tribes = new OwnedLinkedList<Tribe>()

    static let isNotDefeated = Predicate.owned<Tribe>(tribe -> not tribe.isDefeated())
    static let memberIsDead = Predicate.owned<player>(member -> not member.getTroll().isAlive())
    static let hasNoAliveMembers = Predicate.owned<Tribe>(tribe -> tribe.getMembers().every(memberIsDead))

    int tribeId
    rect spawn
    let tribePlayers = new OwnedLinkedList<player>()
    let tribe = CreateForce()
    var defeated = true

    static function getTribes() returns LinkedList<Tribe>
        return tribes

    static function ofPlayer(player query) returns Tribe
        return playerToTribeMap.get(query)

    construct(int id)
        tribeId = id

        let range = getPlayerIdRangeForTribe(tribeId)
        for pid = range.min to range.max
            let member = players[pid]
            if member.isIngame() and not member.isObserver()
                tribePlayers.push(member)
                playerToTribeMap.put(member, this)
                tribe.addPlayer(member)

                defeated = false

        tribePlayers.forEach() a ->
            tribePlayers.forEach() b ->
                if a != b
                    SetPlayerAllianceStateBJ(a, b, bj_ALLIANCE_ALLIED_VISION)

        if not defeated
            tribes.push(this)

    function getId() returns int
        return tribeId

    function isDefeated() returns bool
        return defeated

    function makePlayersObservers()
        getMembers().forEach(member -> member.makeObserver())

    function wasDefeated()
        makePlayersObservers()
        defeated = true
        if tribe.containsPlayer(GetLocalPlayer())
            print(HIGHLIGHT_COLOR + "You have been defeated|r")

    function wasVictorious()
        makePlayersObservers()
        if tribe.containsPlayer(GetLocalPlayer())
            print(HIGHLIGHT_COLOR + "You have won! Please play again!|r")

    function getForce() returns force
        return tribe

    function getMembers() returns LinkedList<player>
        return tribePlayers

    function setSpawn(rect _spawn)
        spawn = _spawn

    function getSpawn() returns rect
        return spawn
