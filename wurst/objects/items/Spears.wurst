package Spears

// Standard library imports:
import ClosureEvents
import ClosureTimers
import DummyCaster
import HashMap
import Orders
import AbilityObjEditing
import ObjectIds
import ObjectIdGenerator

// Third-party imports:
import ManaBurn

// Local imports:
import LocalObjectIDs

let ABILITY_SPEAR_HIT_DETECTION = compiletime(ABIL_ID_GEN.next())

// If you change the following, update tooltip manually in SpearsDefinition
public let SPEAR_RESPAWN_CHANCE   = 0.666
public let IRON_RESPAWN_CHANCE    = 0.666
public let STEEL_RESPAWN_CHANCE   = 0.666
public let DARK_RESPAWN_CHANCE    = 0.
public let POISON_RESPAWN_CHANCE  = 0.5
public let RPOISON_RESPAWN_CHANCE = 0.5
public let UPOISON_RESPAWN_CHANCE = 0.5

let DARK_SPEAR_MANA_BURN_PERCENTAGE = .40
let DARK_SPEAR_MIN_MANA_BURN        = 10.

@compiletime function createAbilSpearHitDetection()
    new AbilityDefinitionThunderBoltCreep(ABILITY_SPEAR_HIT_DETECTION)
        ..setDummyAbility()
        ..setMissileArt("")
        ..setMissileHomingEnabled(true)
        ..setMissileSpeed(700)
        ..setDamage(1, 0)
        ..setDurationHero(1, 0.01)
        ..setDurationNormal(1, 0.01)
        ..setTargetsAllowed(1, commaList(
            TargetsAllowed.air,
            TargetsAllowed.enemies,
            TargetsAllowed.ground,
            TargetsAllowed.neutral,
            TargetsAllowed.organic,
            TargetsAllowed.terrain
        ))
        ..setName("SPEAR HIT")
        ..setEditorSuffix("(Wurst)")

enum SpearType
    Spear
    DarkSpear
    IronSpear
    SteelSpear
    PoisonSpear
    UltraPoisonSpear
    RefinedPoisonSpear

let spellToSpearType = new HashMap<int, SpearType>
let spearListeners = new HashMap<unit, EventListener>()
let darkSpearCounters = new HashMap<unit, int>()

function onSpear(unit caster, unit target, SpearType spearType)
    let dummyCaster = new DummyCaster()
        ..delay(8)
        ..owner(caster.getOwner())
        ..origin(caster.getPos())
    let dummy = dummyCaster.castTarget(ABILITY_SPEAR_HIT_DETECTION, 1, Orders.creepthunderbolt, target)

    bindSpearDamageListener(dummy, caster, target, spearType)

function bindSpearDamageListener(unit dummy, unit caster, unit target, SpearType spearType)
    let listener = EventListener.add(EVENT_UNIT_DAMAGED) ->
        let damaged = GetTriggerUnit()
        if damaged == target and GetEventDamageSource() == dummy
            if spearListeners.has(dummy)
                destroy spearListeners.getAndRemove(dummy)
                onSpearImpact(caster, target, spearType)

    spearListeners.put(dummy, listener)

function onSpearImpact(unit caster, unit target, SpearType spearType)
    switch spearType
        case UltraPoisonSpear
            spawnItemChance(UPOISON_RESPAWN_CHANCE, ITEM_ULTRA_POISON_SPEAR, target.getPos())
        case RefinedPoisonSpear
            spawnItemChance(RPOISON_RESPAWN_CHANCE, ITEM_REFINED_POISON_SPEAR, target.getPos())
        case PoisonSpear
            spawnItemChance(POISON_RESPAWN_CHANCE, ITEM_POISON_SPEAR, target.getPos())
        case DarkSpear
            spawnItemChance(DARK_RESPAWN_CHANCE, ITEM_DARK_SPEAR, target.getPos())
            let burnAmount = DARK_SPEAR_MIN_MANA_BURN +
                (target.getMana() * DARK_SPEAR_MANA_BURN_PERCENTAGE)

            // Perform the mana reduction.
            caster.drainMana(target, burnAmount)
        case SteelSpear
            spawnItemChance(STEEL_RESPAWN_CHANCE, ITEM_STEEL_SPEAR, target.getPos())
        case IronSpear
            spawnItemChance(IRON_RESPAWN_CHANCE, ITEM_IRON_SPEAR, target.getPos())
        case Spear
            spawnItemChance(SPEAR_RESPAWN_CHANCE, ITEM_SPEAR, target.getPos())

    if target.isType(UNIT_TYPE_HERO) and not spearType == SpearType.DarkSpear
        addSpearResistance(target)


function removeDarkSpearCounter(unit target)
    // Deduct counters by 1 or remove from map if they go to 0
    if darkSpearCounters.has(target)
        let currentCounters = darkSpearCounters.get(target)
        if currentCounters > 1
            darkSpearCounters.put(target, currentCounters - 1)
        else
            darkSpearCounters.remove(target)

function addSpearResistance(unit u)
    SetPlayerAbilityAvailable(u.getOwner(), ABILITY_SPEAR_RESIST, false)
    if not u.hasAbility(ABILITY_SPEAR_RESIST)
        u.addAbility(ABILITY_SPEAR_RESIST)
    else if u.getAbilityLevel(ABILITY_SPEAR_RESIST) < 6
        u.incAbilityLevel(ABILITY_SPEAR_RESIST)

    doAfter(1) ->
        if u.getAbilityLevel(ABILITY_SPEAR_RESIST) > 1
            u.decAbilityLevel(ABILITY_SPEAR_RESIST)
        else
            u.removeAbility(ABILITY_SPEAR_RESIST)

function spawnItemChance(real chance, int itemId, vec2 pos)
    if GetRandomReal(0, 1) <= chance
        createItem(itemId, pos)

function onSpell()
    let caster = GetSpellAbilityUnit()
    let target = GetSpellTargetUnit()
    let spell = GetSpellAbilityId()
    if spellToSpearType.has(spell)
        onSpear(caster, target, spellToSpearType.get(spell))

init
    spellToSpearType.put(ABILITY_SPEAR        , SpearType.Spear)
    spellToSpearType.put(ABILITY_SPEAR_DARK   , SpearType.DarkSpear)
    spellToSpearType.put(ABILITY_SPEAR_IRON   , SpearType.IronSpear)
    spellToSpearType.put(ABILITY_SPEAR_STEEL  , SpearType.SteelSpear)
    spellToSpearType.put(ABILITY_SPEAR_POISON , SpearType.PoisonSpear)
    spellToSpearType.put(ABILITY_SPEAR_RPOISON, SpearType.RefinedPoisonSpear)
    spellToSpearType.put(ABILITY_SPEAR_UPOISON, SpearType.UltraPoisonSpear)

    EventListener.add(EVENT_PLAYER_UNIT_SPELL_EFFECT) ->
        onSpell()
