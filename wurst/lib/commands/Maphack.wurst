package Maphack

// Third-party imports:
import Toolkit

// The fog modifier per player.
fogmodifier array fog

// The status of the fog modifier per player.
bool array status

init
    for index = 0 to bj_MAX_PLAYERS - 1
        // Create a fogmodifier per player.
        fog[index] = CreateFogModifierRect(
            players[index],
            FOG_OF_WAR_VISIBLE,
            bj_mapInitialPlayableArea,
            false,
            false
        )

        // Mark that each fogmodifier starts is not yet enabled.
        status[index] = false


    registerToolkitCommand("mh") (triggerPlayer, arguments) ->
        // Look up the index for the triggering player.
        let index = triggerPlayer.getId()

        // Inverse the current state for the player.
        status[index] = not status[index]

        // Perform the appropriate action for the fogmodifier.
        if status[index]
            fog[index].start()
        else
            fog[index].stop()
