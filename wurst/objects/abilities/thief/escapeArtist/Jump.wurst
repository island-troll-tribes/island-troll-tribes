package Jump
import ClosureEvents
import TerrainUtils
import JumpSystem
import Orders
import UnitExtensions
import ChannelAbilityPreset
import LocalObjectIDs
import Lodash
import Assets
import ToolTipsUtils

let CAST_RANGE = 99999.
let COOLDOWN = 40.
let MANACOST = 5
let TOOLTIP_NORM = "Jump"
let TOOLTIP_EXTENDED = "Allows the thief to jump a short distance. Has {0} seconds cooldown.".format(COOLDOWN.toToolTipLightBlue())

@configurable let JUMP_MAX_RANGE = 900.

class Jump extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId, 1, true)
        this.presetManaCost(lvl -> MANACOST)
        this.presetCooldown(lvl -> COOLDOWN)
        this.setHeroAbility(false)
        this.setItemAbility(false)
        this.presetTargetTypes(Targettype.POINT)
        this.presetTargetsAllowed(lvl -> "terrain")
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setIconNormal(Icons.bTNNagaUnBurrow)
        this.setIconResearch(Icons.bTNNagaUnBurrow)
        this.setIconTurnOff(Icons.bTNNagaUnBurrow)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setCastRange(1, CAST_RANGE)
        this.setArtDuration(1, 0)
        this.setBaseOrderID(1, "breathoffrost")
        this.setOrderStringUseTurnOn("breathoffrost")

@compiletime function createJump()
    new Jump(ABILITY_JUMP, "R", new Pair(3, 0))

function onCast()
    let caster = GetSpellAbilityUnit()
    let targetPos = vec2(GetSpellTargetX(), GetSpellTargetY())
    let casterPos = caster.getPos()
    let jumpPos = getJumpPos(casterPos, targetPos, JUMP_MAX_RANGE)
    performJump(caster, jumpPos, 2, 8, 0.85)

init
    registerSpellEffectEvent(ABILITY_JUMP, () -> onCast())

    EventListener.add(EVENT_PLAYER_UNIT_SPELL_CAST) ->
        if GetSpellAbilityId() == ABILITY_JUMP
            onStartCast(JUMP_MAX_RANGE)
