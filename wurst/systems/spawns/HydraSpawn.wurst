package HydraSpawn

// Standard library imports:
import Assets
import ClosureEvents
import ClosureForGroups
import LinkedList

// Local imports:
import LocalObjectIDs
import UnitExtensions

constant HYDRA_ALTAR_SFX = Units.wispExplode

function maybeSummonHydra()
    let altar = GetSellingUnit()
    let target = GetSoldItem()

    if target.getTypeId() != ITEM_HYDRA_HINT
        return

    // Undo the transaction, which is only used to fire this event.
    target.remove()
    altar.addItemToStock(ITEM_HYDRA_HINT, 1, 1)

    let mushroom = altar.itemInSlot(1)
    if mushroom.getTypeId() != ITEM_MUSHROOM
        return

    let riverRoot = altar.itemInSlot(2)
    if riverRoot.getTypeId() != ITEM_RIVER_ROOT
        return

    let elkHide = altar.itemInSlot(3)
    if elkHide.getTypeId() != ITEM_ELK_HIDE
        return

    let herb = altar.itemInSlot(0)
    rect spawn
    switch herb.getTypeId()
        case ITEM_BLUE_HERB
            spawn = gg_rct_ship_LO
        case ITEM_YELLOW_HERB
            spawn = gg_rct_ship_RO
        case ITEM_ORANGE_HERB
            spawn = gg_rct_ship_TO
        case ITEM_PURPLE_HERB
            spawn = gg_rct_ship_BO
        default
            return

    // Check for nearby corpses.
    let meats = new LinkedList<unit>()
    forUnitsInRange(gg_rct_discoduck.getCenter(), 400) (unit u) ->
        if u.getTypeId() == UNIT_MEAT
            meats.push(u)
    if meats.size() < 4
        destroy meats
        return

    for meat in meats
        createItem(ITEM_COOKED_MEAT, meat.getPos())
        meat.remove()

    destroy meats

    mushroom.remove()
    riverRoot.remove()
    elkHide.remove()
    herb.remove()

    flashEffect(HYDRA_ALTAR_SFX, altar, "overhead")

    // Look up the spawn position.
    let pos = spawn.getCenter()

    // Compute the angle between the spawn and the altar.
    let facing = pos.angleTo(altar.getPos())

    // Spawn the hydra.
    createUnit(
        players[PLAYER_NEUTRAL_AGGRESSIVE],
        UNIT_ANCIENT_HYDRA,
        spawn.getCenter(),
        facing
    )


init
    EventListener.add(EVENT_PLAYER_UNIT_SELL_ITEM, () -> maybeSummonHydra())
