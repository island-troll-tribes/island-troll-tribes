package Magic

// Standard library imports:
import Assets
import ClosureTimers
import DummyCaster
import EventHelper
import InstantDummyCaster
import Orders
import RegisterEvents

// Local imports:
import DiscoDuck
import LocalObjectIDs
import Mammoth
import ResourceSpawns
import TheOne


/**
    This function is called when the item "Magic" is used, it has random chance of triggering following effect :
    -Set HP/MP to 30%, 10% chance to happen
    -Set HP/MP respectively to 6 and 2 %, 10% chance to happen
    -Set daytime to midnight, 10% chance to happen
    -Reduce the user level by 1, 10% chance to happen
    -Summon a meteor on user, 20% chance to happen
    -Add 500 xp to user, 10% chance to happen
    -Fully restore HP/MP, 20% chance to happen
    -Spawn 2 manas crystal on user position, 10% chance to happen

    In case mammoth is dead, the magic will summon the disco duck instead of triggering the listed effects.
    In case magic is used in the One region, the One will be summoned instead of trigerring the listed effects.
**/
function useMagic(unit user)
    // Pick an event randomly, as necessary.
    let chance = GetRandomReal(0, 1)

    // Deregister the item from the spawn system.
    lowerItem(1)

    // Using magic always cures the target.
    InstantDummyCaster.castTarget(user.getOwner(), ABILITY_CURE_ALL, 1, "autodispel", user)

    // Perform whichever event the magic activates.
    doAfter(1) () ->
        // Attempt to spawn The One if in the apparopriate area.
        if user.isInRegion(THE_ONE_REGION) and canSpawnTheOne()
            spawnTheOne()
        // Attempt to summon the Disco-Duck if the Mammoth is dead.
        else if Mammoth.getInstance().isDead() and DiscoDuck.getInstance() == null
            flashEffect(Abilities.darkPortalTarget, user, "origin")
            createUnit(
                players[PLAYER_NEUTRAL_AGGRESSIVE],
                UNIT_DISCO_DUCK,
                gg_rct_discoduck.getCenter(),
                (225.0).asAngleDegrees()
            )
        // Mildly damage the unit.
        else if chance < 0.1
            flashEffect(Abilities.deathCoilSpecialArt, user, "origin")
            user.setHP(user.getMaxHP() * 0.3)
            user.setMana(user.getMaxMana() * 0.3)
        // Severely damage the unit.
        else if chance < 0.2
            flashEffect(Objects.orcLargeDeathExplode, user, "origin")
            user.setHP(user.getMaxHP() * 0.06)
            user.setMana(user.getMaxMana() * 0.02)
        // Transition to midnight.
        else if chance < 0.3
            SetTimeOfDay(24)
            let fx = addEffect(Abilities.starfallCaster, user, "origin")
            doAfter(2) () ->
                fx.destr()
        // Delevel the unit.
        else if chance < 0.4
            flashEffect(Abilities.aIsoTarget, user, "chest")
            user.removeLevels(1)
        // Summon a meteor on the unit.
        else if chance < 0.6
            new DummyCaster()
                ..delay(3)
                ..owner(user.getOwner())
                ..origin(user.getPos() + vec2(100,0))
                ..castTarget(ABILITY_DOOMSDAY, 1, Orders.flamestrike, user)
        // Add experience to the unit.
        else if chance < 0.7
            flashEffect(Abilities.aIemTarget,  user, "origin")
            user.addXp(500, true)
        // Fully heal the unit.
        else if chance < 0.9
            flashEffect(Abilities.aIreTarget,  user, "origin")
            user.setHP(user.getMaxHP())
            user.setMana(user.getMaxMana())
        // Summon two mana crystals near the unit.
        else
            for _ = 1 to 2
                flashEffect(Abilities.blinkTarget, createItem(ITEM_MANA_CRYSTAL, user.getPos()).getPos())


init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_USE_ITEM) ->
        if EventData.getManipulatedItem().getTypeId() == ITEM_MAGIC
            useMagic(EventData.getManipulatingUnit())
