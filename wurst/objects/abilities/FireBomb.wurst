package FireBomb

// Standard lib Imports:
import AbilityObjEditing
import Assets
import ObjectIdGenerator
import ObjectIds
import RegisterEvents
import DummyCaster
import Orders

// Local Imports:
import Items
import LocalAssets
import LocalObjectIDs
import ToolTipsUtils
import LocalItemObjEditing

let ABIL_DUMMY_CAST_ID = compiletime(ABIL_ID_GEN.next())
let ABIL_TRUE_CAST_ID = compiletime(ABIL_ID_GEN.next())

let COOLDOWN = 15.
let DURATION = 5.5
let TOTAL_DAMAGE = 80.

let TT = "" +
    "A shell of destructible material that is highly flammable when thrown. " +
    "Dealing approximatively {0} damage over {1} seconds. Good against buildings."
    .format(TOTAL_DAMAGE.toToolTipRed(), DURATION.toToolTipLightBlue())
    +makeToolTipCooldown(COOLDOWN)


// The actual ability to be casted
@compiletime function createTrueCast()
    new AbilityDefinitionFlameStrikeCreep(ABIL_TRUE_CAST_ID)
        ..setAreaofEffect(1, 170.0)
        ..setBuffs(1, "B00R")
        ..setCastingTime(1, 0.5)
        ..setManaCost(1, 0)
        ..setFullDamageDealt(1, TOTAL_DAMAGE)
        ..setFullDamageInterval(1, 2.1)
        ..setHalfDamageDealt(1, 15.0)
        ..setDurationNormal(1, DURATION)
        ..setEditorSuffix("")
        ..setArtEffect("")
        ..setDurationHero(1, 0.0)
        ..setMissileSpeed(1000)
        ..setName("FireBomb Effect")
        ..setCastRange(1, 10000.0)
        ..setArtSpecial(Abilities.flameStrikeTarget)
        ..setTargetsAllowed(1, commaList(
            TargetsAllowed.enemies,
            TargetsAllowed.friend,
            TargetsAllowed.neutral,
            TargetsAllowed.self,
            TargetsAllowed.structure,
            TargetsAllowed.tree
            )
        )

// The dummy ability for the item.
@compiletime function createDummyCast()
    new AbilityDefinitionTinkererClusterRocketsLevel0(ABIL_DUMMY_CAST_ID)
        ..setAreaofEffect(1, 150.0)
        ..setBuffs(1, "")
        ..setCheckDependencies(false)
        ..setCooldown(1, COOLDOWN)
        ..setManaCost(1, 0)
        ..setDamageAmount(1, 0.0)
        ..setDamageInterval(1, 0.0)
        ..setMissileCount(1, 4)
        ..setMaxDamage(1, 0.0)
        ..setBuildingDamageFactor(1, 0.0)
        ..setEffectDuration(1, 0.0)
        ..setDurationNormal(1, 0.0)
        ..setEffects(1, "")
        ..setHeroAbility(false)
        ..setDurationHero(1, 0.0)
        ..setLevels(1)
        ..setMissileArt(Abilities.lordofFlameMissile)
        ..setName("Fire Bomb Cast")
        ..setCastRange(1, 700.0)
        ..setTargetsAllowed(1, "terrain")

@compiletime function createFireBombItem()
    createPerishableItem(ITEM_FIRE_BOMB)
        ..setAbilities(ABIL_DUMMY_CAST_ID.toRawCode())
        ..setInterfaceIcon(Icons.bTNLiquidFire)
        ..setCooldownGroup(ABIL_DUMMY_CAST_ID.toRawCode())
        ..setModelUsed(LocalItems.liquidFire)
        ..setLumberCost(30)
        ..setNameEnhance("Fire Bomb")
        ..setScalingValue(1.2)
        ..setNumberofCharges(2)
        ..setTooltipExtended(TT)
        ..setDescription(TT)

function onFireBombCast()
    new DummyCaster()
        ..owner(GetSpellAbilityUnit().getOwner())
        ..castPoint(ABIL_TRUE_CAST_ID, 1, OrderIds.flamestrike, EventData.getSpellTargetPos())
init
    registerSpellEffectEvent(ABIL_DUMMY_CAST_ID, function onFireBombCast)
