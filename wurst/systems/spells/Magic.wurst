package Magic
import RegisterEvents
import IttDummyCaster
import TheOneSpawn
import Mammoth
import DiscoDuck
import PublicLibrary

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_USE_ITEM) ->
        if GetManipulatedItem().getTypeId() == ITEM_MAGIC
            let user = GetManipulatingUnit()
            let chance = GetRandomReal(0, 1)

            itemLower(1)

            if user.isInRegion(THE_ONE_REGION) and canSpawnTheOne()
                spawnTheOne()
            else if Mammoth.getInstance().isDead() and DiscoDuck.getInstance() == null
                new DiscoDuck()
            else if chance < 0.1
                user.setHP(user.getMaxHP() * 0.3)
                user.setMana(user.getMaxMana() * 0.3)
            else if chance < 0.2
                user.setHP(user.getMaxHP() * 0.06)
                user.setMana(user.getMaxMana() * 0.02)
            else if chance < 0.3
                SetTimeOfDay(24)
            else if chance < 0.4
                user.removeLevels(1)
            else if chance < 0.6
                new IttDummyCaster(3).castTarget(user.getOwner(), 'ANfs', 1, "flamestrike", user)
            else if chance < 0.7
                user.addXp(500, true)
            else if chance < 0.9
                user.setHP(user.getMaxHP())
                user.setMana(user.getMaxMana())
            else
                createItem(ITEM_MANA_CRYSTAL, user.getPos())
                createItem(ITEM_MANA_CRYSTAL, user.getPos())
