package Seizures
import RegisterEvents
import ClosureTimers
import Orders
import DummyCaster
import ChannelAbilityPreset
import Lodash
import Assets
import ID
import ToolTipsUtils

//@configurable constant int ABILITY_ID = 'A05T' // Seizures channel, Old GUI
//@configurable constant int DUMMY_ABILITY_ID = 'A05U' // Seizures bolt, Old GUI

let REAL_SEIZURES_ID = compiletime(ABIL_ID_GEN.next())

let DAMAGE = 4.
let MANACOST = 20
let COOLDOWN = 30.
let ART_DURATION = 8.

let ANIMATION_NAME = "spell,throw"
let TOOLTIP_NORM = "Seizures"
let TOOLTIP_EXTENDED = ("Manipulates the targets brain sending small electric pulses through his nerves causing him seizures. The seizures do {0} "+
                       "damage per stun and have a random chance of occuring. Seizures stop after {1} seconds. "+
                       "Has {2} seconds cooldown.").format(DAMAGE.toToolTipRed(), ART_DURATION.toToolTipLightBlue(), COOLDOWN.toToolTipLightBlue())

let TARGET_ALLOWED = "air,enemies,ground,neutral,organic,terrain"

// New Seizures bolt
function createSeizuresBolt()
    new AbilityDefinitionMountainKingThunderBolt(REAL_SEIZURES_ID)
    ..setDamage(1, DAMAGE)
    ..setMissileArt("")
    ..setMissileSpeed(5000)
    ..setHeroAbility(false)
    ..setManaCost(1, 0)
    ..setAnimationNames("")
    ..setDurationHero(1, 0.10)
    ..setDurationNormal(1, 0.10)
    ..setLevels(1)
    ..setCooldown(1, 0)
    ..setName("Seizure Bolt")

class Seizures extends ChannelAbilityPreset
    construct(int newAbility, string hotkey, Pair<int , int> buttonPos)
        super(newAbility, 1, true)
        this.setName(TOOLTIP_NORM)
        this.setHotkeyNormal(hotkey)
        this.setCooldown(1, COOLDOWN)
        this.setManaCost(1, MANACOST)
        this.setIconNormal(Icons.bTNPurge)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.setTargetType(1, 1)
        this.setHeroAbility(false)
        this.setArtDuration(1, ART_DURATION)
        this.setCasterAttachmentPoint1("")
        this.setAnimationNames(ANIMATION_NAME)
        this.setArtTarget(Abilities.boltImpact)
        this.setTargetsAllowed(1, TARGET_ALLOWED)

@compiletime function createSeizures()
    new Seizures(SPELL_SEIZURES, "S", new Pair(1, 1))
    createSeizuresBolt()

function startPeriodicAction(player owner, unit target, real timeout, int chance)
    doPeriodicallyTimed(0.1, timeout) cb ->
        if GetRandomInt(1, 100) <= chance
            new DummyCaster()
                ..owner(owner)
                ..origin(target.getPos()+vec2(100, 0))
                ..castTarget(REAL_SEIZURES_ID, 1, Orders.thunderbolt, target)

function onCast()
    var target = GetSpellTargetUnit()
    var owner = GetSpellAbilityUnit().getOwner()

    startPeriodicAction(owner, target, 3, 18)
    doAfter(3, () -> startPeriodicAction(owner, target, 3, 15))
    doAfter(6, () -> startPeriodicAction(owner, target, 2, 8))

init
    registerSpellEffectEvent(SPELL_SEIZURES, () -> onCast())
