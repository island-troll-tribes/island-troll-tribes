package IncreaseMetabolism

import AbilityObjEditing
import ObjectIds
import Assets
import HealingSystem
import ToolTipsUtils
import Lodash
import BuffObjEditing
import ID

let BUFF_ID = 'Bicm'
let ICON = Icons.bTNAncestralSpirit
let CAST_RANGE = 700.
let COOLDOWN = 30.
let MANACOST = 20
let MS_FACTOR = 0.2
let AS_FACTOR = 0.
let DURATION = 20.
let HEALING_MULTIPLIER = 0.50
let CAST_TIME = 0.2
let TARGET_ALLOWED = "air,ground,friend,organic,self,neutral"
let TOOLTIP_NORM = "Increase Metabolism"
let TOOLTIP_EXTENDED = "Increases the health recovered by food and potions by {0} and boosts movement speed by {1}. Lasts {2} seconds, Has {3} seconds cooldown."
                  .format(HEALING_MULTIPLIER.toToolTipGreen(), MS_FACTOR.toToolTipOrange(), DURATION.toToolTipLightBlue(), COOLDOWN.toToolTipLightBlue())

function createBuff()
    new BuffDefinition(BUFF_ID, 'Bblo') // Original ID is BloodLust
    ..setTargetAttachmentPoint0(1, "origin")
    ..setTargetAttachmentPoint1(1, "")
    ..setArtTarget(1, Abilities.faerieDragon_Invis)
    ..setIcon(ICON)
    ..setTooltipNormal(1, TOOLTIP_NORM)
    ..setTooltipNormalExtended(1, "This unit recover more health points when eating meat and run faster.")

class IncreaseMetabolism extends AbilityDefinitionSlowCreep
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setAttackSpeedFactor(1, -AS_FACTOR)
        this.setMovementSpeedFactor(1, -MS_FACTOR)
        this.setBuffs(1, toRawCode(BUFF_ID))
        this.setIconNormal(ICON)
        this.setCastRange(1, CAST_RANGE)
        this.setCastingTime(1, CAST_TIME)
        this.setCooldown(1, COOLDOWN)
        this.setDurationHero(1, DURATION)
        this.setDurationNormal(1, DURATION)
        this.setManaCost(1, MANACOST)
        this.setTargetsAllowed(1, TARGET_ALLOWED)
        this.setName(TOOLTIP_NORM)
        this.setHotkeyNormal(hotkey)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)

@compiletime function createSpells()
    new IncreaseMetabolism(SPELL_INCREASE_METABOLISM, "D", new Pair(3, 1))
    new IncreaseMetabolism(SPELL_SAGE_INCREASE_METABOLISM, "Q", new Pair(0, 0))
    createBuff()

init
    onUnitHealed() ->
        let instance = getHealingInstance()
        if instance.target.hasAbility(BUFF_ID) and instance.healingType == HealingType.ITEM
            instance.scaleMultiplier(HEALING_MULTIPLIER + 1)
