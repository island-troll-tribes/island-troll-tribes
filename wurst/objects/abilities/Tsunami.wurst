package Tsunami

// Standard library imports:
import ClosureEvents

// Local imports:
import LocalObjectIDs

// The range from the caster that the effect is centered.
let OFFSET_DISTANCE = 300.

// The range from the center for which units are affected.
let EFFECT_DISTANCE = 150.

// The extra damage that enemy structures take.
let EXTRA_DAMAGE = 30.

function unit.isFire() returns bool
    return this.getTypeId() == UNIT_FIRE
        or this.getTypeId() == UNIT_MAGE_FIRE_SUMMONED
        or this.getTypeId() == UNIT_MAGE_FIRE

init
    EventListener.onPointCast('A038') (unit caster, vec2 target) ->
        // Compute the center of the extra damage.
        let center = caster.getPos().polarOffset(caster.getPos().angleTo(target), OFFSET_DISTANCE)

        // Enumerate all units that are in region for extra damage.
        ENUM_GROUP.enumUnitsInRange(center, EFFECT_DISTANCE)

        // Perform the extra damage for the matching units.
        for u in ENUM_GROUP
            // Only enemy structures are affected.
            if u.isEnemyOf(caster) and IsUnitType(u, UNIT_TYPE_STRUCTURE)
                // Destroy fires outright.
                if u.isFire()
                    u.kill()
                // Otherwise deal additional damage.
                else
                    caster.damageTarget(u, EXTRA_DAMAGE)
