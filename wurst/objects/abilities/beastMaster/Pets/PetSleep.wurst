package PetSleep

// Standard library imports:
import ClosureEvents
import InstantDummyCaster
import OrderIds
import SimError
import Assets
import ChannelAbilityPreset

// Local imports:
import LocalObjectIDs
import Pets
import ToolTipsUtils

// The ID for the ability used to put a pet to sleep.
public let SLEEP_ID = compiletime(ABIL_ID_GEN.next())
public let HEAL_ID  = compiletime(ABIL_ID_GEN.next())

// The amount the pet is healed for when sleeping.
let HEALING_AMOUNT = 90.

let COOLDOWN = 25.
let SLEEP_DURATION = 10.

let ABIL_TT = "Your pet goes to sleep, recovering {0} health point over {1} seconds.".format(
              HEALING_AMOUNT.toToolTipGreen(), SLEEP_DURATION.toToolTipLightBlue())
              + makeToolTipCooldown(COOLDOWN)


@compiletime function createPetSleepSpell() returns AbilityDefinitionSleepcreep
    return new AbilityDefinitionSleepcreep(SLEEP_ID)
        ..setDummyAbility()
        ..setIconNormal(Icons.bTNElunesBlessing)
        ..setDurationHero(1, SLEEP_DURATION)
        ..setDurationNormal(1, SLEEP_DURATION)
        ..setTargetsAllowed(1, commaList(
            TargetsAllowed.nonhero
            ))
        ..setLevels(1)
        ..setName("Pet Sleep")
        ..setEditorSuffix("(Wurst)")
        ..setStunDuration(1, 1)

@compiletime function createPetSleepHealingAbility() returns AbilityDefinitionPotionofLifeRegen
    return new AbilityDefinitionPotionofLifeRegen(HEAL_ID)
        ..setDummyAbility()
        ..setLifeRegenerated(1, HEALING_AMOUNT)
        ..setDurationHero(1, SLEEP_DURATION)
        ..setDurationNormal(1, SLEEP_DURATION)
        ..setItemAbility(false)
        ..setArtCaster("")
        ..setName("Pet Healing")

function createPetDummySleepSpell(int newAbilId) returns ChannelAbilityPreset
    return new ChannelAbilityPreset(newAbilId, 1, true)
        ..setAnimationNames("")
        ..setIconNormal(Icons.bTNElunesBlessing)
        ..setCooldown(1, 20.0)
        ..setFollowThroughTime(1, 1.0)
        ..setName("Pet Sleep")
        ..setTooltipNormalExtended(1, ABIL_TT)
        ..setRequirements(UPGD_PET_TAMED_TRUE.toRawCode())
        ..setEditorSuffix("(Wurst)")

@compiletime function createBasePetSleepSpell() returns ChannelAbilityPreset
    return createPetDummySleepSpell(ABILITY_PET_SLEEP)
    ..setButtonPositionNormalX(2)
    ..setButtonPositionNormalY(0)
    ..setHotkeyNormal("E")
    ..setTooltipNormal(1, makeToolTipNorm("E", "Sleep"))


function onCast(unit caster)
    // Lookup the pet for the caster.
    let pet = caster.getOwner().getPet()

    // Verify that the player has a pet, which should be guaranteed.
    if pet == null
        simError(caster.getOwner(), "You do not own any pet.")
    // Perform the actions associated with a sleeping pet.
    else
        // Put the pet to sleep.
        InstantDummyCaster.castTarget(
            pet.getOwner(),
            SLEEP_ID,
            1,
            OrderIds.sleep,
            pet
        )

        // Heal the pet.
        // I couldn't find the order Id for healing salve, so I looked it up on
        // https://www.hiveworkshop.com/threads/how-to-make-unit-cast-healing-salve-through-triggers.213477/#post-2118173
        InstantDummyCaster.castTarget(
            pet.getOwner(),
            HEAL_ID,
            1,
            852609,
            pet
        )


init
    EventListener.onCast(ABILITY_PET_SLEEP, (unit caster) -> onCast(caster))
