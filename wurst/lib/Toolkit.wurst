package Toolkit

// Standard library imports:
import LinkedList

// Third-party imports:
import ChatCommands

// The interface used for callbacks.
interface Callback
    function run()

// This is deliberately not tracked per player.
var isEnabled = false

// The events that will execute when the toolkit is enabled.
let callbacks = new LinkedList<Callback>()

public function registerToolkitEvent(Callback callback)
    callbacks.add(callback)

// Registers a command that only functions when the toolkit is enabled.
public function registerToolkitCommand(string command, CommandHandler handler)
    registerCommandAll(command) (triggerPlayer, arguments) ->
        if isEnabled
            handler.run(triggerPlayer, arguments)

// Enables the toolkit irreversibly.
public function enableToolkit()
    // Set the flag to enable all toolkit commands.
    isEnabled = true

    // Fire each registered callback for the event.
    for callback in callbacks
        callback.run()

// Checks whether the toolkit has been enabled.
public function isEnabled() returns boolean
    return isEnabled
