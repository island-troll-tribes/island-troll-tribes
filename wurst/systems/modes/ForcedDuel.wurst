package ForcedDuel

// Standard library imports:
import ClosureTimers

// Local imports:
import ColorUtils
import Game
import GameConfig
import PlayerExtensions
import TimerUtils
import Tribe
import StringExtensions
import UnitExtensions

public timer DUEL_TIMER

function moveTrollsToArena()
    print("Your duel has begun!".color(GENERAL_COLOR))

    let tribes = Tribe.getTribes()

    for tribe in tribes
        let pos = gg_rct_Dualing_Area.randomPoint()

        for member in tribe.getMembers()
            let troll = member.getTroll()
            if troll.isAlive()
                troll
                ..pause()
                ..removeBuffs(true, true)
                ..setPos(pos)
                ..setHP(troll.getMaxHP())
                ..setMana(troll.getMaxMana())
                ..setHeat(gameConfig.getMaxHeat())

                member.panCamToTimed(troll, 0.1)

    doAfter(3.0) ->
        for tribe in tribes
            for member in tribe.getMembers()
                let troll = member.getTroll()
                if troll.isAlive()
                    troll.unpause()

init
    registerGameStartEvent() ->
        if gameConfig.isForcedDuelEnabled()
            let timeout = gameConfig.getForcedDuelAfter() * 60
            DUEL_TIMER = getTimer()..doAfter(timeout, () -> moveTrollsToArena())
