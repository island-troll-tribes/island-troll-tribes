package AntiMagic
import ClosureEvents
import ClosureForGroups
import DummyCaster
import Orders
import ChannelAbilityPreset
import Assets
import ID
import Lodash
import ToolTipsUtils

//let ABILITY_ANTI_MAGIC_SHELL = 'A0F8'
//let ABILITY_ANTI_MAGIC_DUMMY = 'XAMG'
let COOLDOWN = 20.
let MANACOST = 20
let AOE = 800.

let SHIELD_AMOUNT = 100.
let DURATION = 10.
let CAST_RANGE = 800.
let SINGLE_AM_MANACOST = 5
let SINGLE_AM_COOLDOWN = 1.
let SINGLE_AM_TOOLTIP_NORM = "Anti-Magic Shell"
let SINGLE_AM_TOOLTIP_EXTENDED = ("Grants an anti-magic barrier to target ally that absorbs {0} magic damage and blocks them "+
                                  "from being ensnared by hunters or ensnare traps. Lasts {1} seconds, has {2} seconds cooldown.")
                                  .format(SHIELD_AMOUNT.toToolTipGreen(), DURATION.toToolTipLightBlue(), SINGLE_AM_COOLDOWN.toToolTipLightBlue())


let TOOLTIP_NORM = "Anti-Magic Shell"
let TOOLTIP_EXTENDED = ("Grants an anti-magic barrier to allies in a {0} radius that absorbs {1} magic damage and blocks them "+
                       "from being ensnared by hunters or ensnare traps. Lasts {2} seconds, has {3} seconds cooldown.")
                       .format(AOE.toToolTipGray(), SHIELD_AMOUNT.toToolTipGreen(), DURATION.toToolTipLightBlue(), COOLDOWN.toToolTipLightBlue())

class AntiMagic extends AbilityDefinitionAntimagicShield
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setRequirements("")
        this.setCheckDependencies(false)
        this.setShieldLife(1, SHIELD_AMOUNT.toInt())
        this.setManaCost(1, SINGLE_AM_MANACOST)
        this.setCastRange(1, CAST_RANGE)
        this.setDurationHero(1, DURATION)
        this.setDurationNormal(1, DURATION)
        this.setName(SINGLE_AM_TOOLTIP_NORM)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, SINGLE_AM_TOOLTIP_NORM))
        this.setTooltipNormalExtended(1, SINGLE_AM_TOOLTIP_EXTENDED)
        this.setCooldown(1, SINGLE_AM_COOLDOWN)
        this.presetHotkey(hotkey)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)

class AntiMagicAoEDummy extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId, 1, true)
        this.presetCastRange(lvl -> 1)
        // this.presetDurationHero(lvl -> 0.01)
        // this.presetDurationNormal(lvl -> 0.01)
        this.presetManaCost(lvl -> MANACOST)
        this.presetCooldown(lvl -> COOLDOWN)
        this.setHeroAbility(false)
        this.setItemAbility(false)
        this.presetTargetType(lvl -> 0)
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.setBaseOrderID(1, "antimagicshell")
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setIconNormal(Icons.bTNAntiMagicShell)
        this.setIconTurnOff(Icons.bTNAntiMagicShell)
        this.setIconResearch(Icons.bTNAntiMagicShell)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)

@compiletime function createAntiMagic()
    new AntiMagic(SPELL_ANTI_MAGIC, "Q", new Pair(0, 0))
    new AntiMagicAoEDummy(SPELL_ANTI_MAGIC_AOE, "Q", new Pair(0, 0))

function onCast()
    let caster = GetSpellAbilityUnit()
    forUnitsInRange(caster.getPos(), AOE) u ->
        if u.isAlive() and u.getOwner().isAllyOf(caster.getOwner())
            new DummyCaster()
                ..owner(caster.getOwner())
                ..origin(u.getPos() + vec2(100, 0))
                ..delay(1)
                ..castTarget(SPELL_ANTI_MAGIC, 1, Orders.antimagicshell, u)

init
    registerSpellEffectEvent(SPELL_ANTI_MAGIC_AOE) ->
        onCast()
