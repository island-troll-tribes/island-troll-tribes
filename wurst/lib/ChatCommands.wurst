package ChatCommands
import LinkedList
import HashMap
import StringUtils

let MAX_PLAYERS = PLAYER_NEUTRAL_AGGRESSIVE - 1

public interface CommandHandler
    function run(player triggerPlayer, LinkedList<string> arguments)

let commands = new HashMap<player, HashMap<string, CommandHandler>>()

public function registerCommand(player thePlayer, string command, CommandHandler handler)
    commands.get(thePlayer).put(command, handler)

public function registerCommandAll(string command, CommandHandler handler)
    for i = 0 to MAX_PLAYERS
        registerCommand(players[i], command, handler)

public function deregisterCommand(player thePlayer, string command)
    if commands.get(thePlayer).has(command)
        commands.get(thePlayer).remove(command)

public function deregisterCommandAll(string command)
    for i = 0 to MAX_PLAYERS
        deregisterCommand(players[i], command)

init
    let t = CreateTrigger()

    for i = 0 to MAX_PLAYERS
        commands.put(players[i], new HashMap<string, CommandHandler>())
        t.registerPlayerChatEvent(players[i], "-", false)

    t.addAction() ->
        let triggerPlayer = GetTriggerPlayer()
        let playerCommands = commands.get(triggerPlayer)
        let tokens = GetEventPlayerChatString().substring(1).split(" ")
        let command = tokens.get(0)
        if playerCommands.has(command)
            playerCommands.get(command).run(triggerPlayer, tokens)

    registerCommandAll("test-wurst-commands") (triggerPlayer, args) ->
        print("player " + triggerPlayer.getId().toString())
        args.forEach(i -> print(i))
