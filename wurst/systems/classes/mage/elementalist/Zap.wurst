package Zap

import RegisterEvents
import ClosureTimers
import DummyCaster
import Orders
import AbilityObjEditing
import BuffObjEditing
import TimerUtils
import TimerExtensions

@configurable constant int ABILITY_ID = 'XEZS'
@configurable constant int SLOW_ABILITY_ID = 'XEZD'
@configurable constant int BUFF_ZAP1_AURA = 'XEZA'
@configurable constant int BUFF_ZAP2_AURA = 'XEZB'
@configurable constant int BUFF_ZAP1 = 'XEZC'
@configurable constant string BUFF_ZAP1_STR = "XEZC"
@configurable constant int BUFF_ZAP2 = 'XEZD'
@configurable constant string BUFF_ZAP2_STR = "XEZD"
@configurable constant string BUFF_ZAP_SLOW = "EZSB"
@configurable constant int BUFF_ZAP_SLOW_INT = 'EZSB'
@configurable constant real CAST_RANGE = 800
@configurable constant real COOLDOWN = 5
@configurable constant int MANACOST = 5
@configurable constant real MAIN_DAMAGE = 15
@configurable constant real CHARGE_DAMAGE = 35
@configurable constant real CHARGE_STACK_DURATION = 30
@configurable constant real CHARGE_REMOVE_TIME = 15
@configurable constant real CHARGE_AS_SLOW = 0.7
@configurable constant real CHARGE_MS_SLOW = 0.7
@configurable constant real CHARGE_SLOW_DURATION_HERO = 2.5
@configurable constant real CHARGE_SLOW_DURATION_NORM = 3.5
@configurable constant string EFFECT_PATH = "Abilities\\Spells\\Orc\\Purge\\PurgeBuffTarget.mdl" 
@configurable constant string TOOLTIP_NORMAL = "[|c00ffcc00Q|r] Zap"
@configurable constant string TOOLTIP_NORMAL_EXT = "Zaps the target for |c00ff000015|r damage. Stacks charges on the target and |c0000ffffexplodes slowing movement and attack speed|r|c00dbf825 |rand|c00dbf825 |rdealing |c00ff000035|r bonus damage on the third charge."


@compiletime function createBuffs()
    new BuffDefinition(BUFF_ZAP_SLOW_INT, 'Bstt')
    ..setArtTarget(1, "")
    ..setTooltipNormal(1, "Zap Slow")
    ..setTooltipNormalExtended(1, "Slowed by Zap spell")
    
    new BuffDefinition(BUFF_ZAP1, 'Bstt')
    ..setArtTarget(1, "")
    ..setTooltipNormal(1, "Zap 1")
    ..setTooltipNormalExtended(1, "1 Zap Charge")

    new BuffDefinition(BUFF_ZAP2, 'Bstt')
    ..setArtTarget(1, "")
    ..setTooltipNormal(1, "Zap 2")
    ..setTooltipNormalExtended(1, "2 Zap Charges")

@compiletime function createDummyAuras()
    new AbilityDefinitionAuraSlow(BUFF_ZAP1_AURA)
    ..setArtTarget("Abilities\\Weapons\\FarseerMissile\\FarseerMissile.mdl")
    ..setTargetAttachmentPoint("chest")
    ..setMovementSpeedFactor(1, 0)
    ..setAreaofEffect(1, 0)
    ..setBuffs(1, BUFF_ZAP1_STR)
    ..setDurationHero(1, 15)
    ..setDurationNormal(1, 15)
    ..setTargetsAllowed(1, "self")
    ..setName("Zap 1")

    new AbilityDefinitionAuraSlow(BUFF_ZAP2_AURA)
    ..setArtTarget("Abilities\\Weapons\\FarseerMissile\\FarseerMissile.mdl")
    ..setTargetAttachmentPoint("chest")
    ..setMovementSpeedFactor(1, 0)
    ..setAreaofEffect(1, 0)
    ..setBuffs(1, BUFF_ZAP2_STR)
    ..setDurationHero(1, 15)
    ..setDurationNormal(1, 15)
    ..setTargetsAllowed(1, "self")
    ..setName("Zap 2")

@compiletime function createSpell()
    new AbilityDefinitionChainLightningcreep('XEZS')
    ..setButtonPositionNormalX(0)
    ..setButtonPositionNormalY(0)
    ..setDamageReductionperTarget(1, 0)
    ..setDamageperTarget(1, MAIN_DAMAGE)
    ..setNumberofTargetsHit(1, 1)
    ..setAreaofEffect(1, 1)
    ..setCastRange(1, CAST_RANGE)
    ..setCooldown(1, COOLDOWN)
    ..setManaCost(1, MANACOST)
    ..setHeroAbility(false)
    ..setItemAbility(false)
    ..setLevels(1)
    ..setHotkeyNormal("Q")
    ..setName("Zap")
    ..setOrderStringUseTurnOn("forkedlightning")
    ..setOrderStringActivate("forkedlightning")
    ..setOrderStringDeactivate("forkedlightning")
    ..setOrderStringTurnOff("forkedlightning")
    ..setTooltipNormal(1, TOOLTIP_NORMAL)
    ..setTooltipNormalExtended(1, TOOLTIP_NORMAL_EXT)

@compiletime function createSlowDummySpell()
    new AbilityDefinitionSlowCreep('XEZD')
    ..setAttackSpeedFactor(1, CHARGE_AS_SLOW)
    ..setMovementSpeedFactor(1, CHARGE_MS_SLOW)
    ..setBuffs(1, BUFF_ZAP_SLOW)
    ..setCastRange(1, 1500)
    ..setCooldown(1, 0)
    ..setDurationHero(1, CHARGE_SLOW_DURATION_HERO)
    ..setDurationNormal(1, CHARGE_SLOW_DURATION_NORM)
    ..setManaCost(1, 0)
    ..setTargetsAllowed(1, "air,allies,enemies,ground")
    ..setName("Zap Dummy Slow")


function onCast()
    var caster = GetSpellAbilityUnit()
    var owner = caster.getOwner()
    var target = GetSpellTargetUnit()
    timer t = null
    
    //Small delay so the effect doesnt hit before animation
    doAfter(0.15) ->
        if target.hasAbility( BUFF_ZAP1_AURA )
            //Upgrade zap 1 charge to zap 2
            target.removeAbility(BUFF_ZAP1_AURA)
            target.addAbility(BUFF_ZAP2_AURA)

            //Remove existing charge removal timers and start a new one
            if (t != null)
                t.release()
            t = getTimer()
            t.doAfter(CHARGE_REMOVE_TIME, () -> begin
                target.removeAbility(BUFF_ZAP2_AURA)
            end)
        else if target.hasAbility(BUFF_ZAP2_AURA)
            //Remove existing charge removal timers
            if (t != null)
                t.release()
            
            //Remove charges and do effects
            target.removeAbility(BUFF_ZAP1_AURA)
            target.removeAbility(BUFF_ZAP2_AURA)
            caster.damageTarget(target, CHARGE_DAMAGE, ATTACK_TYPE_NORMAL)
            new DummyCaster().castTarget(owner, SLOW_ABILITY_ID, 1, Orders.slow, target)
            var efx = AddSpecialEffectTargetUnitBJ("chest", target, EFFECT_PATH)
            efx.setScale(1)
            doAfter(3)->
                efx.destr()
        else 
            target.addAbility(BUFF_ZAP1_AURA)

            //Remove existing charge removal timers and start a new one
            if (t != null)
                t.release()
            t = getTimer()
            t.doAfter(CHARGE_REMOVE_TIME, () -> begin
                target.removeAbility(BUFF_ZAP1_AURA)
            end)

init
    registerSpellEffectEvent(ABILITY_ID, () -> onCast())
