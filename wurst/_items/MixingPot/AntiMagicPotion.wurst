package AntiMagicPotion

import PotionsDefinition
import ID
import AbilityObjEditing
import ObjectIds
import ToolTipsUtils

constant ABIL  = compiletime(ABIL_ID_GEN.next())
//constant ABIL  = "A0GQ"
constant ICON  = ICON_PATH.format("PotionOfClarity")
constant MODEL = "Models\\Items\\PotionAntiMagic.mdl"

constant SHIELD_LIFE = 100
constant CAST_RANGE = 800.
constant DURATION = 25.
constant COOLDOWN = 1.
constant TT = "Cast an Anti-Magic shield on target, blocking {0} magic damage.".format(SHIELD_LIFE.toReal().toToolTipGreen()) + makeToolTipDuration(DURATION, COOLDOWN)

@compiletime function createAbil()
    new AbilityDefinitionAntimagicShield(ABIL)
    ..setName("Anti-Magic Potion")
    ..setCheckDependencies(false)
    ..setShieldLife(1, SHIELD_LIFE)
    ..setHeroAbility(false)
    ..setItemAbility(true)
    ..setManaCost(1, 0)
    ..setCastRange(1, CAST_RANGE)
    ..setDurationHero(1, DURATION)
    ..setDurationNormal(1, DURATION)
    ..setCooldown(1, COOLDOWN)
    ..setEditorSuffix("(Wurst)")

@compiletime function createAntiMagicPotion()
    new PotionDefinition(ITEM_ANTI_MAGIC_POTION, ICON, MODEL, "Anti-Magic Potion", "A", TT, 6, commaList(ABIL), 0.70, commaList(ABIL), 120)..setBtnPos(0, 1)