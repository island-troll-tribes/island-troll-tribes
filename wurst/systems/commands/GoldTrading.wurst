package GoldTrading
import LinkedList
import ChatCommands
import SimError
import PlayerExtensions
import PlayerUtils
import Game
import LegacyColors
import GameConfig

CommandHandler giveGold = (player giver, LinkedList<string> args) -> begin
    let tribeId = giver.getTribe().getId()
    let playerId = findPlayerIdByStringMatchingTribe(args.get(1), tribeId)
    let currentGold = giver.getLumber()
    let target = players[playerId]
    var amount = args.size() > 2 ? args.get(2).toInt() : currentGold

    if playerId < 0 or playerId > 11
        simError(giver, "Command: -g player amount")
    else if amount > currentGold
        simError(giver, "Not enough gold")
    else if playerId == giver.getId()
        simError(giver, "You cannot trade with yourself")
    else if target.getTribe().getId() != tribeId
        simError(giver, "You cannot trade with that player")
    else
        giver.subLumber(amount)
        target.addLumber(amount)

        let targetMessage = "{0}You received|r {1}{2} gold|r{3} from |r{4}".format(GENERAL_COLOR, amount.toString(), GOLD_COLOR, GENERAL_COLOR, giver.getNameColored())
        printTimedToPlayer(targetMessage, 10, target)

        let giverMessage = "{0}Sent|r {1}{2} gold|r{3} to |r{4}".format(GENERAL_COLOR, GOLD_COLOR, amount.toString(), GENERAL_COLOR, target.getNameColored())
        printTimedToPlayer(giverMessage, 10, giver)
end

init
    registerGameStartEvent() () ->
        if not gameConfig.isTeamGoldEnabled()
            registerCommandAll("g", giveGold)
            registerCommandAll("gold", giveGold)
