package ElkJump

// Standard lib Imports:
import ChannelAbilityPreset
import ClosureEvents
import Assets
import UpgradeObjEditing

// Local Imports:
import ToolTipsUtils
import LocalObjectIDs
import JumpSystem

@configurable let JUMP_MAX_RANGE = 550.

let COOLDOWN = 60.

let TOOLTIP_NORM = "Reindeer Jump"
let TOOLTIP_EXTENDED = "" +
    "Rendo uses the Reindeer potent calves to jump a short distance." +
    makeToolTipCooldown(COOLDOWN)


@compiletime function createElkJumpUpgrade()
    new UpgradeDefinition(UPGD_RENDO_ELK_JUMP)
        ..setName(1, "Devoured Reindeer")
        ..setLevels(1)
        ..setIcon(1, Icons.bTNNagaUnBurrow)
        ..addEffectAbilityLevelBonus(0, 1, ABILITY_RENDO_ELK_JUMP.toRawCode())

@compiletime function createRendoJump() returns ChannelAbilityPreset
    return new ChannelAbilityPreset(ABILITY_RENDO_ELK_JUMP, 1, true)
        ..presetManaCost(lvl -> 20)
        ..presetCooldown(lvl -> COOLDOWN)
        ..setHeroAbility(false)
        ..setItemAbility(false)
        ..presetTargetTypes(Targettype.POINT)
        ..setHotkeyNormal("R")
        ..setName(TOOLTIP_NORM)
        ..presetTooltipNormal(lvl -> makeToolTipNorm("R", TOOLTIP_NORM))
        ..presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        ..setIconNormal(Icons.bTNNagaUnBurrow)
        ..setIconResearch(Icons.bTNNagaUnBurrow)
        ..setIconTurnOff(Icons.bTNNagaUnBurrow)
        ..setButtonPositionNormalX(0)
        ..setButtonPositionNormalY(0)
        ..setFollowThroughTime(1, 0)
        ..setCheckDependencies(true)
        ..presetCastRange(lvl -> 99999.)
        ..setRequirements(UPGD_RENDO_ELK_JUMP.toRawCode())
        ..setBaseOrderID(1, "carrionswarm")

init
    EventListener.onPointCast(ABILITY_RENDO_ELK_JUMP) (unit caster, vec2 targetPos) ->
        let jumpPos = getJumpPos(caster.getPos(), targetPos, JUMP_MAX_RANGE)
        performJump(caster, jumpPos, 3, 5, 0.8)

    EventListener.add(EVENT_PLAYER_UNIT_SPELL_CAST) ->
        if GetSpellAbilityId() == ABILITY_RENDO_ELK_JUMP
            onStartCast(JUMP_MAX_RANGE)
