package CameraSettings

// Standard library imports:
import ClosureTimers

// Third-party imports:
import Setting

let zooms = new Setting("zoom")

public function player.setZoom(real zoom)
    // Update the setting.
    zooms.put(this, zoom.toString())

public function player.getZoom() returns real
    return zooms.get(this).toReal()

function updateZoom(real zoom)
    CAMERA_FIELD_ZOFFSET.set(zoom, 1.)
    CAMERA_FIELD_FARZ.set(5000., 0.)

init
    // Use the default zoom for the engine and allow any input.
    zooms.make("0", input -> true)

    // Update the zoom immediately after setting it.
    zooms.action() (target, zoom) ->
        if localPlayer == target
            updateZoom(zoom.toReal())

    // Update the zoom periodically, as it can be accidentally reset via scroll.
    zooms.finish() ->
        doPeriodically(1.) (CallbackPeriodic cb) ->
            updateZoom(zooms.get(localPlayer).toReal())

    // Initialize the setting immediately.
    zooms.initialize()
