package NewSmokeStream

import ChannelAbilityPreset
import LocalObjectIDs
import Lodash
import Assets
import ToolTipsUtils
import BuffObjEditing
import ClosureTimers
import RegisterEvents
import OrderIds
import DummyCaster
import MagicMist

let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let BUFF_ORIGINAL_ID = 'Bclf' // Cloud

let BATTLE_MIST_ID = compiletime(ABIL_ID_GEN.next())

let AOE = 460.
let MS_FACTOR = 0.6

let COOLDOWN = 20.
let MANACOST = 10
let DURATION = 3.
let SMOKE_SPAWNING_DURATION = 6.
let TOOLTIP_NORM = "Smoke Stream"
let TOOLTIP_EXTENDED = "The Magic Thief releases a stream of smoke bocking all combat and spells. "+
                       "Smoke is only released while you are moving."+makeToolTipDuration(DURATION+SMOKE_SPAWNING_DURATION, COOLDOWN)

function createBuff()
    new BuffDefinition(BUFF_ID, BUFF_ORIGINAL_ID)
        ..setIcon(Icons.bTNCloudOfFog)
        ..setTooltipNormal(1, "Smoke Bomb")
        ..setTooltipNormalExtended(1, "This unit can't see because of the smoke of a smoke bomb. It cannot attack anything and moves slower.")

class SmokeStream extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId, 1, true)
        this.presetManaCost(lvl -> MANACOST)
        this.presetCooldown(lvl -> COOLDOWN)
        this.setHeroAbility(false)
        this.setItemAbility(false)
        this.presetTargetTypes(Targettype.NONE)
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setIconNormal(Icons.bTNCloudOfFog)
        this.setIconResearch(Icons.bTNCloudOfFog)
        this.setIconTurnOff(Icons.bTNCloudOfFog)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setArtCaster(Abilities.obsidianRegenAura)
        this.setEffects(1, Abilities.obsidianRegenAura)
        this.setBaseOrderID(1, "unsubmerge")


@compiletime function createSmokeStream()
    new SmokeStream(ABILITY_SMOKE_STREAM, "R", new Pair(3, 0))
    new SmokeStream(ABILITY_AS_SMOKE_STREAM, "A", new Pair(0, 1))
    createBuff()

function onCast()
    let caster = GetSpellAbilityUnit()

    doPeriodicallyTimed(0.50, SMOKE_SPAWNING_DURATION) cb ->
        if caster.getCurrentOrder() != 0 // Checking if unit is moving
            let fx = addEffect(Abilities.cloudOfFog, caster.getPos())
            doAfter(DURATION) () ->
                fx.destr()
            new DummyCaster()
                ..origin(caster.getPos() + vec2(100,0))
                ..castTarget(TRUE_MAGIC_MIST_ID, 1, Orders.silence, caster)

init
    registerSpellEffectEvent(ABILITY_SMOKE_STREAM, () -> onCast())
    registerSpellEffectEvent(ABILITY_AS_SMOKE_STREAM, () -> onCast())
