package Dysentery

import ID
import Lodash
import Assets
import ToolTipsUtils
import BuffObjEditing
import ClosureTimers
import RegisterEvents
import ObjectIds
import AbilityObjEditing
import ObjectIdGenerator

//let BUFF_ID = 'B000'
let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let BUFF_ORIGINAL_ID = 'BNab'
let CAST_RANGE = 500.
let COOLDOWN = 40.
let MANACOST = 0
let DURATION = 45.

let ICON = "ReplaceableTextures\\CommandButtons\\BTNTracking.blp"

let TOOLTIP_NORM = "Dysentery Track AKA \"Charlie Brown"
let TOOLTIP_EXTENDED = "Throws a bottle filled with dirty stuff, causing the target to get dysentery-like symptoms and poop hot, steaming diarrhea uncontrollably. "+
                        "Tracks a lot longer than normal track and is good for tracking long range."+makeToolTipDuration(DURATION, COOLDOWN)
let TARGET_ALLOWED = "organic,enemies,ground"

function createBuff()
    new BuffDefinition(BUFF_ID, BUFF_ORIGINAL_ID)
        ..setIcon(ICON)
        ..setArtTarget(1,"")
        ..setTooltipNormal(1, TOOLTIP_NORM)
        ..setTooltipNormalExtended(1, "This unit is leaving a trail of feces")
        ..setTargetAttachmentPoint0(1, "")
        ..setArtSpecial(1, "")

class Dysentery extends AbilityDefinitionParasite
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setIconNormal(ICON)
        this.setIconResearch(ICON)
        this.setManaCost(1, MANACOST)
        this.setCooldown(1, COOLDOWN)
        this.setDurationHero(1, DURATION)
        this.setDurationNormal(1, DURATION)
        this.setBuffs(1, toRawCode(BUFF_ID))
        this.setDamageperSecond(1, 0)
        this.setSummonedUnitCount(1, 0)
        this.setMissileArc(0.35)
        this.setMissileArt(Abilities.brewmasterMissile1)
        this.setTargetsAllowed(1, TARGET_ALLOWED)
        this.setCastRange(1, CAST_RANGE)


@compiletime function createDysentery()
    new Dysentery(SPELL_DYSENTERY, "Q", new Pair(0, 0))
    createBuff()

function onCast()
    let caster = GetSpellAbilityUnit()
    let target = GetSpellTargetUnit()

    doPeriodicallyTimed(0.5, DURATION) cb ->
        if target.isAlive() and target.hasAbility(BUFF_ID)
            // Well since it's about diarrhea and hot poop....
            let poopPrint = createUnit(caster.getOwner(), UNIT_LOCUST_POOP, target.getPos(), target.getFacingAngle())
            poopPrint.setTimedLife(30)


init
    registerSpellEffectEvent(SPELL_DYSENTERY, () -> onCast())
