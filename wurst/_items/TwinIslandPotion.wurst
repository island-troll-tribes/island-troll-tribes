package TwinIslandPotion
import ClosureEvents
import AbilityObjEditing
import DummyCaster
import Orders
import ClosureTimers
import BuffObjEditing

@compiletime function createBuff()
    new BuffDefinition('XTWB', 'Bult')
    ..setName(1, "Twin Islands")
    ..setIcon("ReplaceableTextures\\CommandButtons\\BTNPotionGreenSmall.blp")
    ..setIconNormal(1, "ReplaceableTextures\\CommandButtons\\BTNPotionGreenSmall.blp")
    ..setTooltipNormal(1, "Revealed")
    ..setTooltipNormalExtended(1, "Revealed for 30 seconds.")

@compiletime function createVisionSpell()
    new AbilityDefinitionFaerieFire('XTWN')
    ..setDurationHero(1, 30)
    ..setDurationNormal(1, 30)
    ..setDefenseReduction(1, 0)
    ..setHeroAbility(false)
    ..setItemAbility(false)
    ..setCastRange(1, 90000)
    ..setCastingTime(1, 0)
    ..setArtEffect("")
    ..setArtCaster("")
    ..setArtTarget("")
    ..setCooldown(1, 0)
    ..setManaCost(1, 0)
    ..setLevels(1)
    ..setBuffs(1, "XTWB")

function onCast()
    var caster = GetSpellAbilityUnit()
    var owner = caster.getOwner()

    for u in udg_trolls
        if u.getOwner().isEnemyOf(owner) and u.isAlive()
            PingMinimapForForceEx(GetPlayersAllies(owner), u.getPos().x, u.getPos().y, 8, bj_MINIMAPPINGSTYLE_ATTACK, 255, 0, 0)
            
            //Temporary vision required by dummy
            var vis = createVision(owner, u.getPos(), 300, true)
            vis.start()
            doAfter(0.5) ->
                vis.destr()
            new DummyCaster().castTarget(owner, 'XTWN', 1, Orders.faeriefire, u)


init
    registerSpellEffectEvent('A03F', () -> onCast())