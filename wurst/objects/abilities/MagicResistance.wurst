package MagicResistance


// Standard library imports:
import AbilityObjEditing
import BuffObjEditing
import Lodash
import LinkedList

// Local imports:
import LocalObjectIDs
import LocalAssets
import ColorUtils

let MAX_LEVEL = 10
let MIN_LEVEL = 1
// Magic resist per lvl.
let RESIST_PER_LVL = .1

// List of the buffs for magic resistance ability.
let MAGIC_RESIST_BUFFS = compiletime(map((lvl, _) -> createDummyBuff(lvl), range(MIN_LEVEL - 1, MAX_LEVEL + 1).toList()))

// Creates the buffs.
function createDummyBuff(int lvl) returns int
    return createDummyBuffObject("{0} Magic Resist".format(lvl.toString()).color(MAGIC_RESIST_COLOR),
                                 "This unit is resisting {0} of magic damage.".format(((lvl * RESIST_PER_LVL * 100).round().toString() + "%").color(MAGIC_RESIST_COLOR)),
                                 LocalIcons.bTNResistMagic).abilId

@compiletime function createMagicResistAbility() returns AbilityDefinitionRunedBracers
    return new AbilityDefinitionRunedBracers(ABILITY_MAGIC_RESIST)
        ..setLevels(MAX_LEVEL)
        ..presetDamageReduction(lvl -> RESIST_PER_LVL * lvl)

// Used to get a unit's current level of magic resistance.
public function unit.getMagicResist() returns int
    return this.getAbilityLevel(ABILITY_MAGIC_RESIST)

// Used to set the level of magic resistance for a unit.
public function unit.setMagicResist(int lvl)
    // Verify the lvl is in allowed range.
    if lvl > MAX_LEVEL or lvl < MIN_LEVEL
        return

    // If the unit already has magic resistance.
    if this.hasAbility(ABILITY_MAGIC_RESIST)
        // Remove the corresponding buff.
        this.removeAbility(MAGIC_RESIST_BUFFS.get(this.getMagicResist()))
        // Remove the resist ability.
        this.removeAbility(ABILITY_MAGIC_RESIST)
    // Add the resist ability.
    this.addAbility(ABILITY_MAGIC_RESIST)
    // Set the ability to the desired level.
    this.setAbilityLevel(ABILITY_MAGIC_RESIST, lvl)
    // Add the buff corresponding to the desired level.
    this.addAbility(MAGIC_RESIST_BUFFS.get(lvl))
