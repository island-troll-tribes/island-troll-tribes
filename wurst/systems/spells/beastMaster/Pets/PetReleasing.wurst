package PetReleasing

// Standard library imports:
import ClosureEvents

// Local imports:
import Classes
import ID
import PetAbilities
import Pets
import Transformation

public function releasePet(unit caster)
    // Look up the pet.
    let pet = caster.getOwner().getPet()

    // Deregister the pet.
    caster.getOwner().removePet()

    // Revert the available abilities.
    setPetAbilities(caster.getOwner(), false)

    // Compute the new owner.
    let owner = pet.getPetGrowthChain().indexOf(pet.getTypeId()) == 0
        ? PLAYER_NEUTRAL_PASSIVE
        : PLAYER_NEUTRAL_AGGRESSIVE

    // Transfer the ownership of the pet.
    pet.setOwner(Player(owner), true)

    // Notify the owning tribe.
    if localPlayer.isAllyOf(caster.getOwner())
        print("A pet was released!")

// Releases the pet upon choosing a sub-class.
function handleTransformation(unit origin, int targetID)
    // Exit if the unit is not originally a base class.
    if getTrollClassType(origin) != ClassType.BASE_CLASS
        return

    // Exit if the unit is not becoming a sub-class.
    if getTrollClassType(targetID) != ClassType.SUB_CLASS
        return

    // Exit if the unit does not currently own a pet.
    if origin.getOwner().getPet() == null
        return

    // Release the pet if all conditions are met.
    releasePet(origin)

init
    EventListener.onCast(SPELL_PET_RELEASE) (unit caster) ->
        releasePet(caster)

    // Release pets if the new class does not have them.
    registerPriorEffect() (unit target, int unitID) ->
        handleTransformation(target, unitID)
