package DefenderOrb
import RegisterEvents
import Orders
import ClosureTimers
import ClosureForGroups
import EventHelper
import LocalObjectIDs
import DummyCaster

@configurable constant int ABILITY_ID = 'A04P'
@configurable constant int DUMMY_ABILITY_ID = 'A04O'
@configurable constant int DUMMY_ID = UNIT_LOCUST_ALT
@configurable constant int BUFF_ID = 'BPSE'
@configurable constant real INTERVAL = 0.05
@configurable constant real DURATION = 10
@configurable constant real SPEED = 400
@configurable constant real DISTANCE = 300
@configurable constant real RADIUS = 200

constant INTERVAL_SPEED = INTERVAL * SPEED

function onCast()
    var owner = GetSpellAbilityUnit().getOwner()
    var pos = EventData.getSpellTargetPos()
    var angle = angle(0)
    var dummy = createUnit(owner, DUMMY_ID, pos.polarOffset(angle, DISTANCE), angle)

    doPeriodicallyTimed(INTERVAL, DURATION) cb ->
        angle += (INTERVAL_SPEED).fromDeg()
        var dpos = pos.polarOffset(angle, DISTANCE)
        dummy.setXY(dpos)

        forUnitsInRange(dummy.getPos(), RADIUS) u ->
            if u.getOwner().isEnemyOf(owner) and not u.hasAbility(BUFF_ID)
                new DummyCaster()
                    ..owner(owner)
                    ..origin(dpos)
                    ..castTarget(DUMMY_ABILITY_ID, 1, Orders.thunderbolt, u)
        if cb.isLast()
            dummy.kill()

init
    registerSpellEffectEvent(ABILITY_ID, () -> onCast())
