package MagicMist

import AbilityObjEditing
import Assets
import ObjectIdGenerator
import LocalObjectIDs
import Lodash
import ObjectIds
import ToolTipsUtils
import BuffObjEditing
import ClosureEvents
import ClosureTimers
import DummyCaster
import OrderIds

// I should rework cloudcast.jurst but too lazy right now

public constant TRUE_MAGIC_MIST_ID = compiletime(ABIL_ID_GEN.next())
constant BUFF_ID = compiletime(BUFF_ID_GEN.next())
constant CAST_RANGE = 400.
constant COOLDOWN = 35.
constant MANACOST = 20
constant DURATION = 10.
constant TOOLTIP_NORM = "Magic Mist"
constant TOOLTIP_EXTENDED = "The Priest creates an artificial fog preventing all combat within it." + makeToolTipDuration(DURATION, COOLDOWN)

constant TARGET_ALLOWED = "friendly,enemy,air,debris,ground,structure"
constant AOE = 400.


function createBuff()
    new BuffDefinition(BUFF_ID, 'Bclf') // CloudFog
    ..setTooltipNormal(1, "Magic Mist")
    ..setTooltipNormalExtended(1, "This unit is cought in magic mist, it can't attack anything")

class MagicMistDummy extends AbilityDefinitionSilenceCreep
    construct(int newAbilityId)
        super(newAbilityId)
        this.setArtEffect("")
        this.setDummyAbility()
        this.setAreaofEffect(1, AOE)
        this.setBuffs(1, BUFF_ID.toRawCode())
        this.setDurationHero(1, 1)
        this.setDurationNormal(1, 1)
        this.setAttacksPrevented(1, 3)
//        this.setTargetsAllowed(1, TARGET_ALLOWED)

class MagicMist extends AbilityDefinitionBloodMageFlameStrike
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setManaCost(1, MANACOST)
        this.setCastRange(1, CAST_RANGE)
        this.setCooldown(1, COOLDOWN)
        this.setName(TOOLTIP_NORM)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.presetHotkey(hotkey)
        this.setHalfDamageDealt(1, 1)
        this.setFullDamageDealt(1, 1)
        this.setMaximumDamage(1, 1)
        this.setBuildingReduction(1, 1)
        this.setArtEffect("")
        this.setIconNormal(Icons.bTNCloudOfFog)
        this.setArtSpecial("")
        this.setTargetsAllowed(1, "debris")
        this.setHeroAbility(false)
        this.setDurationHero(1, 0.01)
        this.setDurationNormal(1, 0.01)
        this.setEffects(1, "")
        this.setAreaofEffect(1, AOE)
        this.setBuffs(1, toRawCode(BUFF_ID))


@compiletime function createRangedHeal()
    new MagicMist(ABILITY_MAGIC_MIST, "A", new Pair(0, 0))
    new MagicMistDummy(TRUE_MAGIC_MIST_ID)
    createBuff()


init
    EventListener.onPointCast(ABILITY_MAGIC_MIST) (unit caster, vec2 target) ->
        let fx = addEffect(Abilities.cloudOfFog, target)..setScale(2)
        doAfter(DURATION) () ->
            fx.destr()
        doPeriodicallyTimed(1, DURATION) (CallbackCounted cb) ->
            new DummyCaster()
                ..origin(caster.getPos() + vec2(100,0))
                ..castPoint(TRUE_MAGIC_MIST_ID, 1, OrderIds.silence, target)
