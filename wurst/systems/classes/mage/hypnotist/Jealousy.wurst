package Jealousy
import RegisterEvents
import LinkedList
import Orders
import Table
import ClosureTimers
import ClosureForGroups

@configurable constant int ABILITY_ID = 'A051'
@configurable constant real INTERVAL = 0.05
@configurable constant real CURSE_RADIUS = 600
@configurable constant real CURSE_DURATION = 7

constant targetTable = new Table()

function onCast()
    var target = GetSpellTargetUnit()
    var owner = target.getOwner()
    var pos = target.getPos()
    var bewitched = new LinkedList<int>()

    doPeriodicallyTimed(INTERVAL, CURSE_DURATION) cb ->
        bewitched.forEach(idx -> targetTable.removeHandle(idx))

        if not target.isAlive()
            cb.stop()
            destroy bewitched
        else if cb.isLast()
            destroy bewitched
        else
            bewitched.clear()
            pos = target.getPos()
            forUnitsInRange(pos, CURSE_RADIUS) u ->
                var index = u.getHandleId()
                if u != target and u.isAlive() and u.getOwner().isAllyOf(owner) and not targetTable.hasHandle(index)
                    bewitched.push(index)
                    targetTable.saveUnit(index, target)
                    if target.isInvulnerable() or u.isInvulnerable()
                        //u.issuePointOrderById(Oders.move, target.getPos())
                        //Move to target location or something, the above code crashes game
                    else
                        u.issueTargetOrderById(Orders.attack, target)
                        
function onAnyOrder()
    var u = GetTriggerUnit()
    var index = u.getHandleId()

    if targetTable.hasHandle(index)
        var target = targetTable.loadUnit(index)
        if target.isAlive()
            let t = getPlayerUnitEventTrigger(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER)
            t.disable()
            if target.isInvulnerable() or u.isInvulnerable()
                //u.issuePointOrderById(Oders.move, target.getPos())
                //Move to target location or something, the above code crashes game
            else
                u.issueTargetOrderById(Orders.attack, target)
            t.enable()

init
    registerSpellEffectEvent(ABILITY_ID, () -> onCast())
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, () -> onAnyOrder())
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER, () -> onAnyOrder())
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, () -> onAnyOrder())
