package StormEarthFire
import ClosureForGroups
import Orders
import ClosureEvents
import DummyCaster

@configurable constant int ABILITY_ID = 'A02G'
@configurable constant int STORM_ABILITY_ID = 'ACfl'
@configurable constant int EARTH_ABILITY_ID = 'A017'
@configurable constant int FIRE_ABILITY_ID = 'ACtb'
@configurable constant int FAN_ABILITY_ID = 'A01T'
@configurable constant real RADIUS = 512

function onCast()
    var caster = GetSpellAbilityUnit()
    var owner = caster.getOwner()
    var pos = caster.getPos()

    new DummyCaster()
        ..owner(owner)
        ..origin(pos)
        ..castImmediate(FAN_ABILITY_ID, 1, Orders.fanofknives)

    forUnitsInRange(pos, RADIUS) u ->
        if u.isAlive() and u.isType(UNIT_TYPE_HERO) and u.getOwner().isEnemyOf(owner)
            new DummyCaster()
                ..owner(owner)
                ..origin(pos)
                ..castTarget(EARTH_ABILITY_ID, 1, Orders.entanglingroots, u)
            new DummyCaster()
                ..owner(owner)
                ..origin(pos)
                ..castTarget(FIRE_ABILITY_ID, 1, Orders.creepthunderbolt, u)

init
    registerSpellEffectEvent(ABILITY_ID, () -> onCast())
