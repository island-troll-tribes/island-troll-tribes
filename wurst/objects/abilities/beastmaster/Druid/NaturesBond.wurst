package NaturesBond

// Standard Library Imports:
import Icons
import ChannelAbilityPreset
import BuffObjEditing
import BuffIds
import Abilities

// Local Imports:
import LocalObjectIDs
import ToolTipsUtils
import HealingSystem
import Pets


let COOLDOWN = 60.
let MANA_COST = 20
let HEAL_DIST = .5
let DURATION = 15.
let CAST_RANGE = 99999.
let HOTKEY = "D"
let NAME = "Nature's Bond"
let ICON = Icons.bTNSpiritLink
let TOOLTIP_EXT   = (""+
    "The strong bond between the Druid and his pet causes them to split all "+
    "damage between themselves. They also share {0} of any healing with eachother."
    ).format((HEAL_DIST).toToolTipGreen())
    + makeToolTipDuration(DURATION, COOLDOWN)
let BUFF_TOOLTIP = "This unit is sharing damage and healing with another."
let BUFF_ID = compiletime(BUFF_ID_GEN.next())

@compiletime function createNaturesBond() returns BuffDefinition
    return new BuffDefinition(BUFF_ID, BuffIds.spiritLink)
        ..setIcon(ICON)
        ..setName(1, NAME)
        ..setArtTarget(1, Abilities.spiritLinkZapTarget)
        ..setTargetAttachmentPoint0(1, "chest")
        ..setTooltipNormal(1, NAME)
        ..setTooltipNormalExtended(1, BUFF_TOOLTIP)
        ..setEditorSuffix(1, "(Wurst)")

@compiletime function createSpiritLinkAbility() returns AbilityDefinitionSpiritLink
    return new AbilityDefinitionSpiritLink(ABILITY_NATURES_BOND)
        ..setName(NAME)
        ..presetManaCost(_ -> MANA_COST)
        ..presetHotkey(HOTKEY)
        ..presetCooldown(_ -> COOLDOWN)
        ..presetCastRange(_ -> CAST_RANGE)
        ..presetAreaofEffect(_ -> CAST_RANGE)
        ..presetTooltipNormal(_ -> makeToolTipNorm(HOTKEY, NAME))
        ..presetTooltipNormalExtended(_ -> TOOLTIP_EXT)
        ..setRequirements("")
        ..setCheckDependencies(false)
        ..presetDurationHero(_ -> DURATION)
        ..presetDurationNormal(_ -> DURATION)
        ..setBuffs(1, BUFF_ID.toRawCode())
        ..presetMaximumNumberofTargets(_ -> 2)
        // Note: Once spirit link is casted, it will link to friendly units
        // even allied, I don't think we can alter this behavior
        // To obtain desired behavior, we need to trigger the damage
        // distribution effect
        ..presetTargetsAllowed(_ -> commaList(
            TargetsAllowed.playerunits,
            TargetsAllowed.organic,
            TargetsAllowed.air,
            TargetsAllowed.ground
        ))


function onHeal(HealingInstance instance)
    // First check if the unit is affected by Nature's Bond.
    // Then if the unit has the buff and is not a pet, it must be the troll that own's the pet.
    if not instance.target.hasAbility(BUFF_ID) or instance.target.isPet()
        return

    // If the unit is the owner of a pet heal the pet 50% of the amount the troll healed.
    if pets.has(instance.target.getOwner())
        new HealingInstance(
            instance.target.getOwner().getPet(),
            instance.caster,
            instance.amount * HEAL_DIST,
            instance.healingType
            )

init
    onUnitHealed() ->
        onHeal(getHealingInstance())
