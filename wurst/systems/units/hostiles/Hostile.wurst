package Hostile
import LinkedList
import UnitEntity
import ExpiringRawMeat
import TerrainUtils
import ClosureTimers
import initlater GreenFish
import initlater Fish
import initlater Hawk

public let MEAT_DROP_OFFSET_DISTANCE = 85.

public function spawnRawMeat(vec2 pos, int count)
    for i = 1 to count
        new ExpiringRawMeat(pos, MEAT_EXPIRATION_DEFAULT_DURATION)

public abstract class Hostile extends UnitEntity
    construct(unit whichUnit)
        super(whichUnit)

    construct(player whichPlayer, vec2 pos)
        super(whichPlayer, pos)

    construct(player whichPlayer, vec2 pos, angle facing)
        super(whichPlayer, pos, facing)

    function getDrops() returns LinkedList<int>
        return new LinkedList<int>()

    function getNumCorpses() returns int
        return 1

    override function postCreate()
        super.postCreate()

    override function onDeath()
        let pos = getPos()
        let numCorpses = (getNumCorpses() * udg_FOOD_FOR_KILL_PROPORTION).round()
        let drops = getDrops()
        let numDrops = drops.size()

        if numDrops != 0
            let dropsIter = drops.iterator()
            doPeriodicallyCounted(ANIMATION_PERIOD, numDrops) cb ->
                createItem(dropsIter.next(), pos)
                if cb.isLast()
                    dropsIter.close()
                    destroy drops
        else
            destroy drops

        //Find an offset position for the meat so items are separated on drop
        doAfter(ANIMATION_PERIOD * numDrops) ->
            for angleIndex = 0 to 3
                let meatPos = pos.polarOffset((angleIndex * 90.).fromDeg(), MEAT_DROP_OFFSET_DISTANCE)
                if meatPos.isTerrainWalkable()
                    spawnRawMeat(meatPos, numCorpses)
                    break
                else if angleIndex == 3
                    spawnRawMeat(pos, numCorpses)
                    break

        if this instanceof Hawk
            or this instanceof Fish
            or this instanceof GreenFish
            udg_FISH_CURRENT -= 1
        else
            udg_ANIMAL_CURRENT -= 1

        super.onDeath()
