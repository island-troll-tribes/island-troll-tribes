package Omnicure

// Standard library imports:
import Orders
import Assets
import LocalObjectIDs
import InstantDummyCaster
import ChannelAbilityPreset
import ClosureEvents
import ClosureForGroups

// Third-party imports:
import Lodash

// Local imports:
import ToolTipsUtils

let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let COOLDOWN = 50.
let MANACOST = 10
let TOOLTIP_NORM = "OmniCure"
let TOOLTIP_EXTENDED = "An upgrade of Cure All that affects all allies as well as the caster. Can cure jealousy, "+
                        "track, disease, snake poison, and many others. Can not be used on enemies. Has {0} seconds cooldown."
                        .format(COOLDOWN.toToolTipLightBlue())

let TARGET_ALLOWED = "targetAllowed"


public class Omnicure extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos, int manaCost)
        super(newAbilityId, 1, true)
        this.presetManaCost(lvl -> manaCost)
        this.presetCooldown(lvl -> COOLDOWN)
        this.setHeroAbility(false)
        this.setItemAbility(false)
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setIconNormal(Icons.bTNBigBadVoodooSpell)
        this.setIconResearch(Icons.bTNBigBadVoodooSpell)
        this.setIconTurnOff(Icons.bTNBigBadVoodooSpell)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setFollowThroughTime(1, 0.25)
        this.setArtDuration(1, 0)


@compiletime function createOmnicure()
    new Omnicure(ABILITY_OMNICURE, "E", new Pair(1, 0), MANACOST)
    new Omnicure(ABILITY_OMNICURE_ITEM, "E", new Pair(1, 0), 0)

public function castOmnicure(unit caster)
    let owner = caster.getOwner()
    forUnitsInRect(bj_mapInitialPlayableArea) (unit u) ->
        if u.isType(UNIT_TYPE_HERO) and u.getOwner().isAllyOf(owner)
            InstantDummyCaster.castTarget(owner, ABILITY_CURE_ALL, 1, Orders.autodispel, u)

init
    EventListener.onCast(ABILITY_OMNICURE) (unit caster) ->
        castOmnicure(caster)
    EventListener.onCast(ABILITY_OMNICURE_ITEM) (unit caster) ->
        castOmnicure(caster)
