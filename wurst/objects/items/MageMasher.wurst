package MageMasher

// Standard lib Imports:
import AbilityObjEditing
import DamageType
import ObjectIdGenerator
import Orders
import DummyCaster

// Local Imports:
import LocalObjectIDs
import Priest
import Mage
import UnitEntity

let MANABURN_ID = compiletime(ABIL_ID_GEN.next())
let DRAIN_AMOUNT = 10.

@compiletime function createDummyManaburn()
    new AbilityDefinitionManaBurndemon(MANABURN_ID)
        ..setAnimationNames("")
        ..setHeroAbility(false)
        ..setItemAbility(false)
        ..setLevels(1)
        ..presetCooldown(lvl -> 0)
        ..presetBoltDelay(lvl -> 0.15)
        ..presetBoltLifetime(lvl -> 0.4)
        ..presetCastRange(lvl -> 500)
        ..presetCastingTime(lvl -> 0)
        ..presetMaxManaDrained(lvl -> DRAIN_AMOUNT)

function mageMasherHit(unit attacker, unit target)
    let owner = attacker.getOwner()
    if isValidMasherTarget(owner, target)
        new DummyCaster()
            ..delay(0.45)
            ..owner(owner)
            ..origin(attacker.getPos())
            ..castTarget(MANABURN_ID, 1, Orders.manaburn, target)

function isValidMasherTarget(player attackOwner, unit target) returns bool
    let ent = UnitEntity.findForUnit(target)
    return target.getOwner().isEnemyOf(attackOwner)
        and target.isAlive()
        and (ent instanceof Mage or ent instanceof Priest)

init
    DamageEvent.addListener() () ->
        let attacker = DamageEvent.getSource()
        if attacker.hasItemById(ITEM_MAGE_MASHER) and DamageEvent.getAttackType() == ATTACK_TYPE_HERO
            mageMasherHit(attacker, GetTriggerUnit())
