package Depress
import RegisterEvents
import ClosureTimers
import AbilityObjEditing
import ObjectIdGenerator
import Assets
import ID
import Lodash
import ObjectIds
import ToolTipsUtils
import BuffObjEditing

//@configurable constant int ABILITY_ID = 'A025' Old GUI
let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let DUMMY_SLOW_ID = compiletime(ABIL_ID_GEN.next())
let MS_SLOW = 0.20
let AS_SLOW = 0.10
let BUFF_TOOLTIP = "Depress Slow"
let BUFF_TOOLTIPEXTENDED = "Slowed by Depress"
let DURATION = 10.

let MANA_LOST_INIT = 10.
let DEPRESS_TICK = 4
let MANA_LOST_PER_TICK = 5.
let TOTAL_MANA_LOST = MANA_LOST_INIT + DEPRESS_TICK * MANA_LOST_PER_TICK

let MANACOST = 10
let COOLDOWN = 20.
let BUFF = ""
let SPELL_TOOLTIP_NORM = "Depress"
let SPELL_TOOLTIP_EXTENDED = ("The mage casts a spell causing a chemical inbalance in the target's head. The target becomes severly depressed "+
                             "causing their attack speed to be reduced by {0} and movement speed by {1} while loosing {2} mana over {3} seconds. Has {4} seconds cooldown.")
                            .format(AS_SLOW.toToolTipOrange(), MS_SLOW.toToolTipOrange(), TOTAL_MANA_LOST.toToolTipTeal(), DURATION.toToolTipOrange(), COOLDOWN.toToolTipLightBlue())

let ANIMATION_NAMES = "Attack,two,Spell"
let BUFF_ACIDBOMB = 'BNab'

class DepressBuff extends BuffDefinition
    construct(int newBuffId, string whichSpell)
        super(newBuffId, BUFF_ACIDBOMB)
        this.setTargetAttachmentPoint1(1, "overhead")
        this.setArtTarget(1, Abilities.orbDarkness)
        this.setIcon(Icons.bTNShadowPact)
        this.setTooltipNormal(1, "Depressed"+whichSpell)
        this.setTooltipNormalExtended(1, "This unit loses energy over time")

class Depress extends AbilityDefinitionAlchemistAcidBomb
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setName(SPELL_TOOLTIP_NORM)
        this.setHotkeyNormal(hotkey)
        this.setCooldown(1, COOLDOWN)
        this.setManaCost(1, MANACOST)
        this.setIconNormal(Icons.bTNShadowPact)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setDurationHero(1, DURATION)
        this.setDurationNormal(1, DURATION)
        this.setTooltipNormalExtended(1, SPELL_TOOLTIP_EXTENDED)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, SPELL_TOOLTIP_NORM))
        this.setAttackSpeedReduction(1, -AS_SLOW)
        this.setMovementSpeedReduction(1, -MS_SLOW)
        this.setAnimationNames(ANIMATION_NAMES)
        this.setAreaofEffect(1, 0)
        this.setPrimaryDamage(1, 1)
        this.setSecondaryDamage(1, 0)
        this.setArmorPenalty(1, 0)
        this.setMissileArc(0.70)
        this.setMissileArt(Abilities.blackArrowMissile)
        this.setMissileSpeed(1400)
        this.setBuffs(1, toRawCode(BUFF_DEPRESSED_ARROW))
        this.setHeroAbility(false)
        this.setLevels(1)


@compiletime function createDepress()
    new Depress(SPELL_DEPRESS, "S", new Pair(1, 1))
    new DepressBuff(BUFF_DEPRESSED_AURA, "(Aura)")
    new DepressBuff(BUFF_DEPRESSED_ARROW, "(Arrow)")
    new DepressBuff(BUFF_DEPRESSED_THISTLE, "(Thistle)")

function onCast()
    let target = GetSpellTargetUnit()

    doAfter(0.2) ->
        if target.getMana() >= 40
            target.subMana(MANA_LOST_INIT)
            doPeriodicallyCounted(2.00, DEPRESS_TICK, (cb) -> target.subMana(MANA_LOST_PER_TICK))

init
    registerSpellEffectEvent(SPELL_DEPRESS, () -> onCast())
