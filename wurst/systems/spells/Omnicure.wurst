package Omnicure

import RegisterEvents
import InstantDummyCaster
import Orders
import ChannelAbilityPreset
import ID
import Lodash
import Assets
import ToolTipsUtils

let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let COOLDOWN = 50.
let MANACOST = 10
let TOOLTIP_NORM = "OmniCure"
let TOOLTIP_EXTENDED = "An upgrade of Cure All that affects all allies as well as the caster. Can cure jealousy, "+
                        "track, disease, snake poison, and many others. Can not be used on enemies. Has {0} seconds cooldown."
                        .format(COOLDOWN.toToolTipLightBlue())

class Omnicure extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos, int manaCost)
        super(newAbilityId, 1, true)
        this.presetManaCost(lvl -> manaCost)
        this.presetCooldown(lvl -> COOLDOWN)
        this.setHeroAbility(false)
        this.setItemAbility(false)
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setIconNormal(Icons.bTNBigBadVoodooSpell)
        this.setIconResearch(Icons.bTNBigBadVoodooSpell)
        this.setIconTurnOff(Icons.bTNBigBadVoodooSpell)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setFollowThroughTime(1, 0.25)
        this.setArtDuration(1, 0)

public class OmnicureItem extends Omnicure
    construct(int newAbilityId)
        super(newAbilityId, "", new Pair(0, 0), 0)
        this.setButtonPositionNormalX(0)
        this.setButtonPositionNormalY(0)

@compiletime function createOmnicure()
    new Omnicure(SPELL_OMNICURE, "E", new Pair(1, 0), MANACOST)

public function castOmnicure()
    let heroes = GetUnitsInRectMatching(bj_mapInitialPlayableArea, Condition(() -> begin
        let filterUnit = GetFilterUnit()
        return filterUnit.isType(UNIT_TYPE_HERO) and filterUnit.getOwner().isAllyOf(GetSpellAbilityUnit().getOwner())
    end))
    let owner = GetSpellAbilityUnit().getOwner()
    for hero in heroes
        InstantDummyCaster.castTarget(owner, SPELL_CURE_ALL, 1, Orders.autodispel, hero)

init
    registerSpellEffectEvent(SPELL_OMNICURE, function castOmnicure)
