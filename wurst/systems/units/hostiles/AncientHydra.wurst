package AncientHydra

// Standard library imports:
import Assets
import ClosureEvents
import ClosureForGroups
import ClosureTimers
import DummyCaster
import Orders

// Local imports:
import Hostile
import ID
import LesserHydra
import SoundUtils
import StringExtensions
import UnitExtensions

constant SPELL_NETHER = 'A037'


public class AncientHydra extends Hostile
    private static let unitTypeId = UNIT_ANCIENT_HYDRA

    static function initialize()
        register(unitTypeId, hydra -> new AncientHydra(hydra))

    CallbackPeriodic callback
    var frostReady = true

    construct(unit whichUnit)
        super(whichUnit)

    construct(vec2 pos)
        super(players[PLAYER_NEUTRAL_AGGRESSIVE], pos)

    override function postCreate()
        super.postCreate()
        let hydra = getUnit()
            ..addSleep(false)
            ..setCreepGuard(false)

        playMammothHorn()
        printTimed("The ancient hydra has been summoned!".color("red".toColor()), 5)
        flashEffect(Abilities.massTeleportCaster, hydra, "overhead")
        callback = doPeriodically(4, cb -> attackCenter())

        EventListener.add(hydra, EVENT_UNIT_DAMAGED, -> onAttacked())
        EventListener.add(hydra, EVENT_PLAYER_UNIT_SPELL_EFFECT, -> onSpellEffect())

    function attackCenter()
        let hydra = getUnit()

        if frostReady
            forNearestUnit(getPos(), 600, Filter(-> begin
                return not GetFilterUnit().getOwner().isAllyOf(players[PLAYER_NEUTRAL_AGGRESSIVE])
            end), closest -> begin
                if closest != null
                    hydra.issuePointOrder("breathoffrost", closest.getPos())
                    frostReady = false
                    doAfter(10, -> begin
                        frostReady = true
                    end)
            end)

        if frostReady
            hydra.issuePointOrder("attack", gg_rct_discoduck.getCenter())

    override function getNumCorpses() returns int
        return 8

    override function getTypeId() returns int
        return unitTypeId

    override function onDeath()
        destroy callback

        let pos = getPos()
        for i = 0 to 1
            new LesserHydra(pos)

        super.onDeath()

    function onAttacked()
        let attacker = GetEventDamageSource()
        if attacker.getTypeId() == UNIT_BEE
            attacker.setOwner(players[PLAYER_NEUTRAL_AGGRESSIVE], true)

    function onSpellEffect()
        if GetSpellAbilityId() == SPELL_NETHER
            getUnit().removeAbility(BUFF_NETHER)
            new DummyCaster()
                ..owner(players[PLAYER_NEUTRAL_AGGRESSIVE])
                ..origin(GetSpellAbilityUnit().getPos())
                ..castTarget(SPELL_NETHER, 1, Orders.banish, GetSpellAbilityUnit())


init
    AncientHydra.initialize()
