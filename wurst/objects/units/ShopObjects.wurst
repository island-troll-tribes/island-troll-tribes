package ShopObjects

import Assets
import UnitObjEditing
import AbilityObjEditing
import ObjEditingUtils
import ObjectIdGenerator

import LinkedList

import LocalObjectIDs
import LocalAssets

public function HeroDefinition.setTotemShopInterface()
    this
    ..setLumberCost(0)
    ..setFoodCost(0)
    ..setGoldCost(0)
    ..setStockStartDelay(0)
    ..setStockReplenishInterval(999) // Will be programatically refilled
    ..setStockMaximum(1)

@compiletime function createDummies()
    new HeroDefinition(UNIT_RANDOM_TROLL, UnitIds.shadowhunter)
    ..setTotemShopInterface()
    ..setName("Random Troll |cffffcc00(D)|r")
    ..setTooltipBasic("Random")
    ..setTooltipExtended("")
    ..setHotkey("D")
    ..setStockReplenishInterval(0)
    ..setIconGameInterface(Icons.bTNSelectHeroOn)
    ..setUnitSoundSet("")

    new HeroDefinition(UNIT_REPICK_TROLL, UnitIds.shadowhunter)
    ..setTotemShopInterface()
    ..setName("Repick Troll |cffffcc00(R)|r")
    ..setTooltipBasic("Repick")
    ..setTooltipExtended("")
    ..setHotkey("R")
    ..setStockReplenishInterval(0)
    ..setIconGameInterface(Icons.bTNCancel)
    ..setUnitSoundSet("")

@compiletime function createTotem()
    let ABILITY_SELECT_UNITS_TOTEM = compiletime(UNIT_ID_GEN.next())

    new AbilityDefinitionAlliedBuilding(ABILITY_SELECT_UNITS_TOTEM)
    ..setActivationRadius(1, 1) // Small activation radius so it doesn't try selecting trolls
    ..setShowSelectUnitButton(1, false)

    new BuildingDefinition(UNIT_TROLL_TOTEM, UnitIds.cantina)
    ..setName("Troll Totem")
    ..setDescription("Troll picker")
    ..setModelFile(Buildings.taurenTotem1)
    ..setTooltipBasic("Build Troll Totem")
    ..setScalingValue(1.5)
    ..setNormalAbilities(commaList(asList(
        AbilityIds.invulnerable,
        'Asud', // Needed for AddUnitToStock  (Asud is sell units ability - not in stdlib?)
        ABILITY_SELECT_UNITS_TOTEM
    )))
    ..setPlacementRequires("")
    ..setGroundTexture("")
    ..setPathingMap(PathingTextures.simpleSolid12x12) // Make bought heroes not be too close units away
    ..setUnitsSold("")