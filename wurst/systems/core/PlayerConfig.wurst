package PlayerConfig

// TODO: Although this import is not used, it is needed to avoid breaking the
//       underlying hashtables used elsewhere. It's not clear if this is due to
//       the initialization order of objects or just an odd behavior of the
//       compiler. This should be reinvestigated once #593 is complete.
import GameMode

public class PlayerConfig
    static thistype array configs

    static function initialize()
        for i = 0 to bj_MAX_PLAYER_SLOTS - 1
            new PlayerConfig(players[i])

    static function ofPlayer(player target) returns PlayerConfig
        return configs[target.getId()]

    // The player this configuration is for.
    player target

    // Whether the player has enabled legacy hotkeys.
    bool legacyHotkey = false

    construct(player target)
        // Update the global mapping.
        configs[target.getId()] = this

        // Store the target for reverse lookups.
        this.target = target

    function isLegacyHotkeyEnabled() returns bool
        return legacyHotkey

    function toggleLegacyHotkey()
        legacyHotkey = not legacyHotkey

init
    PlayerConfig.initialize()
