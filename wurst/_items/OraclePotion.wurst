package OraclePotion

import AbilityObjEditing
import AbilityObjectCreationUtils
import BuffObjEditing
import ObjectIdGenerator
import RegisterEvents
import HashMap
import TimerUtils
import ObjectIds
import ClosureTimers
import ID
import ItemObjEditing


constant let ABILITY_ID = compiletime(ABIL_ID_GEN.next())
constant let ABILITY_DUMMY_ID = compiletime(ABIL_ID_GEN.next())
constant let BUFF_ID = compiletime(BUFF_ID_GEN.next())
constant let BUFF_AURA_ID = compiletime(ABIL_ID_GEN.next())
constant let SPELLBOOK_ABILITY_ID = compiletime(createHiddenSpellbookWithAbilities("{0},{1}".format(toRawCode(ABILITY_ID), toRawCode(BUFF_AURA_ID))))

constant let SPELL_ICON = "ReplaceableTextures\\CommandButtons\\BTNCharm.blp"
constant let DURATION = 20.
constant let COOLDOWN = 20.

@compiletime function createTrueSightBuff()
    new BuffDefinition(BUFF_ID, 'Bstt')
    ..setArtTarget(1, "")
    ..setTooltipNormal(1, "True Sight")
    ..setTooltipNormalExtended(1, "This unit can see invisible unit.")
    ..setIcon("ReplaceableTextures\\PassiveButtons\\PASBTNMagicalSentry.blp")
 
@compiletime function createDummyAbility()
    new AbilityDefinitionWindWalk(ABILITY_DUMMY_ID)
    ..setArtCaster("")
    ..setArtEffect("")
    ..setArtTarget("")
    ..setArtSpecial("")
    ..setIconNormal(SPELL_ICON)
    ..setIconResearch(SPELL_ICON)
    ..setIconTurnOff(SPELL_ICON)
    ..setHeroAbility(false)
    ..setItemAbility(true)
    ..setLevels(1)
    ..setHotkeyNormal("A")
    ..setName("TrueSight")
    ..setTooltipNormal(1, "dummy spell")
    ..setTooltipNormalExtended(1, "dummy spell")
    ..setCooldown(1, COOLDOWN)
    ..setManaCost(1, 0)
    ..setAnimationNames("")
    ..setDurationHero(1, 0.01)
    ..setCastingTime(1, 0)
    ..setDurationNormal(1, 0.01)
    ..setEffectSound("")

@compiletime function createDummyAura()
    new AbilityDefinitionAuraSlow(BUFF_AURA_ID)
    ..setArtTarget("Abilities\\Spells\\Human\\MagicSentry\\MagicSentryCaster.mdl")
    ..setTargetAttachmentPoint("overhead")
    ..setMovementSpeedFactor(1, 0)
    ..setAreaofEffect(1, 0)
    ..setBuffs(1, toRawCode(BUFF_ID))
    ..setDurationHero(1, 20)
    ..setDurationNormal(1, 20)
    ..setTargetsAllowed(1, "self")
    ..setName("True Sight")
 
@compiletime function createTrueSightSpell()
    new AbilityDefinition(ABILITY_ID, 'Atru')
    ..setCastRange(1, 900)
    ..setName("Oracle clarity")
    ..setTooltipNormal(1, "True Sight")
    ..setTooltipNormalExtended(1, "Reveal invisible unit")
    ..setBuffs(1, "Binf")
    ..setItemAbility(true)
    ..setDurationHero(1, 20)
    ..setDurationNormal(1, 20)
    ..setButtonPositionNormalY(0)
    ..setArtCaster("Abilities\\Spells\\Human\\MagicSentry\\MagicSentryCaster.mdl")
    ..setCasterAttachmentPoint("overhead")
    ..setCasterAttachments(0)

@compiletime function createOraclePotion()
    new ItemDefinition(ITEM_ORACLE_POTION, 'pinv')
    ..setName("Oracle Potion")
    ..setAbilities(toRawCode(ABILITY_DUMMY_ID))
    ..setActivelyUsed(true)
    ..setTooltipExtended("The consumer of this potion gain the true sight buff for 20 seconds, allowing him to see stealthed unit. Good versus thief & vision ward")
    ..setModelUsed("Abilities\\Spells\\Other\\AcidBomb\\BottleMissile.mdx")
    ..setLumberCost(11)
    ..setGoldCost(0)
    ..setInterfaceIcon("ReplaceableTextures\\CommandButtons\\BTNCharm.blp")
    ..setDescription("Give true sight")
    ..setDroppedWhenCarrierDies(true)
    ..setNumberofCharges(1)

HashMap<unit, timer> removalTimers = new HashMap<unit,timer>()

function onCast()
    var caster = GetSpellAbilityUnit()

    if removalTimers.has(caster)
        removalTimers.getAndRemove(caster).release()
    caster.addAbility(SPELLBOOK_ABILITY_ID)
    caster.getOwner().setAbilityAvailable(SPELLBOOK_ABILITY_ID, false)

    let t = getTimer()
    t.doAfter(DURATION) ->
        caster.removeAbility(SPELLBOOK_ABILITY_ID)
        caster.removeAbility(BUFF_AURA_ID)
        removalTimers.remove(caster)
    removalTimers.put(caster, t)

init
    registerSpellEffectEvent(ABILITY_DUMMY_ID, () -> onCast())