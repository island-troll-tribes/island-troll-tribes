package BattleArmor

import AbilityObjEditing
import ObjectIdGenerator
import RegisterEvents
import ClosureForGroups
import ObjectIds
import DummyCaster
import ID
import Orders
import ItemObjEditing
import UnitExtensions

constant let COOLDOWN = 75.
constant let SPELL_RADIUS = 800.
constant let SPELL_ANTIMAGIC = 'A0F8'
constant let SPELL_DAMAGE_REDUCTION = 'Alsr'
constant let ABILITY_BATTLE_GEAR_STAT_BONUS = 'A0E7'
constant let ABILITY_DUMMY_ID = compiletime(ABIL_ID_GEN.next())
constant let ITEM_ABILITIES = commaList(ABILITY_DUMMY_ID, ABILITY_BATTLE_GEAR_STAT_BONUS, ABILITY_ARMOR_BONUS7, SPELL_DAMAGE_REDUCTION)

@compiletime function createDummyAbility()
    new AbilityDefinitionWindWalk(ABILITY_DUMMY_ID)
    ..setArtCaster("")
    ..setArtEffect("")
    ..setArtTarget("")
    ..setArtSpecial("")
    ..setHeroAbility(false)
    ..setItemAbility(true)
    ..setLevels(1)
    ..setHotkeyNormal("A")
    ..setName("Battle Armor Antimagic")
    ..setTooltipNormal(1, "dummy spell")
    ..setTooltipNormalExtended(1, "dummy spell")
    ..setCooldown(1, COOLDOWN)
    ..setManaCost(1, 0)
    ..setAnimationNames("")
    ..setDurationHero(1, 0.01)
    ..setCastingTime(1, 0)
    ..setDurationNormal(1, 0.01)
    ..setEffectSound("")

@compiletime function createBattleArmor()
    new ItemDefinition(ITEM_BATTLE_ARMOR, 'bspd')
    ..setName("Battle Armor")
    ..setAbilities(ITEM_ABILITIES)
    ..setTooltipExtended("Increase all stats by 4 and armor by 7, provide 20% resistance against spell, can cast Antimagic on surrounding allies, 75 seconds cooldown")
    ..setModelUsed("Doodads\\LordaeronSummer\\Terrain\\Crates\\Crates0.mdx")
    ..setGoldCost(0)
    ..setInterfaceIcon("ReplaceableTextures\\CommandButtons\\BTNRobeOfTheMagi.blp")
    ..setDescription("Increase all stats by 4 and armor by 7, provide 20% resistance against spell, can cast Antimagic on surrounding allies, 75 seconds cooldown")
    ..setDroppedWhenCarrierDies(true)
    ..setScalingValue(0.80)
    ..setActivelyUsed(true)
    ..setCooldownGroup("Item Move Speed Bonus")
    ..setHitPoints(75)
    ..setLumberCost(68)
    ..setLevelUnclassified(2)
    ..setPerishable(false)
    ..setPriority(41)
    ..setTooltipBasic("Trade for Battle Armor")

function onCast()
    var caster = GetSpellAbilityUnit()

    forUnitsInRange(caster.getPos(), SPELL_RADIUS) u ->
        if u.isTroll() and u.getOwner().isAllyOf(caster.getOwner())
            new DummyCaster()
            ..owner(caster.getOwner())
            ..origin(u.getPos() + vec2(100, 0))
            ..castTarget(SPELL_ANTIMAGIC, 1, Orders.antimagicshell, u)
init
    registerSpellEffectEvent(ABILITY_DUMMY_ID, () -> onCast())