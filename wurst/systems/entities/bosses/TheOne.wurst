package TheOne

// Standard library imports:
import Assets
import ClosureTimers
import RegisterEvents
import LinkedList

// Local imports:
import ID
import JumpSystem
import BonyAnimal
import GroupUtils
import UnitEntity

public let THE_ONE_REGION = CreateRegion()
    ..addRect(gg_rct_TheOne)

let spawnSpot = gg_rct_TheOneCliff.getLeftTop()

// The singleton instance.
public TheOne theOne = null

public class TheOne extends BonyAnimal
    private static let unitTypeId = UNIT_ONE

    static function initialize()
        register(unitTypeId, target -> new TheOne(target))

    // The callback responsible for periodically incrementing the level.
    static CallbackPeriodic cb

    construct(unit whichUnit)
        super(whichUnit)

    override function postCreate()
        super.postCreate()

    static function beginLevelling()
        cb = doPeriodically(60) cb ->
            if theOne != null and theOne.getUnit().isAlive()
                theOne.getUnit().addLevels(1, true)

    override function getDrops() returns LinkedList<int>
        let drops = super.getDrops()
            ..add(ITEM_ESSENCE_BEES)
            ..add(ITEM_STEEL_INGOT)
        return drops

    override function getNumCorpses() returns int
        return 3

    // Avoid removing unit by not propagating the call upwards.
    override function postDeath()
        // Disable levelling while dead.
        destroy cb

public function spawnTheOne()
    // Delay to effect slightly.
    doAfter(1) ->
        // Instantiate the singleton the first time it is needed.
        if theOne == null
            theOne = new TheOne(
                // Spawn the unit.
                createUnit(
                    players[PLAYER_NEUTRAL_AGGRESSIVE],
                    UNIT_ONE,
                    spawnSpot,
                    spawnSpot.angleTo(gg_rct_TheOne.getCenter())
                )
            )
        else
            // Revive the existing unit if it exists.
            theOne.getUnit().revive(spawnSpot, true)

        // Order the unit to jump into the arena.
        performJump(theOne.getUnit(), gg_rct_TheOne.getCenter())

        // Begin the callback to periodically increment the level.
        TheOne.beginLevelling()

public function canSpawnTheOne() returns boolean
    return theOne == null or not theOne.getUnit().isAlive()

function checkInvulnerability() returns bool
    // The One is always invulnerable when outside its arena.
    if not theOne.getUnit().isInRegion(THE_ONE_REGION)
        return true

    // The One is invulnerable unless there is a troll present in its arena.
    for troll in getTrolls()
        // Disable invulnerability if both conditions are met.
        if troll.isAlive() and troll.isInRegion(THE_ONE_REGION)
            return false

    // Enable invulnerability if no matches were found.
    return true

function toggleInvulnerability()
    // Exit if The One is not currently alive.
    if theOne == null or not theOne.getUnit().isAlive()
        return

    // Toggle the invulnerability.
    if checkInvulnerability()
        theOne.getUnit().addAbility(AbilityIds.invulnerable)
    else
        theOne.getUnit().removeAbility(AbilityIds.invulnerable)

init
    TheOne.initialize()
    CreateTrigger()
        ..registerEnterRegion(THE_ONE_REGION, null)
        ..addAction(function toggleInvulnerability)
    CreateTrigger()
        ..registerLeaveRegion(THE_ONE_REGION, null)
        ..addAction(function toggleInvulnerability)
    registerPlayerUnitEvent(
        EVENT_PLAYER_UNIT_DEATH,
        function toggleInvulnerability
    )
