package HydraSpawn
import AncientHydra
import ClosuresForItems
import ID
import ClosureEvents
import LinkedList
import GameConfig
import ClosureForGroups
import Assets

constant HYDRA_ALTAR_SFX = Units.wispExplode

function maybeSummonHydra()
    let altar = GetSellingUnit()

    if altar.getTypeId() != UNIT_OMINOUS_ALTAR
        return

    let mushroom = altar.itemInSlot(1)
    if mushroom.getTypeId() != ITEM_MUSHROOM
        return

    let riverRoot = altar.itemInSlot(2)
    if riverRoot.getTypeId() != ITEM_RIVER_ROOT
        return

    let elkHide = altar.itemInSlot(3)
    if elkHide.getTypeId() != ITEM_ELK_HIDE
        return

    let herb = altar.itemInSlot(0)
    rect spawn
    switch herb.getTypeId()
        case ITEM_BLUE_HERB
            spawn = gg_rct_ship_LO
        case ITEM_YELLOW_HERB
            spawn = gg_rct_ship_RO
        case ITEM_ORANGE_HERB
            spawn = gg_rct_ship_TO
        case ITEM_PURPLE_HERB
            spawn = gg_rct_ship_BO
        default
            return


    if gameConfig.isOldMeatSystemEnabled()
        let meats = new LinkedList<unit>()
        forUnitsInRange(altar.getPos(), 400) (unit u) ->
            if u.getTypeId() == UNIT_MEAT
                meats.push(u)
        if meats.size() < 4
            destroy meats
            return

        for meat in meats
            createItem(ITEM_COOKED_MEAT, meat.getPos())
            meat.remove()

        destroy meats

    else
        let meats = new LinkedList<item>()
        forItemsInMap() itm ->
            if itm.getTypeId() == ITEM_RAW_MEAT and itm.getPos().distanceTo(altar.getPos()) <= 400
                meats.push(itm)

        if meats.size() < 4
            destroy meats
            return

        for meat in meats
            createItem(ITEM_COOKED_MEAT, meat.getPos())
            meat.remove()

        destroy meats

    mushroom.remove()
    riverRoot.remove()
    elkHide.remove()
    herb.remove()

    flashEffect(HYDRA_ALTAR_SFX, altar, "overhead")
    new AncientHydra(spawn.getCenter())


init
    EventListener.add(EVENT_PLAYER_UNIT_SELL_ITEM, () -> maybeSummonHydra())
