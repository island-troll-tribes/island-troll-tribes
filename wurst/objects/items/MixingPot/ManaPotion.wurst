package ManaPotion

import LocalObjectIDs
import ToolTipsUtils
import ChannelAbilityPreset
import ClosureEvents
import Assets
import ObjEditingUtils
import ItemObjEditing
import Items

constant ABIL  = compiletime(ABIL_ID_GEN.next())

constant CAST_RANGE = 500.
constant MANA_RESTORED = 60.

constant ICON  = ICON_PATH.format("PotionBlueSmall")
constant MODEL = "Models\\PotionBlueLesser.mdl"
constant TT    = "A dilute potion made from Athelas Seed. Instantly restores {0} Mana point to a target ally.".format(MANA_RESTORED.toToolTipLightBlue())

constant TARGET_ALLOWED = "friend,ground,hero,invulnerable,organic,self,vulnerable"

@compiletime function createAbil()
    new ItemChannelCast(ABIL, "Mana Potion", Targettype.UNIT, TARGET_ALLOWED, CAST_RANGE, 0)

@compiletime function createManaPotion() returns ItemDefinition
    return createPerishableItem(ITEM_MANA_POTION)
        ..setName("Mana Potion")
        ..setInterfaceIcon(ICON)
        ..setModelUsed(MODEL)
        ..setHotkey("W")
        ..setTooltipBasic("Mana Potion")
        ..setTooltipExtended(TT)
        ..setLumberCost(5)
        ..setAbilities(commaList(ABIL))
        ..setScalingValue(0.7)
        ..setCooldownGroup("A002")
        ..setStockReplenishInterval(120)
        ..setNumberofCharges(1)

init
    EventListener.onTargetCast(ABIL) (unit caster, unit target) ->
        target.addMana(MANA_RESTORED)
        flashEffect(Abilities.aImaTarget, target, "origin")
