package DreamEater
import RegisterEvents
import ID
import Icons
import Lodash
import ToolTipsUtils
import ChannelAbilityPreset
import Abilities
import SimError

//@configurable constant int ABILITY_ID = 'A04Z' Old GUI
@configurable constant int BUFF_ID = 'BOvd' // Only Work on Hypnotized target :(
@configurable constant string EFFECT_PATH = Abilities.aIsoTarget

let HP_RESTORED = 50.
let MANA_RESTORED = 30.
let MANA_EATEN = 50.
let DAMAGE = 20.
let MANACOST = 10
let COOLDOWN = 20.

let ANIMATION_NAME = "channel"
let TOOLTIP_NORM = "Dream Eater"
let TOOLTIP_EXTENDED = ("Steals an enemy dream restoring {0} Health Point and {1} Mana. Also reduces enemy's Mana by {2} and deals {3} damage. "+
                        "Only works if the enemy is dreaming. Has {4} seconds cooldown.")
                       .format(HP_RESTORED.toToolTipGreen(), MANA_RESTORED.toToolTipTeal(), MANA_EATEN.toToolTipTeal(), DAMAGE.toToolTipRed(),
                        COOLDOWN.toToolTipLightBlue())

let TARGET_ALLOWED = "enemies,ground,hero"

class DreamEater extends ChannelAbilityPreset
    construct(int newAbility, string hotkey, Pair<int , int> buttonPos)
        super(newAbility, 1, true)
        this.setName(TOOLTIP_NORM)
        this.setHotkeyNormal(hotkey)
        this.setCooldown(1, COOLDOWN)
        this.setManaCost(1, MANACOST)
        this.setIconNormal(Icons.bTNDevourMagic)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.setArtDuration(1, 0)
        this.setTargetType(1, 1)
        this.setAnimationNames(ANIMATION_NAME)
        this.setTargetsAllowed(1, TARGET_ALLOWED)

@compiletime function createDreamEater()
    new DreamEater(SPELL_DREAM_EATER, "W", new Pair(1, 0))

function onStartCast()
    if GetSpellAbilityId() == SPELL_DREAM_EATER and not GetSpellTargetUnit().hasAbility(BUFF_ID)
        GetSpellAbilityUnit().abortOrder()
        simError(GetTriggerPlayer(), "Target is not Hypnotized.")

function onCast()
    var target = GetSpellTargetUnit()
    target.addEffect(EFFECT_PATH, "overhead").destr()
    target.subHP(DAMAGE)

    var value = target.getMana() - MANA_EATEN
    target.setMana(max(value, 10))

    var caster = GetSpellAbilityUnit()
    caster.addHP(HP_RESTORED)
    caster.addMana(MANA_RESTORED)

init
    registerSpellEffectEvent(SPELL_DREAM_EATER, () -> onCast())
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_CAST, () -> onStartCast())
