package HatcheryHatch
import RegisterEvents
import UnitExtensions
import LocalObjectIDs

public constant ABILITY_HATCH = 'A03Q'
public constant ABILITY_HAWK_GATHER = 'A03T'
public constant ABILITY_HAWK_HEALTH = 'A03V'
public constant ABILITY_HAWK_INVENTORY = 'A03W'
public constant ABILITY_HAWK_MOVE = 'A03U'
public constant ABILITY_HAWK_SPEED = 'A03S'

init
    registerSpellEffectEvent(ABILITY_HATCH) ->
        let hatchery = GetSpellAbilityUnit()
        let items = hatchery.getInventoryItemCountMap()
        if items.get(ITEM_HAWK_EGG) == 1
            let dark = items.get(ITEM_SPIRIT_DARKNESS)
            let speed = dark + items.get(ITEM_HAWK_EGG) + items.get(ITEM_STICK)
            let move = dark + items.get(ITEM_CLAY_BALL) + items.get(ITEM_SPIRIT_WIND)
            let gather = dark + items.get(ITEM_BLUE_HERB) + items.get(ITEM_ORANGE_HERB)
            let health = dark + items.get(ITEM_JUNGLE_BEAR_HIDE) + items.get(ITEM_ELK_HIDE)
            let inventory = dark + items.get(ITEM_THISTLES) + items.get(ITEM_MUSHROOM)
            let extra = items.get(ITEM_MANA_CRYSTAL) + items.get(ITEM_SPIRIT_WATER) + items.get(ITEM_ACORN) + items.get(ITEM_TINDER)

            if speed + move + gather + health + inventory + extra > 4
                var id = UNIT_HAWK_HATCHLING
                if dark > 4
                    id = UNIT_NETHER_DRAGON_HATCHLING
                else if inventory > 2
                    id = UNIT_FOREST_DRAGON_HATCHLING
                else if move > 2
                    id = UNIT_RED_DRAGON_HATCHLING
                else if health > 2
                    id = UNIT_BRONZE_DRAGON_HATCHLING

                consumeItems(hatchery)
                let hawk = createUnit(players[PLAYER_NEUTRAL_PASSIVE], id, hatchery.getPos(), angle(0))
                hawk.setAbilityLevel(ABILITY_HAWK_GATHER,    gather.   clamp(1,6))
                hawk.setAbilityLevel(ABILITY_HAWK_HEALTH,    health.   clamp(1,6))
                hawk.setAbilityLevel(ABILITY_HAWK_INVENTORY, inventory.clamp(1,6))
                hawk.setAbilityLevel(ABILITY_HAWK_MOVE,      move.     clamp(1,6))
                hawk.setAbilityLevel(ABILITY_HAWK_SPEED,     speed.    clamp(1,6))
        // Deallocate the item counts object once it is no longer needed.
        destroy items


function consumeItems(unit hatchery)
    //Remove all hatchery-usable items from the inventory


    for i = 0 to 5
        var maybeEgg = hatchery.itemInSlot(i)
        if maybeEgg.getTypeId() == ITEM_HAWK_EGG
            var charges = maybeEgg.getCharges()
            if charges > 1
                maybeEgg.setCharges(charges - 1)
            else
                maybeEgg.remove()
        hatchery.getItemById(ITEM_SPIRIT_DARKNESS).remove()
        hatchery.getItemById(ITEM_STICK).remove()
        hatchery.getItemById(ITEM_CLAY_BALL).remove()
        hatchery.getItemById(ITEM_SPIRIT_WIND).remove()
        hatchery.getItemById(ITEM_BLUE_HERB).remove()
        hatchery.getItemById(ITEM_ORANGE_HERB).remove()
        hatchery.getItemById(ITEM_JUNGLE_BEAR_HIDE).remove()
        hatchery.getItemById(ITEM_ELK_HIDE).remove()
        hatchery.getItemById(ITEM_THISTLES).remove()
        hatchery.getItemById(ITEM_MUSHROOM).remove()
        hatchery.getItemById(ITEM_MANA_CRYSTAL).remove()
        hatchery.getItemById(ITEM_SPIRIT_WATER).remove()
        hatchery.getItemById(ITEM_ACORN).remove()
        hatchery.getItemById(ITEM_TINDER).remove()
