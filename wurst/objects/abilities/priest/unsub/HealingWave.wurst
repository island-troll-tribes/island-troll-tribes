package HealingWave

import RegisterEvents
import ClosureTimers
import Assets
import HealingSystem
import EffectUtils
import LinkedList
import MapBounds
import Lodash
import ChannelAbilityPreset
import LocalObjectIDs
import ToolTipsUtils

//let ABILITY_ID = 'XPHW'
let CAST_RANGE = 6000.
let BOUNCE_SEARCH_RANGE = 6000.
let COOLDOWN = 20.
let MANACOST = 10
let HEAL_AMOUNT = 50.
let HEAL_REDUCTION_BOUNCE_FACTOR = 0.1
let BOUNCE_COUNT = 7.
let TOOLTIP_NORMAL = "Healing wave"
let TOOLTIP_NORMAL_EXT = ("Calls forth a wave of healing energy that bounces up to {0} times, healing {1} damage on the primary target. "+
                        "Each jump heals {2} less. Bounces on an extremely large range. Has {3} seconds cooldown.")
                        .format(BOUNCE_COUNT.toToolTipOrange(), HEAL_AMOUNT.toToolTipGreen(), HEAL_REDUCTION_BOUNCE_FACTOR.toToolTipOrange(), COOLDOWN.toToolTipLightBlue())
let HEALING_WAVE_HIT_FX = Abilities.healingWaveTarget
let HEAL_HIT_FX = Abilities.healTarget

class HealingWave extends ChannelAbilityPreset
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId, 1, true)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.presetCastRange(lvl -> CAST_RANGE)
        this.presetFollowThroughTime(lvl -> 0.275)
        this.presetCooldown(lvl -> COOLDOWN)
        this.presetManaCost(lvl -> MANACOST)
        this.presetTargetType(lvl -> 1)
        this.presetTargetsAllowed(lvl -> "air,ground,friend,self,vuln,invu,organic")
        this.setHeroAbility(false)
        this.setBaseOrderID(1, "healingwave")
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORMAL)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORMAL))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_NORMAL_EXT)
        this.setIconNormal(Icons.bTNHealingWave)

@compiletime function createHealingWave()
    new HealingWave(ABILITY_HEALING_WAVE, "S", new Pair(1, 1))
    new HealingWave(ABILITY_MH_HEALING_WAVE, "E", new Pair(2, 0))
    new HealingWave(ABILITY_SAGE_HEALING_WAVE, "D", new Pair(2, 1))

class HealingWaveInstance
    unit caster
    player owner
    int bouncesLeft
    real healingAmount
    LinkedList<unit> targetsHit = new LinkedList<unit>()

    construct(unit caster, unit target, real healingInitialAmount, int bounces)
        this.caster = caster
        this.healingAmount = healingInitialAmount
        this.bouncesLeft = bounces
        this.owner = caster.getOwner()
        waveBounce(target, caster, true)

    function waveBounce(unit target, unit previousTarget, bool isFirstBounce)
        new HealingInstance(target, this.healingAmount, HealingType.ABILITY)

        var lightningFxPath = isFirstBounce ? LIGHTNING_HEALING_WAWE_PRIMARY : LIGHTNING_HEALING_WAWE_SEECONDARY
        attachLightningFX(previousTarget, target, lightningFxPath, 1.5)

        let fx1 = addEffect(HEALING_WAVE_HIT_FX, target.getPos3Real())
        let fx2 = target.addEffect(HEAL_HIT_FX, "origin")
        doPeriodicallyTimed(ANIMATION_PERIOD, 1.5) fxUpdater ->
            fx1.setPos(target.getPos3Real())
            if fxUpdater.isLast()
                fx1.setPos(boundMax)
                fx1.destr()
                fx2.destr()

        this.healingAmount *= 1 - HEAL_REDUCTION_BOUNCE_FACTOR
        this.bouncesLeft -= 1
        this.targetsHit.add(target)
        if this.bouncesLeft > 0
            doAfter(0.3) ->
                let nextTarget = findBounceTarget(target)
                if nextTarget != null
                    waveBounce(nextTarget, target, false)
                else
                    destroy this
        else
            destroy this

    function findBounceTarget(unit bounceOrigin) returns unit
        let units = CreateGroup()
            ..enumUnitsInRange(bounceOrigin.getPos(), BOUNCE_SEARCH_RANGE)

        unit nearest = null
        var closestDistance = REAL_MAX
        for u in units
            if isValidTarget(u)
                let distance = u.getPos().distanceTo(bounceOrigin.getPos())
                if distance < closestDistance
                    nearest = u
                    closestDistance = distance

        units.destr()
        return nearest

    function isValidTarget(unit target) returns bool
        return target.isAlive()
            and target.getOwner().isAllyOf(this.owner)
            and not target.isType(UNIT_TYPE_STRUCTURE)
            and not target.isType(UNIT_TYPE_MECHANICAL)
            and not targetsHit.has(target)
            and not target.getHP().round() == target.getMaxHP().round()

    ondestroy
        destroy this.targetsHit

init
    // Don't know how to make it simplier
    registerSpellEffectEvent(ABILITY_HEALING_WAVE) ->
        var caster = GetSpellAbilityUnit()
        var target = GetSpellTargetUnit()
        new HealingWaveInstance(caster, target, HEAL_AMOUNT, BOUNCE_COUNT.toInt())

    registerSpellEffectEvent(ABILITY_MH_HEALING_WAVE) ->
        var caster = GetSpellAbilityUnit()
        var target = GetSpellTargetUnit()
        new HealingWaveInstance(caster, target, HEAL_AMOUNT, BOUNCE_COUNT.toInt())

    registerSpellEffectEvent(ABILITY_SAGE_HEALING_WAVE) ->
        var caster = GetSpellAbilityUnit()
        var target = GetSpellTargetUnit()
        new HealingWaveInstance(caster, target, HEAL_AMOUNT, BOUNCE_COUNT.toInt())
