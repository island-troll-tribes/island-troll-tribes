package TeamGoldSharing

// Standard library imports:
import ClosureTimers
import RegisterEvents

// Third-party imports:
import Lodash

// Local imports:
import Game
import GameConfig
import LocalObjectIDs
import Tribe

function syncTribeGold(unit whichUnit, boolean useMin)
    nullTimer() ->
        let owner = whichUnit.getOwner()
        let tribe = Tribe.ofPlayer(owner)
        let members = tribe.getMembers()
        let gold = useMin
            ? min(map((member, index) -> member.getLumber(), members))
            : owner.getLumber()
        for member in members
            member.setLumber(gold)

function initializeTeamGold()
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SELL_ITEM) ->
        syncTribeGold(GetBuyingUnit(), true)

    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_PAWN_ITEM) ->
        syncTribeGold(GetTriggerUnit(), false)

    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_PICKUP_ITEM) ->
        if GetManipulatedItem().getTypeId() == ITEM_ALIGATOR_GOLD
            syncTribeGold(GetTriggerUnit(), false)

init
    registerGameStartEvent() ->
        if gameConfig.isTeamGoldEnabled()
            initializeTeamGold()
