package ResourceSpawns
import ID
import HashMap
import LinkedList
import GameTimer
import Game
import Interpolation
import ErrorHandling
import TerrainUtils
import SpawnRateAdjustment
import ClosureTimers
import GameConfig
import BushSpawns
import Pool
import Elk
import Panther
import Bear
import Wolf
import Snake
import Hawk
import GreenFish
import Fish
import Lodash
import UnitEntity

let itemSpawnInfoMap = new IterableMap<int, SpawnInfo>()
let animalSpawnInfoMap = new IterableMap<int, SpawnInfo>()
let animalSpawnFunctions = new HashMap<int, BiFunction<real, real, UnitEntity>>()
let islandSpawnList = new LinkedList<IslandSpawner>()
let FISH_PER_AREA = 1
var gameStartTime = 0.

class SpawnInfo
    int id
    real initialSpawnWeight
    real finalSpawnWeight
    real weightChangeTime
    construct(int id, real initialSpawnWeight, real finalSpawnWeight, real weightChangeTime)
        this.id = id
        this.initialSpawnWeight = initialSpawnWeight
        this.finalSpawnWeight = finalSpawnWeight
        this.weightChangeTime = weightChangeTime

    function getTimeAdjustedSpawnWeight() returns real
        if this.weightChangeTime == 0
            return finalSpawnWeight
        //Clamp ratio between 0 - 1 with min & max
        var ratio = min(1, max(0, (getElapsedGameTime() - gameStartTime) / this.weightChangeTime))
        let spawnWeightAdjusted = linear(initialSpawnWeight, finalSpawnWeight, ratio)
        return spawnWeightAdjusted

class AnimalSpawnInfo extends SpawnInfo
    construct(int id, real initialSpawnWeight, real finalSpawnWeight, real weightChangeTime)
        super(id, initialSpawnWeight, finalSpawnWeight, weightChangeTime)
        animalSpawnInfoMap.put(id, this)

class ItemSpawnInfo extends SpawnInfo
    construct(int id, real initialSpawnWeight, real finalSpawnWeight, real weightChangeTime)
        super(id, initialSpawnWeight, finalSpawnWeight, weightChangeTime)
        itemSpawnInfoMap.put(id, this)

class IslandSpawner
    private constant Pool<rect> spawnRegionsPool = null
    private Pool<BiFunction<real, real, UnitEntity>> animalPool = null
    private let spawnsPerType = new HashMap<int, int>()
    int itemsSpawnCount
    int animalsSpawnCount
    private int itemsSpawnedTotal = 0
    private Pool<int> itemPool = null

    construct(int itemsSpawnCount, int animalsSpawnCount, Pool<rect> spawnRegionsPool)
        this.itemsSpawnCount = itemsSpawnCount
        this.animalsSpawnCount = animalsSpawnCount
        this.spawnRegionsPool = spawnRegionsPool
        islandSpawnList.push(this)

    function randomSpawnPoint() returns vec2
        rect spawnRect = this.spawnRegionsPool.random()
        while true
            vec2 spawnPoint = spawnRect.randomPoint()
            if spawnPoint.isTerrainLand() and spawnPoint.isTerrainWalkable()
                return spawnPoint

        error("ERROR: randomSpawnPoint() returned null!")
        return vec2(0,0)

    function spawnItemFromPool()
        if this.itemPool == null
            error("ERROR: spawnItemFromPool tried to spawn without having a generated pool!")
            return

        let spawnPoint = randomSpawnPoint()
        let spawnedItem = createItem(itemPool.random(), spawnPoint)

        //Keep track of spawned total & spawned item type total to balance out spawning
        this.itemsSpawnedTotal += 1
        let spawnedType = spawnedItem.getTypeId()
        if not this.spawnsPerType.has(spawnedType)
            this.spawnsPerType.put(spawnedType, 1)
        else
            this.spawnsPerType.put(spawnedType, this.spawnsPerType.get(spawnedType) + 1)

    function spawnAnimalFromPool()
        if this.animalPool == null
            error("ERROR: spawnAnimalFromPool tried to spawn without having a generated pool!")
            return

        let spawnPoint = randomSpawnPoint()
        let spawnFunc = this.animalPool.random()
        spawnFunc.call(spawnPoint.x, spawnPoint.y)

    function generateAnimalPool()
        if this.animalPool != null
            destroy this.animalPool

        this.animalPool = new Pool<BiFunction<real, real, UnitEntity>>
        for id in animalSpawnInfoMap
            let spawnInfo = animalSpawnInfoMap.get(id)
            this.animalPool.add((spawnInfo.getTimeAdjustedSpawnWeight() * gameConfig.getHostileSpawnRate()).round(), animalSpawnFunctions.get(id))

    function generateItemPool()
        /*
        This function generates an island-specific itempool with adjusted weights according to how much current state of spawned items
        differs from what the adjusted weights are.

        For example, if there is less flints than the amount of flints that the spawn ratios try to aim for, we increase flint spawns by up to 50% depending on how much behind the spawns are
        */
        if this.itemPool != null
            destroy this.itemPool

        this.itemPool = new Pool<int>()
        var totalWeight = 0.
        for i in itemSpawnInfoMap
            totalWeight += itemSpawnInfoMap.get(i).getTimeAdjustedSpawnWeight()

        for itm in itemSpawnInfoMap
            let spawnInfo = itemSpawnInfoMap.get(itm)
            let itemWeight = spawnInfo.getTimeAdjustedSpawnWeight()
            let weightRatio = itemWeight / totalWeight
            var adjustmentRatio = 1.
            if this.itemsSpawnedTotal != 0
                let itemRatio = this.spawnsPerType.get(itm) / this.itemsSpawnedTotal
                let difference = (weightRatio-itemRatio).abs()
                if itemRatio < weightRatio
                    adjustmentRatio = linear(1, 1.5, max(1, (difference / 0.5)))
                else if itemRatio > weightRatio
                    adjustmentRatio = linear(1, 0.5, max(1, (difference / 0.5)))

            this.itemPool.add((itemWeight * adjustmentRatio).round(), spawnInfo.id)

    ondestroy
        if this.spawnRegionsPool != null
            destroy this.spawnRegionsPool
        destroy this.spawnsPerType

public function handleItemSpawning()
    for island in islandSpawnList
        island.generateItemPool()

    let spawnedCount = new HashMap<IslandSpawner, int>()

    //Use animation period * 2 since the old system was WAY slower than this
    doPeriodically(ANIMATION_PERIOD*2) spawnLoop ->
        //Spawn one item on each island until they all reached the max count
        bool loopDidSpawn = false
        for island in islandSpawnList
            //Abruptly stop loop if item limit reached
            if (udg_ITEM_LIMIT_MODE and udg_ITEM_CURRENT >= udg_ITEM_MAX)
                loopDidSpawn = false
                break

            //Spawn item if island-specific spawn count is not yet achieved
            if not spawnedCount.has(island) or (spawnedCount.get(island) < island.itemsSpawnCount * udg_ITEM_BASE)
                island.spawnItemFromPool()
                udg_ITEM_CURRENT += 1
                spawnedCount.put(island, spawnedCount.get(island) + 1)
                loopDidSpawn = true

        //Return when all islandSpawners iterated without any spawns (or abrupt stop condition reached)
        if not loopDidSpawn
            destroy spawnedCount
            destroy spawnLoop //Break


public function handleAnimalSpawning()
    for island in islandSpawnList
        island.generateAnimalPool()

    let spawnedCount = new HashMap<IslandSpawner, int>()

    //Use animation period * 2 since the old system was WAY slower than this
    doPeriodically(ANIMATION_PERIOD*2) spawnLoop ->
        //Spawn one animal on each island until they all reached the max count
        bool loopDidSpawn = false
        for island in islandSpawnList
            //Abruptly stop loop if unit limit reached
            //The check uses udg_item_limit_mode too
            if (udg_ITEM_LIMIT_MODE and udg_ANIMAL_CURRENT >= udg_ANIMAL_MAX)
                loopDidSpawn = false
                break

            //Spawn item if island-specific spawn count is not yet achieved
            if not spawnedCount.has(island) or (spawnedCount.get(island) < island.animalsSpawnCount * udg_FOOD_BASE)
                island.spawnAnimalFromPool()
                udg_ANIMAL_CURRENT += 1
                spawnedCount.put(island, spawnedCount.get(island) + 1)
                loopDidSpawn = true

        //Return when all islandSpawners iterated without any spawns (or abrupt stop condition reached)
        if not loopDidSpawn
            destroy spawnedCount
            destroy spawnLoop //Break

function initItemSpawnInfo()
    new ItemSpawnInfo(ITEM_TINDER, 500, 70, 860)
    new ItemSpawnInfo(ITEM_CLAY_BALL, 100, 185, 340)
    new ItemSpawnInfo(ITEM_STICK, 300, 450, 360)
    new ItemSpawnInfo(ITEM_FLINT, 300, 222, 300)
    new ItemSpawnInfo(ITEM_MANA_CRYSTAL, 0, 160, 384)
    new ItemSpawnInfo(ITEM_STONE, 100, 3330, 552)
    new ItemSpawnInfo(ITEM_MUSHROOM, 0, 120, 360)
    new ItemSpawnInfo(ITEM_MAGIC, 25, 25, 1)

function initAnimalSpawnInfo()
    new AnimalSpawnInfo(UNIT_ELK, 1000, 1000, 0)

    //Hostiles dont spawn during first wave, therefore we have the change time for them
    new AnimalSpawnInfo(UNIT_SNAKE, 0, 100, 115)
    new AnimalSpawnInfo(UNIT_JUNGLE_WOLF, 0, 200, 115)
    new AnimalSpawnInfo(UNIT_JUNGLE_BEAR, 0, 100, 115)
    new AnimalSpawnInfo(UNIT_PANTHER, 0, 100, 115)

    animalSpawnFunctions.put(UNIT_ELK, (x, y) -> new Elk(vec2(x,y)))
    animalSpawnFunctions.put(UNIT_SNAKE, (x, y) -> new Snake(vec2(x,y)))
    animalSpawnFunctions.put(UNIT_JUNGLE_WOLF, (x, y) -> new Wolf(vec2(x,y)))
    animalSpawnFunctions.put(UNIT_JUNGLE_BEAR, (x, y) -> new Bear(vec2(x,y)))
    animalSpawnFunctions.put(UNIT_PANTHER, (x, y) -> new Panther(vec2(x,y)))

function initIslandSpawners()
    let NWSpawns = new Pool<rect>()
    NWSpawns.add(udg_ISLAND1_1, gg_rct_spawn_area_1_1)
    NWSpawns.add(udg_ISLAND1_2, gg_rct_spawn_area_1_2)
    NWSpawns.add(udg_ISLAND1_3, gg_rct_spawn_area_1_3)
    new IslandSpawner(udg_NORTH_LEFT_ITEM, udg_NORTH_LEFT_FOOD, NWSpawns)

    let NESpawns = new Pool<rect>()
    NESpawns.add(udg_ISLAND2_1, gg_rct_spawn_area_2_1)
    NESpawns.add(udg_ISLAND2_2, gg_rct_spawn_area_2_2)
    NESpawns.add(udg_ISLAND2_3, gg_rct_spawn_area_2_3)
    new IslandSpawner(udg_NORTH_RIGHT_ITEM, udg_NORTH_RIGHT_FOOD, NESpawns)

    let SESpawns = new Pool<rect>()
    SESpawns.add(udg_ISLAND3_1, gg_rct_spawn_area_3_1)
    SESpawns.add(udg_ISLAND3_2, gg_rct_spawn_area_3_2)
    SESpawns.add(udg_ISLAND3_3, gg_rct_spawn_area_3_3)
    new IslandSpawner(udg_SOUTH_RIGHT_ITEM, udg_SOUTH_RIGHT_FOOD, SESpawns)

    let SWSpawns = new Pool<rect>()
    SWSpawns.add(udg_ISLAND4_1, gg_rct_spawn_area_4_1)
    SWSpawns.add(udg_ISLAND4_2, gg_rct_spawn_area_4_2)
    SWSpawns.add(udg_ISLAND4_3, gg_rct_spawn_area_4_3)
    new IslandSpawner(udg_SOUTH_LEFT_ITEM, udg_SOUTH_LEFT_FOOD, SWSpawns)

function startSpawnCycle()
    doPeriodically(120) spawnerCallback ->
        handleAnimalSpawning()
        doAfter(5) ->
            handleItemSpawning()
            adjustBaseSpawnrates()

    //Another loop because 1 spawned fish per area per 240 seconds is hard to cut in half
    doPeriodically(240) fishHawkCallback ->
        //Delays so these dont happen exactly at the same time as the 120 second interval actions
        doAfter(10, -> addItemsToBushes())
        doAfter(15, -> spawnFishAndHawks())

function adjustBaseSpawnrates()
    //Replacement for modStats function of the legacy system.
    //Dynamically adjust overall item & animal amounts (and specific item type amounts but thats handles by spawnInfo classes now)
    udg_ITEM_BASE = max(.15,udg_ITEM_BASE-0.2)
    udg_FOOD_BASE = max(.15,udg_FOOD_BASE-0.2)

function spawnFishOrHawk(rect spawnRect)
    if udg_ITEM_LIMIT_MODE and udg_FISH_CURRENT < udg_FISH_MAX
        let rand = GetRandomInt(1, 13)
        let spawnPoint = spawnRect.randomPoint()
        if rand <= 4
            new Hawk(spawnPoint)
        else if rand <= 6
            new GreenFish(spawnPoint)
        else
            new Fish(spawnPoint)
        udg_FISH_CURRENT += 1

function spawnFishAndHawks()
    doPeriodicallyCounted(ANIMATION_PERIOD * 2, FISH_PER_AREA) fishHawkLoop ->
        spawnFishOrHawk(gg_rct_out_1_1)
        spawnFishOrHawk(gg_rct_out_3_2)
        spawnFishOrHawk(gg_rct_out_1_2)
        spawnFishOrHawk(gg_rct_out_5_2)
        spawnFishOrHawk(gg_rct_out_2_2)
        spawnFishOrHawk(gg_rct_out_3_1)
        spawnFishOrHawk(gg_rct_out_4_1)
        spawnFishOrHawk(gg_rct_out_4_2)
        spawnFishOrHawk(gg_rct_our_5_1)
        spawnFishOrHawk(gg_rct_out_2_1)
        spawnFishOrHawk(gg_rct_fish_new_2)
        spawnFishOrHawk(gg_rct_fish_new_3)
        spawnFishOrHawk(gg_rct_fish_new_1)
        spawnFishOrHawk(gg_rct_fish_new_4)
        spawnFishOrHawk(gg_rct_fish_new_6)
        spawnFishOrHawk(gg_rct_fish_new_5)
        spawnFishOrHawk(gg_rct_fish_new_7)
        spawnFishOrHawk(gg_rct_fish_new_8)

init
    initIslandWeights()
    initSpawnRegionWeights()
    initItemSpawnInfo()
    initAnimalSpawnInfo()
    initIslandSpawners()

    registerGameStartEvent() ->
        gameStartTime = getElapsedGameTime()

        //First spawns. Do twice to retain original amounts after halving the items per spawn and doubling cycle frequency.
        handleAnimalSpawning()
        doAfter(5, -> handleAnimalSpawning())
        doAfter(10, -> handleAnimalSpawning())
        doAfter(15, -> handleItemSpawning())
        doAfter(20, -> handleItemSpawning())
        doAfter(25, -> spawnFishAndHawks())
        doAfter(30, -> addItemsToBushes())
        startSpawnCycle()
