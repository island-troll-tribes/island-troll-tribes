package TwinIslandPotion
import ClosureEvents
import AbilityObjEditing
import DummyCaster
import Orders
import ClosureTimers
import ClosureForGroups
import BuffObjEditing
import LocalObjectIDs
import UnitExtensions

@compiletime function createBuff()
    new BuffDefinition('XTWB', 'Bult')
    ..setName(1, "Twin Islands")
    ..setIcon("ReplaceableTextures\\CommandButtons\\BTNPotionGreenSmall.blp")
    ..setIconNormal(1, "ReplaceableTextures\\CommandButtons\\BTNPotionGreenSmall.blp")
    ..setTooltipNormal(1, "Revealed")
    ..setTooltipNormalExtended(1, "Revealed for 30 seconds.")

@compiletime function createVisionSpell()
    new AbilityDefinitionFaerieFire('XTWN')
    ..setDurationHero(1, 30)
    ..setDurationNormal(1, 30)
    ..setDefenseReduction(1, 0)
    ..setHeroAbility(false)
    ..setItemAbility(false)
    ..setCastRange(1, 999999)
    ..setCastingTime(1, 0)
    ..setArtEffect("")
    ..setArtCaster("")
    ..setArtTarget("")
    ..setCooldown(1, 0)
    ..setManaCost(1, 0)
    ..setLevels(1)
    ..setBuffs(1, "XTWB")

function onCast()
    let caster = GetSpellAbilityUnit()
    let owner = caster.getOwner()
    let casterPos = caster.getPos()
    forUnitsAll() u ->
        if u.getOwner().isEnemyOf(owner) and u.isAlive()
            if u.isTroll()
                PingMinimapForForceEx(GetPlayersAllies(owner), u.getPos().x, u.getPos().y, 8, bj_MINIMAPPINGSTYLE_ATTACK, 255, 0, 0)

                //Temporary vision required by dummy
                var vis = createVision(owner, u.getPos(), 200, true)
                vis.start()
                doAfter(0.2) ->
                    vis.destr()
                new DummyCaster()
                    ..owner(owner)
                    ..origin(casterPos)
                    ..castTarget('XTWN', 1, Orders.faeriefire, u)
            else if u.getTypeId() == UNIT_SPIRIT_WARD
                PingMinimapForForceEx(GetPlayersAllies(owner), u.getPos().x, u.getPos().y, 8, bj_MINIMAPPINGSTYLE_SIMPLE, 0, 0, 255)

init
    registerSpellEffectEvent('A03F', () -> onCast())
