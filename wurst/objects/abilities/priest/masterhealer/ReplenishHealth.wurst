package ReplenishHealth

// Standard library imports:
import AbilityObjEditing
import Assets
import BuffObjEditing
import ObjectIdGenerator
import ObjectIds
import Orders
import RegisterEvents

// Third-party imports:
import Lodash

// Local imports:
import ColorUtils
import LocalObjectIDs
import ToolTipsUtils


let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let ORIGINAL_BUFF_ID = 'BEim' // Immolation

let ABILITY_REPLENISH_HEALTH_EFFECT = compiletime(ABIL_ID_GEN.next())
let AMOUNT_HP_HEALED = 5.0
let AOE = 500.
let TARGET_ALLOWED = "air,allies,friend,ground,invulnerable,organic,self,vulnerable"

let COOLDOWN = 5.0
let MANA_DRAINED = 5.0
let MANACOST = 20
let TOOLTIP_NORM = "Replenish Health"
let TOOLTIP_EXTENDED = (
    "{0}Sustained manacost:|r {1} per second |n|n"+
    "Replenishes health to the Master Healer and nearby allies, healing {2} health per second. "+
    "|nDrains mana until deactivated."
).format(
    GOLD_COLOR.toColorString(),
    MANA_DRAINED.toToolTipLightBlue(),
    AMOUNT_HP_HEALED.toToolTipGreen()
)

function createBuff()
    new BuffDefinition(BUFF_ID, ORIGINAL_BUFF_ID)
    ..setIcon(Icons.bTNHolyBolt)
    ..setArtSpecial(1, "")
    ..setArtTarget(1, Abilities.aNrmTarget)
    ..setEffectSoundLooping(1, Sounds.fountainOfLifeLoop1)
    ..setTooltipNormal(1, TOOLTIP_NORM)
    ..setTooltipNormalExtended(1, "This unit is replenishing the health of nearby units")

class ReplenishHealth extends AbilityDefinitionAuraRegenerationHealingWard
    construct(int newAbilityId)
        super(newAbilityId)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> TOOLTIP_NORM)
        this.setAmountofHitPointsRegenerated(1, AMOUNT_HP_HEALED)
        this.setPercentage(1, false)
        this.setAreaofEffect(1, AOE)
        this.setTargetsAllowed(1, TARGET_ALLOWED)

class ReplenishHealthDummy extends AbilityDefinitionImmolationcreep
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setCheckDependencies(false)
        this.setDamageperInterval(1, 0)
        this.setManaDrainedperSecond(1, MANA_DRAINED)
        this.setBufferManaRequired(1, 0)
        this.setIconNormal(Icons.bTNHolyBolt)
        this.setIconTurnOff(Icons.dISBTNHolyBolt)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setBuffs(1, toRawCode(BUFF_ID))
        this.setTargetsAllowed(1, "")
        this.setCooldown(1, COOLDOWN)
        this.setAreaofEffect(1, 1)
        this.setHeroAbility(false)
        this.setLevels(1)
        this.setAreaofEffect(1, 1)
        this.setManaCost(1, MANACOST)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, "Activate "+TOOLTIP_NORM))
        this.setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        this.setTooltipLearnExtended(TOOLTIP_EXTENDED)
        this.setTooltipTurnOffExtended(1, TOOLTIP_EXTENDED)
        this.setTooltipTurnOff(1, makeToolTipNorm(hotkey, "Deactivate "+TOOLTIP_NORM))
        this.setHotkeyNormal(hotkey)
        this.setHotkeyTurnOff(hotkey)


@compiletime function createReplenishHealth()
    new ReplenishHealthDummy(ABILITY_REPLENISH_HEALTH, "D", new Pair(3, 1))
    new ReplenishHealth(ABILITY_REPLENISH_HEALTH_EFFECT)
    createBuff()


function onCast()
    let caster = GetTriggerUnit()
    if not caster.hasAbility(ABILITY_REPLENISH_HEALTH)
        return

    switch GetIssuedOrderId()
        case Orders.immolation
            caster.addAbility(ABILITY_REPLENISH_HEALTH_EFFECT)
        case Orders.unimmolation
            caster.removeAbility(ABILITY_REPLENISH_HEALTH_EFFECT)

function onDeath()
    let caster = GetTriggerUnit()
    if caster.hasAbility(ABILITY_REPLENISH_HEALTH_EFFECT)
        caster.removeAbility(ABILITY_REPLENISH_HEALTH_EFFECT)

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_ORDER, function onCast)
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_DEATH, function onDeath)
