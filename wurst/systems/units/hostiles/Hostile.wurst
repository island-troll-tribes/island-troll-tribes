package Hostile
import LinkedList
import UnitEntity

public function createCorpse(player p, int unitId, vec2 pos, angle facing) returns unit
    return CreateCorpse(p, unitId, pos.x, pos.y, facing.degrees())

public abstract class Hostile extends UnitEntity
    protected constant let drops = new LinkedList<int>()

    construct(unit whichUnit)
        super(whichUnit)

    construct(player whichPlayer, vec2 pos)
        super(whichPlayer, pos)

    construct(player whichPlayer, vec2 pos, angle facing)
        super(whichPlayer, pos, facing)

    function addDrop(int itemId)
        drops.add(itemId)

    function getNumCorpses() returns int
        return 1

    override function onDeath()
        let pos = getPos()
        let numCorpses = getNumCorpses()

        for i = 1 to numCorpses
            createCorpse(players[PLAYER_NEUTRAL_AGGRESSIVE], UNIT_MEAT, pos, randomAngle())

        let iter = drops.iterator()
        for drop from iter
            createItem(drop, pos)
        iter.close()

        super.onDeath()


init
    Hostile.initialize()
