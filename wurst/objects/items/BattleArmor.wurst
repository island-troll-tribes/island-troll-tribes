package BattleArmor

// Standard library imports:
import AbilityObjEditing
import Assets
import ClosureForGroups
import DummyCaster
import ItemObjEditing
import ObjectIdGenerator
import ObjectIds
import Orders
import RegisterEvents

// Local imports:
import LocalObjectIDs
import ToolTipsUtils
import UnitExtensions

constant COOLDOWN = 75.
constant ABILITY_RADIUS = 800.
constant ABILITY_ANTIMAGIC = 'A0F8'
constant ABILITY_DAMAGE_REDUCTION = 'Alsr'
constant ABILITY_BATTLE_GEAR_STAT_BONUS = 'A0E7'
constant ABILITY_DUMMY_ID = compiletime(ABIL_ID_GEN.next())
constant ITEM_ABILITIES = commaList(ABILITY_DUMMY_ID, ABILITY_BATTLE_GEAR_STAT_BONUS, ABILITY_ARMOR_BONUS7)

constant MODEL = "Models\\Armor.mdx"
constant TOOLTIP = "Battle Armor"
public constant BATTLE_ARMOR_TOOLTIP_EXTENDED = "Increase all stats by 4 and armor by 7, provide 20% resistance against spell, can cast Antimagic on surrounding allies."
                            +makeToolTipCooldown(COOLDOWN)

@compiletime function createDummyAbility()
    new AbilityDefinitionWindWalk(ABILITY_DUMMY_ID)
    ..setArtCaster("")
    ..setArtEffect("")
    ..setArtTarget("")
    ..setArtSpecial("")
    ..setHeroAbility(false)
    ..setItemAbility(true)
    ..setLevels(1)
    ..setHotkeyNormal("A")
    ..setName(TOOLTIP + " Antimagic")
    ..setTooltipNormal(1, "dummy spell")
    ..setTooltipNormalExtended(1, "dummy spell")
    ..setCooldown(1, COOLDOWN)
    ..setManaCost(1, 0)
    ..setAnimationNames("")
    ..setDurationHero(1, 0.01)
    ..setCastingTime(1, 0)
    ..setDurationNormal(1, 0.01)
    ..setEffectSound("")

@compiletime function createBattleArmor()
    new ItemDefinition(ITEM_BATTLE_ARMOR, 'bspd')
    ..setName(TOOLTIP)
    ..setAbilities(ITEM_ABILITIES)
    ..setTooltipExtended(BATTLE_ARMOR_TOOLTIP_EXTENDED)
    ..setModelUsed(MODEL)
    ..setGoldCost(0)
    ..setInterfaceIcon(Icons.bTNRobeOfTheMagi)
    ..setDescription(BATTLE_ARMOR_TOOLTIP_EXTENDED)
    ..setDroppedWhenCarrierDies(true)
    ..setTintingColor2Green(80)
    ..setTintingColor3Blue(80)
    ..setScalingValue(1.50)
    ..setActivelyUsed(true)
    ..setCooldownGroup("Item Move Speed Bonus")
    ..setHitPoints(75)
    ..setLumberCost(68)
    ..setLevelUnclassified(2)
    ..setPerishable(false)
    ..setPriority(41)
    ..setTooltipBasic("Trade for "+TOOLTIP)

function onCast()
    var caster = GetSpellAbilityUnit()

    forUnitsInRange(caster.getPos(), ABILITY_RADIUS) u ->
        if u.isTroll() and u.getOwner().isAllyOf(caster.getOwner())
            new DummyCaster()
            ..owner(caster.getOwner())
            ..origin(u.getPos() + vec2(100, 0))
            ..castTarget(ABILITY_ANTIMAGIC, 1, Orders.antimagicshell, u)
init
    registerSpellEffectEvent(ABILITY_DUMMY_ID, () -> onCast())
