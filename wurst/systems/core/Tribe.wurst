package Tribe
import LinkedList
import HashMap
import TribeUtils
import Lodash
import TrollExtensions

public class Tribe
    static let playerToTribeMap = new HashMap<player, Tribe>()
    static let tribes = new LinkedList<Tribe>()

    static Predicate<Tribe> isNotDefeated = tribe -> not tribe.isDefeated()
    static Predicate<player> memberIsDead = member -> not member.getTroll().isAlive()
    static Predicate<Tribe> hasNoAliveMembers = tribe -> tribe.getMembers().every(memberIsDead)

    int tribeId
    rect spawn
    let tribePlayers = new LinkedList<player>()
    let tribe = CreateForce()
    var defeated = true

    static function getTribes() returns LinkedList<Tribe>
        return tribes

    static function ofPlayer(player query) returns Tribe
        return playerToTribeMap.get(query)

    construct(int id)
        tribeId = id

        let range = getPlayerIdRangeForTribe(tribeId)
        for pid = range.min to range.max
            let member = players[pid]
            if member.isIngame() and not isobserver[pid]
                tribePlayers.push(member)
                playerToTribeMap.put(member, this)
                tribe.addPlayer(member)

                defeated = false

        tribePlayers.forEach() a ->
            tribePlayers.forEach() b ->
                if a != b
                    SetPlayerAllianceStateBJ(a, b, bj_ALLIANCE_ALLIED_VISION)

        tribes.push(this)

        // TODO remove legacy code here
        TEAM[tribeId + 1] = tribe

    function getId() returns int
        return tribeId

    function isDefeated() returns bool
        return defeated

    function makePlayersObservers()
        getMembers().forEach() member ->
            member.setState(PLAYER_STATE_OBSERVER, 1)

    function wasDefeated()
        defeated = true

        if tribe.containsPlayer(GetLocalPlayer())
            print(HIGHLIGHT_COLOR + "You have been defeated|r")

        makePlayersObservers()

    function wasVictorious()
        if tribe.containsPlayer(GetLocalPlayer())
            print(HIGHLIGHT_COLOR + "You have won! Please play again!|r")

        makePlayersObservers()

    function getForce() returns force
        return tribe

    function getMembers() returns LinkedList<player>
        return tribePlayers

    function setSpawn(rect _spawn)
        spawn = _spawn

    function getSpawn() returns rect
        return spawn
