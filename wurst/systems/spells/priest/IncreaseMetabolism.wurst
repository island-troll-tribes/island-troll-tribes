package IncreaseMetabolism
import AbilityObjEditing
import ObjectIds
import Assets
import HealingSystem

let ABILITY_ID = 'XIMB'
let BUFF_ID = 'Bblo'
let ICON = Icons.bTNReincarnation
let CAST_RANGE = 700.
let COOLDOWN = 30.
let MANACOST = 20
let MS_FACTOR = 0.2
let AS_FACTOR = 0.
let DURATION = 20.
let CAST_TIME = 0.2
let TOOLTIP_NORM = "Increase M|cffffcc00e|rtabolism"
let TOOLTIP_EXT = "Increases the health recovered by food and potions by 50% and boosts movement speed by 20%. Lasts 20 seconds."

@compiletime function createSpell()
    new AbilityDefinitionSlowCreep(ABILITY_ID)
        ..setAttackSpeedFactor(1, -AS_FACTOR)
        ..setMovementSpeedFactor(1, -MS_FACTOR)
        ..setBuffs(1, toRawCode(BUFF_ID))
        ..setIconNormal(ICON)
        ..setIconResearch(ICON)
        ..setIconTurnOff(ICON)
        ..setCastRange(1, CAST_RANGE)
        ..setCastingTime(1, CAST_TIME)
        ..setCooldown(1, COOLDOWN)
        ..setDurationHero(1, DURATION)
        ..setDurationNormal(1, DURATION)
        ..setManaCost(1, MANACOST)
        ..setTargetsAllowed(1, "air,ground,friend,organic,self,neutral")
        ..setName("Increase metabolism")
        ..setHeroAbility(false)
        ..setItemAbility(false)
        ..setLevels(1)
        ..setHotkeyNormal("E")
        ..setHotkeyLearn("E")
        ..setHotkeyTurnOff("E")
        ..presetTooltipNormal(lvl -> TOOLTIP_NORM)
        ..presetTooltipNormalExtended(lvl -> TOOLTIP_EXT)

init
    onUnitHealed() ->
        let instance = getHealingInstance()
        if instance.target.hasAbility(BUFF_ID)
            instance.increaseMultiplier(0.5)
