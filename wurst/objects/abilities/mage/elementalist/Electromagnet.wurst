package Electromagnet
import RegisterEvents
import ClosureTimers
import ClosureForGroups
import LocalObjectIDs

@configurable constant int ABILITY_ID = 'A04N'
@configurable constant real MAX_PULL_SPEED = 400
@configurable constant real PULL_RADIUS = 700
@configurable constant real MIN_PULL_DISTANCE = 80
@configurable constant real INTERVAL = 0.03125000
@configurable constant real DURATION = 1.00

constant SPEED = MAX_PULL_SPEED * INTERVAL
constant RADIUS = PULL_RADIUS * PULL_RADIUS // avoid square root
constant MIN_DISTANCE = MIN_PULL_DISTANCE * MIN_PULL_DISTANCE // avoid square root

function onCast()
    var caster = GetSpellAbilityUnit()
    var owner = caster.getOwner()
    var pos = caster.getPos()

    doPeriodicallyTimed(INTERVAL, DURATION) cb ->
        forUnitsInRange(pos, PULL_RADIUS) u ->
            var id = u.getTypeId()
            if u.isAlive() and u.getOwner().isEnemyOf(owner)
                    and not (u.isType(UNIT_TYPE_STRUCTURE) or u.isType(UNIT_TYPE_FLYING))
                    and not (id == UNIT_MAMMOTH or id == UNIT_DISCO_DUCK)

                var upos = u.getPos()
                var dvec = pos - upos
                var distance = dvec.dot(dvec)

                if distance > MIN_DISTANCE
                    var speed = max((distance / RADIUS), 0.35) * SPEED
                    var resultPos = upos.polarOffset(upos.angleTo(pos), speed)
                    u.setXY(resultPos)

init
    registerSpellEffectEvent(ABILITY_ID, () -> onCast())
