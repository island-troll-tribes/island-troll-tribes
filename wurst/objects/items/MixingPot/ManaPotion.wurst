package ManaPotion

// Standard library imports:
import Assets
import ClosureEvents
import ChannelAbilityPreset

// Local imports:
import Items
import LocalAssets
import ToolTipsUtils
import LocalObjectIDs
import LocalItemObjEditing

let ABIL  = compiletime(ABIL_ID_GEN.next())

let CAST_RANGE = 500.
let MANA_RESTORED = 60.

let ICON  = Icons.bTNPotionBlueSmall
let MODEL = LocalItems.potionBlueLesser
let TT    = "A dilute potion made from Athelas Seed. Instantly restores {0} Mana point to a target ally.".format(MANA_RESTORED.toToolTipLightBlue())

let TARGET_ALLOWED = "friend,ground,hero,invulnerable,organic,self,vulnerable"

@compiletime function createAbil() returns ChannelAbilityPreset
    return createItemChannelCast(ABIL)
        ..setName("Mana Potion")
        ..presetTargetTypes(Targettype.UNIT)
        ..setTargetsAllowed(1, TARGET_ALLOWED)
        ..setCastRange(1, CAST_RANGE)
        ..setManaCost(1, 0)
        ..setCooldown(1, 0)

@compiletime function createManaPotion() returns ItemDefinition
    return createPerishableItem(ITEM_MANA_POTION)
        ..setNameEnhance("Mana Potion")
        ..setInterfaceIcon(ICON)
        ..setModelUsed(MODEL)
        ..setHotkey("W")
        ..setTooltipExtended(TT)
        ..setLumberCost(5)
        ..setAbilities(commaList(ABIL))
        ..setScalingValue(0.7)
        ..setCooldownGroup("A002")
        ..setStockReplenishInterval(120)
        ..setNumberofCharges(1)

init
    EventListener.onTargetCast(ABIL) (unit caster, unit target) ->
        target.addMana(MANA_RESTORED)
        flashEffect(Abilities.aImaTarget, target, "origin")
