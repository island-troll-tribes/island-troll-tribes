package TrollUpgrade

// Standard library imports:
import EventHelper
import HashMap
import LinkedList
import RegisterEvents

// Third-party imports
import Lodash

// Local imports:
import AutoSkill
import Experience
import LocalObjectIDs
import Transformation

// TODO: Build this out automatically.
public constant trollUpgradeMap = new HashMap<int, int>()
    ..put(ABILITY_WARRIOR,          UNIT_WARRIOR)
    ..put(ABILITY_TRACKER,          UNIT_TRACKER)
    ..put(ABILITY_JUGGERNAUT,       UNIT_JUGGERNAUT)
    ..put(ABILITY_ELEMENTALIST,     UNIT_ELEMENTALIST)
    ..put(ABILITY_HYPNOTIST,        UNIT_HYPNOTIST)
    ..put(ABILITY_DEMENTIA_MASTER,  UNIT_DEMENTIA_MASTER)
    ..put(ABILITY_BOOSTER,          UNIT_BOOSTER)
    ..put(ABILITY_MASTER_HEALER,    UNIT_MASTER_HEALER)
    ..put(ABILITY_SAGE,             UNIT_SAGE)
    ..put(ABILITY_DRUID,            UNIT_DRUID)
    ..put(ABILITY_SHAPESHIFTER,     UNIT_SHAPESHIFTER_WOLF)
    ..put(ABILITY_JUNGLE_TYRANT,    UNIT_JUNGLE_TYRANT)
    ..put(ABILITY_ESCAPE_ARTIST,    UNIT_ESCAPE_ARTIST)
    ..put(ABILITY_CONTORTIONIST,    UNIT_CONTORTIONIST)
    ..put(ABILITY_ASSASSIN,         UNIT_ASSASSIN)
    ..put(ABILITY_OBSERVER,         UNIT_OBSERVER)
    ..put(ABILITY_TRAPPER,          UNIT_TRAPPER)
    ..put(ABILITY_SPY,              UNIT_SPY)
    ..put(ABILITY_RADAR_GATHERER,   UNIT_RADAR_GATHERER)
    ..put(ABILITY_HERB_MASTER,      UNIT_HERB_MASTER)
    ..put(ABILITY_OMNIGATHERER,     UNIT_OMNIGATHERER)

let transformations = new LinkedList<Pair<int, int>>()
    // Hunter
    ..add(new Pair(UNIT_HUNTER, UNIT_WARRIOR))
    ..add(new Pair(UNIT_HUNTER, UNIT_TRACKER))
    ..add(new Pair(UNIT_HUNTER, UNIT_JUGGERNAUT))
    ..add(new Pair(UNIT_WARRIOR, UNIT_JUGGERNAUT))
    ..add(new Pair(UNIT_TRACKER, UNIT_JUGGERNAUT))
    // Beastmaster
    ..add(new Pair(UNIT_BEAST_MASTER, UNIT_SHAPESHIFTER_WOLF))
    ..add(new Pair(UNIT_BEAST_MASTER, UNIT_DRUID))
    ..add(new Pair(UNIT_BEAST_MASTER, UNIT_JUNGLE_TYRANT))
    ..add(new Pair(UNIT_SHAPESHIFTER_WOLF, UNIT_JUNGLE_TYRANT))
    ..add(new Pair(UNIT_DRUID, UNIT_JUNGLE_TYRANT))
    // Priest
    ..add(new Pair(UNIT_PRIEST, UNIT_BOOSTER))
    ..add(new Pair(UNIT_PRIEST, UNIT_MASTER_HEALER))
    ..add(new Pair(UNIT_PRIEST, UNIT_SAGE))
    ..add(new Pair(UNIT_BOOSTER, UNIT_SAGE))
    ..add(new Pair(UNIT_MASTER_HEALER, UNIT_SAGE))
    // Mage
    ..add(new Pair(UNIT_MAGE, UNIT_ELEMENTALIST))
    ..add(new Pair(UNIT_MAGE, UNIT_HYPNOTIST))
    ..add(new Pair(UNIT_MAGE, UNIT_DEMENTIA_MASTER))
    ..add(new Pair(UNIT_ELEMENTALIST, UNIT_DEMENTIA_MASTER))
    ..add(new Pair(UNIT_HYPNOTIST, UNIT_DEMENTIA_MASTER))
    // Scout
    ..add(new Pair(UNIT_SCOUT, UNIT_TRAPPER))
    ..add(new Pair(UNIT_SCOUT, UNIT_OBSERVER))
    ..add(new Pair(UNIT_SCOUT, UNIT_SPY))
    ..add(new Pair(UNIT_TRAPPER, UNIT_SPY))
    ..add(new Pair(UNIT_OBSERVER, UNIT_SPY))
    // Gatherer
    ..add(new Pair(UNIT_GATHERER, UNIT_RADAR_GATHERER))
    ..add(new Pair(UNIT_GATHERER, UNIT_HERB_MASTER))
    ..add(new Pair(UNIT_GATHERER, UNIT_OMNIGATHERER))
    ..add(new Pair(UNIT_RADAR_GATHERER, UNIT_OMNIGATHERER))
    ..add(new Pair(UNIT_HERB_MASTER, UNIT_OMNIGATHERER))
    // Thief
    ..add(new Pair(UNIT_THIEF, UNIT_CONTORTIONIST))
    ..add(new Pair(UNIT_THIEF, UNIT_ESCAPE_ARTIST))
    ..add(new Pair(UNIT_THIEF, UNIT_ASSASSIN))
    ..add(new Pair(UNIT_CONTORTIONIST, UNIT_ASSASSIN))
    ..add(new Pair(UNIT_ESCAPE_ARTIST, UNIT_ASSASSIN))

// Generate the transformations during compilation.
let transformationIDs = compiletime(registerTransformations(transformations))

function upgradeUnit(unit origin, int targetID)
    // Save the statistics for the unit for restoration after level reduction.
    // TODO: Avoid level resetting.
    let str = origin.getStr(false)
    let agi = origin.getAgi(false)
    let inT = origin.getInt(false)

    // Once the unit transform into a subclass, medallion of courage is researched
    // this way the super sub spell gets unlocked
    SetPlayerTechResearched(origin.getOwner(), UPGD_MEDALLION_COURAGE, 1)

    // Transform the unit.
    transformUnit(origin, targetID)

    // Reset the experience for the unit.
    // TODO: Avoid level resetting.
    origin.removeLevels(30)
    // TODO: Remove this once compound requirements for abilities are fixed.
    unlockResearch(origin)

    // Restore the statistics.
    // TODO: Avoid level resetting.
    origin.setStr(str)
    origin.setAgi(agi)
    origin.setInt(inT)

    // Apply autoskilling with the corrected level.
    // TODO: Remove level correction and use a transformation callback.
    // TODO: Move this to AutoSkill package.
    autoSkill(origin)

    // Exit out of the ability menu.
    if GetLocalPlayer() == origin.getOwner()
        ForceUICancel()

function onCast(unit origin, int abilityID)
    // Look up the transformation denoted by the learned ability.
    let targetID = trollUpgradeMap.get(abilityID)

    // Exit if the skill does not mark an upgrade.
    if targetID == 0
        return

    // Perform the upgrade after allowing casting to finish.
    upgradeUnit(origin, targetID)

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_FINISH) ->
        onCast(
            EventData.getSpellAbilityUnit(),
            EventData.getSpellAbilityId()
        )

    // Register the transformations at runtime.
    registerTransformations(transformations, transformationIDs, false)
