package OraclePotion

import AbilityObjEditing
import AbilityObjectCreationUtils
import BuffObjEditing
import PotionsDefinition
import RegisterEvents
import HashMap
import TimerUtils
import ObjectIds
import ClosureTimers
import ID
import Assets
import ToolTipsUtils

constant ABILITY_ID = compiletime(ABIL_ID_GEN.next())
constant ABILITY_DUMMY_ID = compiletime(ABIL_ID_GEN.next())

constant ICON = Icons.bTNGreaterInvisibility
constant MODEL = "Models\\Items\\PotionOracle.mdx"

constant BUFF_ID = compiletime(BUFF_ID_GEN.next())
constant BUFF_AURA_ID = compiletime(ABIL_ID_GEN.next())
constant SPELLBOOK_ABILITY_ID = compiletime(createHiddenSpellbookWithAbilities("{0},{1}".format(toRawCode(ABILITY_ID), toRawCode(BUFF_AURA_ID))))

constant DURATION = 20.
constant COOLDOWN = 20.

constant TT = "The consumer of this potion gain true sight, allowing him to see stealthed unit. Good versus thief & vision ward." + makeToolTipDuration(DURATION, COOLDOWN)

@compiletime function createAbilities()
    new BuffDefinition(BUFF_ID, 'Bstt')
    ..setArtTarget(1, "")
    ..setTooltipNormal(1, "True Sight")
    ..setTooltipNormalExtended(1, "This unit can see invisible unit.")
    ..setIcon(Icons.pASBTNMagicalSentry)

    new AbilityDefinitionWindWalk(ABILITY_DUMMY_ID)
    ..setArtCaster("")
    ..setArtEffect("")
    ..setArtTarget("")
    ..setArtSpecial("")
    ..setIconNormal(Icons.bTNGreaterInvisibility)
    ..setIconResearch(Icons.bTNGreaterInvisibility)
    ..setIconTurnOff(Icons.bTNGreaterInvisibility)
    ..setHeroAbility(false)
    ..setItemAbility(true)
    ..setLevels(1)
    ..setHotkeyNormal("A")
    ..setName("TrueSight")
    ..setTooltipNormal(1, "dummy spell")
    ..setTooltipNormalExtended(1, "dummy spell")
    ..setCooldown(1, COOLDOWN)
    ..setManaCost(1, 0)
    ..setAnimationNames("")
    ..setDurationHero(1, 0.01)
    ..setCastingTime(1, 0)
    ..setDurationNormal(1, 0.01)
    ..setEffectSound("")

    new AbilityDefinitionAuraSlow(BUFF_AURA_ID)
    ..setArtTarget(Abilities.magicSentryCaster)
    ..setTargetAttachmentPoint("overhead")
    ..setMovementSpeedFactor(1, 0)
    ..setAreaofEffect(1, 0)
    ..setBuffs(1, toRawCode(BUFF_ID))
    ..setDurationHero(1, DURATION)
    ..setDurationNormal(1, DURATION)
    ..setTargetsAllowed(1, "self")
    ..setName("True Sight")

    new AbilityDefinition(ABILITY_ID, 'Atru')
    ..setCastRange(1, 900)
    ..setName("Oracle clarity")
    ..setTooltipNormal(1, "True Sight")
    ..setTooltipNormalExtended(1, "Reveal invisible unit")
    ..setBuffs(1, "Binf")
    ..setItemAbility(true)
    ..setDurationHero(1, DURATION)
    ..setDurationNormal(1, DURATION)
    ..setButtonPositionNormalY(0)
    ..setArtCaster(Abilities.magicSentryCaster)
    ..setCasterAttachmentPoint("overhead")
    ..setCasterAttachments(0)

@compiletime function createOraclePotion()
    new PotionDefinition(ITEM_ORACLE_POTION, ICON, MODEL, "Oracle Potion", "Z", TT, 11, ABILITY_DUMMY_ID.toRawCode(), 1, ABILITY_DUMMY_ID.toRawCode(), 90)..setBtnPos(0, 2)

HashMap<unit, timer> removalTimers = new HashMap<unit,timer>()

function onCast()
    var caster = GetSpellAbilityUnit()

    if removalTimers.has(caster)
        removalTimers.getAndRemove(caster).release()
    caster.addAbility(SPELLBOOK_ABILITY_ID)
    caster.getOwner().setAbilityAvailable(SPELLBOOK_ABILITY_ID, false)

    let t = getTimer()
    t.doAfter(DURATION) ->
        caster.removeAbility(SPELLBOOK_ABILITY_ID)
        caster.removeAbility(BUFF_AURA_ID)
        removalTimers.remove(caster)
    removalTimers.put(caster, t)

init
    registerSpellEffectEvent(ABILITY_DUMMY_ID, () -> onCast())