package MageMasher

import AbilityObjEditing
import ObjectIdGenerator
import ClosureEvents
import DamageType
import ID
import IttDummyCaster
import Orders
import Priest
import Mage
import UnitEntity

let MANABURN_ID = compiletime(ABIL_ID_GEN.next())
let DRAIN_AMOUNT = 6.

@compiletime function createDummyManaburn()
    new AbilityDefinitionManaBurndemon(MANABURN_ID)
        ..setAnimationNames("")
        ..setHeroAbility(false)
        ..setItemAbility(false)
        ..setLevels(1)
        ..presetCooldown(lvl -> 0)
        ..presetBoltDelay(lvl -> 0.15)
        ..presetBoltLifetime(lvl -> 0.4)
        ..presetCastRange(lvl -> 99999)
        ..presetCastingTime(lvl -> 0)
        ..presetMaxManaDrained(lvl -> DRAIN_AMOUNT)

function mageMasherHit(unit attacker, unit target)
    let owner = attacker.getOwner()
    if isValidMasherTarget(owner, target)
        new IttDummyCaster(0.45).castTarget(owner, MANABURN_ID, 1, Orders.manaburn, target, attacker.getPos())

function isValidMasherTarget(player attackOwner, unit target) returns bool
    let ent = UnitEntity.findForUnit(target)
    return target.getOwner().isEnemyOf(attackOwner)
        and target.isAlive()
        and (ent instanceof Mage or ent instanceof Priest)

init
    EventListener.add(EVENT_UNIT_DAMAGED) ->
        let attacker = GetEventDamageSource()
        if attacker.hasItemById(ITEM_MAGE_MASHER) and getDamageType() == DamageType.ATTACK
            mageMasherHit(attacker, GetTriggerUnit())
