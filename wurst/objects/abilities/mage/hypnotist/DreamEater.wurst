package DreamEater
import ClosureEvents
import LocalObjectIDs
import Icons
import Lodash
import ToolTipsUtils
import ChannelAbilityPreset
import Abilities
import SimError

@configurable constant int BUFF_ID = BUFF_HYPNOTIZED

let HP_RESTORED = 50.
let MANA_RESTORED = 30.
let MANA_EATEN = 50.
let DAMAGE = 40.
let MANACOST = 10
let COOLDOWN = 20.

let ANIMATION_NAME = "channel"
let TOOLTIP_NORM = "Dream Eater"
let TOOLTIP_EXTENDED = ("Steals an enemy dream restoring {0} Health Point and {1} Mana. Also reduces enemy's Mana by {2} and deals {3} magic damage. "+
                        "Only works if the enemy is dreaming. Has {4} seconds cooldown.")
                       .format(HP_RESTORED.toToolTipGreen(), MANA_RESTORED.toToolTipTeal(), MANA_EATEN.toToolTipTeal(), DAMAGE.toToolTipRed(),
                        COOLDOWN.toToolTipLightBlue())

let TARGET_ALLOWED = "enemies,ground,hero"

function constructDreamEater(
    int newAbility,
    string hotkey,
    Pair<int , int> buttonPos
) returns AbilityDefinition
    return new ChannelAbilityPreset(newAbility, 1, true)
        ..setName(TOOLTIP_NORM)
        ..setHotkeyNormal(hotkey)
        ..setCooldown(1, COOLDOWN)
        ..setManaCost(1, MANACOST)
        ..setIconNormal(Icons.bTNDevourMagic)
        ..setButtonPositionNormalX(buttonPos.a)
        ..setButtonPositionNormalY(buttonPos.b)
        ..setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        ..setTooltipNormal(1, makeToolTipNorm(hotkey, TOOLTIP_NORM))
        ..setArtDuration(1, 0)
        ..setTargetType(1, 1)
        ..setAnimationNames(ANIMATION_NAME)
        ..setTargetsAllowed(1, TARGET_ALLOWED)

@compiletime function createDreamEater()
    constructDreamEater(ABILITY_DREAM_EATER, "W", new Pair(1, 0))
    constructDreamEater(ABILITY_LEGACY_DREAM_EATER, "D", new Pair(1, 0))


function onTargetCast(unit caster, unit target)
    // Apply the Soul Theft visual effect.
    flashEffect(Abilities.aIsoTarget, target, "overhead")

    // Damage the target.
    UnitDamageTarget(caster, target, DAMAGE, false, false, ATTACK_TYPE_NORMAL, DAMAGE_TYPE_MAGIC, null)
    target.subMana(MANA_EATEN)

    // Heal the caster.
    caster.addHP(HP_RESTORED)
    caster.addMana(MANA_RESTORED)


init
    EventListener.onTargetCast(ABILITY_DREAM_EATER) (unit caster, unit target) ->
        onTargetCast(caster, target)
    EventListener.onTargetCast(ABILITY_LEGACY_DREAM_EATER) (unit caster, unit target) ->
        onTargetCast(caster, target)

    // Cancel the casting if the target is not under the effect of Hypnosis.
    EventListener.add(EVENT_PLAYER_UNIT_SPELL_CAST) ->
        if (GetSpellAbilityId() == ABILITY_DREAM_EATER
            or GetSpellAbilityId() == ABILITY_LEGACY_DREAM_EATER)
            and not GetSpellTargetUnit().hasAbility(BUFF_ID)
            GetSpellAbilityUnit().abortOrder()
            simError(GetTriggerPlayer(), "Target is not Hypnotized.")
