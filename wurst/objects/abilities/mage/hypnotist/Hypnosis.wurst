package Hypnosis
import RegisterEvents

import AbilityObjEditing
import ObjectIds
import LocalObjectIDs
import Lodash
import ToolTipsUtils

constant MANA_RESTORED = 75.
constant DAMAGE = 20.
constant MANACOST = 10
constant COOLDOWN = 20.
constant CAST_RANGE = 300.
constant DURATION = 5.

constant ANIMATION_NAME = "channel"
constant TOOLTIP_NORM = "Hypnosis"
constant TOOLTIP_EXTENDED = "Puts an enemy into a dream-like state for {0} seconds. Deals {1} damage, but also restores enemy energy by {2}. Has {3} seconds cooldown."
                       .format(DURATION.toToolTipOrange(), DAMAGE.toToolTipRed(), MANA_RESTORED.toToolTipTeal(), COOLDOWN.toToolTipLightBlue())

constant ICON = "ReplaceableTextures\\CommandButtons\\BTNHypnosis.blp"
constant TARGET_ALLOWED = "enemies,ground"
constant BUFFS = toRawCode(BUFF_HYPNOTIZED) + ",BUsp,Bust" // Hypnotized, Sleep,

class Hypnosis extends AbilityDefinitionSleepcreep
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setName(TOOLTIP_NORM)
        this.setHotkeyNormal(hotkey)
        this.setCooldown(1, COOLDOWN)
        this.setManaCost(1, MANACOST)
        this.setIconNormal(ICON)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setDurationHero(1, DURATION)
        this.setDurationNormal(1, DURATION)
        this.setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.setAnimationNames(ANIMATION_NAME)
        this.setCheckDependencies(false)
        this.setStunDuration(1, 1)
        this.setBuffs(1, BUFFS)
        this.setCastRange(1, CAST_RANGE)


@compiletime function createHypnosis()
    new Hypnosis(ABILITY_HYPNOSIS, "Q", new Pair(0, 0))

function onCast()
    var target = GetSpellTargetUnit()
    var caster = GetSpellAbilityUnit()
    caster.damageTarget(target, DAMAGE, ATTACK_TYPE_NORMAL)
    target.addMana(MANA_RESTORED)

init
    registerSpellEffectEvent(ABILITY_HYPNOSIS, () -> onCast())
