package DiseasePotion

import LocalObjectIDs
import ClosureEvents
import HashMap
import ClosureTimers
import ClosureForGroups
import HealingSystem

public let DISEASE_HEALING_REDUCTION_FACTOR = 0.3
let DURATION = 25.
let DAMAGE_TICK_INTERVAL = 0.5
let DAMAGE_PER_SECOND = 3
let AOE = 200.
let EFFECT_PATH = "Abilities\\Spells\\Undead\\PlagueCloud\\PlagueCloudCaster.mdl"
HashMap<unit, DiseaseInstance> diseaseInstances = new HashMap<unit,DiseaseInstance>()

public function isUnitDiseased(unit u) returns bool
    return diseaseInstances.has(u)

function onCastDisease()
    let caster = GetSpellAbilityUnit()
    let target = GetSpellTargetUnit()
    forUnitsInRange(target.getPos(), AOE) u ->
        if isValidTarget(caster.getOwner(), u)
            applyDisease(caster, u)

function isValidTarget(player attacker, unit u) returns bool
    return u.getOwner().isEnemyOf(attacker)
    and not u.isType(UNIT_TYPE_STRUCTURE)
    and not u.isType(UNIT_TYPE_MECHANICAL)
    and u.isAlive()

function applyDisease(unit caster, unit target)
    //Remove any old timers
    if diseaseInstances.has(target)
        destroy diseaseInstances.getAndRemove(target)

    diseaseInstances.put(target, new DiseaseInstance(caster, target))

function onHealed()
    let instance = getHealingInstance()
    if isUnitDiseased(instance.getTarget()) and instance.healingType == HealingType.ITEM
        instance.scaleMultiplier(1 - DISEASE_HEALING_REDUCTION_FACTOR)

class DiseaseInstance
    unit owner
    unit target
    timer t
    effect fx

    function tickDamage()
        owner.damageTarget(target, DAMAGE_PER_SECOND * DAMAGE_TICK_INTERVAL, ATTACK_TYPE_NORMAL)

    construct(unit owner, unit target)
        this.owner = owner
        this.target = target
        this.t = CreateTimer()
        this.fx = addEffect(EFFECT_PATH, target, "origin")

        t.doPeriodicallyTimed(DAMAGE_TICK_INTERVAL, DURATION) cb ->
            this.tickDamage()
            if cb.isLast() and diseaseInstances.has(target)
                destroy diseaseInstances.getAndRemove(target)

    ondestroy
        t.destr()
        fx.destr()

init
    registerSpellEffectEvent(ABILITY_DISEASE) ->
        onCastDisease()

    onUnitHealed() ->
        onHealed()
