
library Hints requires Map, TimerUtils {

  public struct Hints {
    public  static constant real TIME_BETWEEN_HINTS = 120.0;

    public  static force   players = CreateForce();
    private static integer next = 0;

    private trigger action = null;
    private string  message;

    public static method add(string hint) -> thistype {
      thistype self = thistype.allocate();
      self.message = hint;
      return self;
    }

    public method addAction(code what) -> thistype {
      this.action = CreateTrigger();
      TriggerAddAction( this.action, what );
      return this;
    }

    public static method addWithAction(string hint, code what) -> thistype {
      thistype self = Hints.add(hint);
      self.addAction(what);
      return self;
    }

    public static method getNext() -> thistype {
      next += 1;
      if (thistype(next).message == "") {
        next = 1;
      }
      return thistype(next);
    }

    public static method pingLoc(location which) {
      PingMinimapLocForForceEx( Hints.players, which, 7.00, bj_MINIMAPPINGSTYLE_FLASHY, 100, 100, 100 );
    }

    public static method pingUnit(unit which) {
      Hints.pingLoc( GetUnitLoc( which ) );
    }

    public method display() {
      DisplayTimedTextToPlayer( GetLocalPlayer(), 0, 0, 12, GOLD_COLOR + "Hint: |r" + GENERAL_COLOR + message + "|r" );
      if (this.action != null) {
        TriggerExecute( this.action );
      }
    }

    public static method displayPeriodic() {
      TimerStart( NewTimer(), TIME_BETWEEN_HINTS, false, function() { Hints.getNext().display(); });
    }

    public static method addPlayer( player p ) {
      ForceAddPlayer( players, p );
      DisplayTimedTextToPlayer( p, 0, 0, 5, GRAY_COLOR + "Your hints are now on" );
    }

    public static method removePlayer( player p ) {
      ForceRemovePlayer( players, p );
      DisplayTimedTextToPlayer( p, 0, 0, 5, GRAY_COLOR + "Your hints are now off." );
    }

    public static method togglePlayer( player p ) {
      if ( IsPlayerInForce(  p, players ) )
        Hints.removePlayer( p );
      else
        Hints.addPlayer( p );
    }

    public static method onInit() {
      integer i;

      for (0 <= i <= 11) {
        if ( !IsPlayerObserver( Player( i ) ) )
          ForceAddPlayer( players, Player( i ) );
      }

      ChatCommands.registerArgFunc( null, "h", static method( ArgsList args ) {
        togglePlayer( args.triggerPlayer );
      });

      Map.onGameStart( static method Hints.displayPeriodic );
    }
  }

  function onInit() {
    Hints.add( "For help on building, refer to these shops or for some basics, the quest log." ).addAction( function() { Hints.pingLoc( GetRectCenter( gg_rct_vision ) ); } );
    Hints.add( "To toggle hints, type " + HIGHLIGHT_COLOR + "-h|r." );
    Hints.add( "Use |r" + ENERGY_COLOR + "-changelog|r" + GENERAL_COLOR + " to view changelog for this version" );
    Hints.add( "Use |r" + ENERGY_COLOR + "-bug the mammoth is flying!|r" + GENERAL_COLOR + " to report a bug with |r" + GOLD_COLOR +  "Clan " + HOSTING_CLAN + "|r" );
    Hints.add( "Use |r" + ENERGY_COLOR + "-register rendo@example.com|r" + GENERAL_COLOR + " to register your email with |r" + GOLD_COLOR + "Clan " + HOSTING_CLAN + "|r" + GENERAL_COLOR + " to recieve info about arranged games and other news|r" );
    Hints.add( "The Master Crafter in the center of the map can help your team build if you do not have enough inventory slots." ).addAction( function() { Hints.pingUnit( MASTER_CRAFTER ); } );
    Hints.add( "Boats frequently travel up and down the streams. The fur trade is especially lucrative." );
    Hints.add( "Remember to keep a watch on you and your team's stats. Remind someone if they are running low on anything." );
    Hints.add( "Beware the mammoth. He is a strong foe and defeating him is a team effort, but you will be rewarded...." ).addAction( function() { Hints.pingUnit( Mammoth ); } );
    Hints.add( "Try not to sit around in your base the whole game. You can almost always be doing something useful." );
    Hints.add( "If any of your stats hit 0, you die instantly so keep them up at all times." );
    Hints.add( "If it seems you have collected all the items or hunted all the wildlife in one area, move to another. Sometimes fertile land is just an island away." );
    Hints.add( "Fighting the bosses around the edges of the map is a good way to level up and gain valuable items." );
    Hints.add( "If you have no hunter, fires can be used to trap elk. They also make cheap lookouts." );
    Hints.add( "Speed, strategy, and meat are crucial to winning wars." );
    Hints.add( "Use quickmake skills and unload items to quickly manage the inventories of buildings." );
    Hints.add( "Type " + ENERGY_COLOR + "-changelog|r" + GENERAL_COLOR + " to view changelog for this version" );
    Hints.add( "You can attack the Master Crafter in the middle of the map to advertise a duel. You can attack it again to cancel the advertisement." ).addAction( function() { Hints.pingUnit( MASTER_CRAFTER ); } );
  }

}
