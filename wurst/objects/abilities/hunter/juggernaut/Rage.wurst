package Rage

// Standard library imports:
import Assets
import BuffObjEditing
import ChannelAbilityPreset

// Third-party imports:
import Lodash

// Local imports:
import ColorUtils
import LocalObjectIDs
import ToolTipsUtils

let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let BUFF_ORIGINAL_ID = 'BHad'

let MS_BONUS = 0.
let AS_BONUS = 0.05
let TOOLTIP_NORM = "Rage"
let TOOLTIP_EXTENDED = "Permanently increase attack speed by {0}."

function createBuff()
    new BuffDefinition(BUFF_ID, BUFF_ORIGINAL_ID)
        ..setIcon(Icons.bTNHeadHunterBerserker)
        ..setArtSpecial(1,"")
        ..setTooltipNormal(1, TOOLTIP_NORM)
        ..setTooltipNormalExtended(1, "This unit is enraged, attacks and move faster.")
        ..setArtTarget(1, Abilities.berserkertarget)
        ..setTargetAttachmentPoint0(1, "overhead")

class Rage extends AbilityDefinitionTaurenChieftainEnduranceAura
    construct(int newAbilityId, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setLevels(3)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNormHeroAura(TOOLTIP_NORM, lvl))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED.format((AS_BONUS + (lvl * 0.05)).toToolTipRed()))

        this.setTooltipLearn(makeToolTipLearn(TOOLTIP_NORM))
        this.setTooltipLearnExtended("Permanently increase attack speed by {0}.".format("10/15/20".color(COLOR_ORANGE)))

        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setIconNormal(Icons.bTNHeadHunterBerserker)
        this.setIconResearch(Icons.bTNHeadHunterBerserker)
        this.setBuffs(1, toRawCode(BUFF_ID))
        this.setAreaofEffect(1, 1)
        this.setArtTarget("")
        this.presetAttackSpeedIncrease(lvl -> AS_BONUS + (lvl * 0.05))
        this.presetMovementSpeedIncrease(lvl -> MS_BONUS)


@compiletime function createRage()
    new Rage(ABILITY_RAGE, new Pair(2, 1))
    createBuff()
