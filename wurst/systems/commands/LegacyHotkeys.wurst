package LegacyHotkeys

// Standard library imports:
import OnUnitEnterLeave

// Local imports:
import ChatCommands
import LocalObjectIDs
import PlayerExtensions

function onEnterHandler(unit target)
    // Exit if the legacy hotkeys are not enabled for this player.
    if not target.getOwner().getConfig().isLegacyHotkeyEnabled()
        return

    // Switch the base abilities to provide legacy hotkeys.
    if target.hasAbility(ABILITY_NEGATIVE_BLAST)
        target
            ..removeAbility(ABILITY_NEGATIVE_BLAST)
            ..addAbility(ABILITY_LEGACY_NEGATIVE_BLAST)

init
    registerCommandAll("lh") (triggerer, args) ->
        // Switch this option on.
        triggerer.getConfig().toggleLegacyHotkey()

        // Notify the user of the change.
        triggerer.print(
            "Toggled legacy hotkeys to: {0}".format(
                triggerer.getConfig().isLegacyHotkeyEnabled().toString()
            )
        )

    onEnter() ->
        onEnterHandler(getEnterLeaveUnit())
