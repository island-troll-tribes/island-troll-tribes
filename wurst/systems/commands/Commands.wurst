package Commands
import HashMap
import LinkedList
import ChatCommands
import StringExtensions
import ChatUtils
import PlayerExtensions
import Game

class PlayerAllianceSetting
    string name
    alliancetype setting
    bool invertSetting
    HashMap<player, bool> values

    construct(string name, alliancetype setting)
        this.name = name
        this.setting = setting
        this.values = new HashMap<player, bool>()
        this.invertSetting = false

    construct(string name, alliancetype setting, bool invertSetting)
        this.name = name
        this.setting = setting
        this.values = new HashMap<player, bool>()
        this.invertSetting = invertSetting

let settings = new HashMap<string, PlayerAllianceSetting>()

function handlePlayerAllianceSettingToggle(player triggerPlayer, LinkedList<string> args)
    let setting = settings.get(args.get(0))
    let newValue = args.size() > 1 ? args.get(1).isConfirmation() : not setting.values.get(triggerPlayer)
    let msg = "{0} has turned {1} {2}".format(triggerPlayer.getNameColored(), setting.name.color(SPECIAL_COLOR), newValue.toSwitch().color(ENERGY_COLOR))
    DisplayTimedTextToForce(triggerPlayer.getAllies(), 5, msg)
    setting.values.put(triggerPlayer, newValue)
    triggerPlayer.getTribe().getMembers().forEach() member ->
        if member != triggerPlayer
            triggerPlayer.setPlayerAlliance(member, setting.setting, setting.invertSetting ? not newValue : newValue)

init
    settings
    ..put("ac", new PlayerAllianceSetting("advanced control", ALLIANCE_SHARED_ADVANCED_CONTROL))
    ..put("sc", new PlayerAllianceSetting("share control", ALLIANCE_SHARED_CONTROL))
    ..put("p", new PlayerAllianceSetting("private mode", ALLIANCE_SHARED_VISION, true))

    registerCommandAll("clear") (triggerPlayer, args) ->
        if localPlayer == triggerPlayer
            ClearTextMessages()

    registerCommandAll("c") (triggerPlayer, args) ->
        let id = triggerPlayer.getId()
        let zoom = args.get(1).toReal()
        let msg = "Current zoom is {0} (default 0)".format(zoom.toInt().toString().color(ENERGY_COLOR))
        ZOOM_DISTANCE[id] = zoom
        ZoomSetCamera(id)
        printTimedToPlayer(msg, 5, triggerPlayer)

    registerCommandAll("rn") (triggerPlayer, args) ->
        for i = 0 to PLAYER_NEUTRAL_AGGRESSIVE - 1
            if udg_RealNames[i] != ""
                let msg = "{0} {1}".format((i + 1).toString().color(COLOR_CODE[i]), players[i].getName())
                printTimedToPlayer(msg, 10, triggerPlayer)

    // registerCommandAll("obs") (triggerPlayer, args) ->
    //    SetPlayerState(triggerPlayer, PLAYER_STATE_OBSERVER, 1)

    registerGameStartEvent() ->
        // deregisterCommandAll("obs")

        registerCommandAll("ac", (triggerPlayer, args) -> handlePlayerAllianceSettingToggle(triggerPlayer, args))
        registerCommandAll("sc", (triggerPlayer, args) -> handlePlayerAllianceSettingToggle(triggerPlayer, args))
        registerCommandAll("p", (triggerPlayer, args) -> handlePlayerAllianceSettingToggle(triggerPlayer, args))

        registerCommandAll("ms") (triggerPlayer, args) ->
            let hero = triggerPlayer.getHero()
            if hero.isAlive()
                let msg = "Your movement speed is {0}".format(hero.getMoveSpeed().toString().color(ENERGY_COLOR))
                printTimedToPlayer(msg, 5, triggerPlayer)

        registerCommandAll("cc") (triggerPlayer, args) ->
            let id = args.get(1).toInt() mod 12
            let msg = "Adjusted player color to {0}Player {1}|r".format(COLOR_CODE[id], (id + 1).toString())
            printTimedToPlayer(msg, 5, triggerPlayer)
            triggerPlayer.setColor(ConvertPlayerColor(id), true)

        registerCommandAll("recipes") (triggerPlayer, args) ->
            let msg = "{0}Ok, here are the build help locations.\nYou can see the recipe for every item and building by selecting the wards, take time to read".format(GENERAL_COLOR)
            PingMinimapForPlayer(triggerPlayer, gg_rct_vision.getCenterX(), gg_rct_vision.getCenterY(), 5)
            PanCameraToForPlayer(triggerPlayer, gg_rct_vision.getCenterX(), gg_rct_vision.getCenterY())
            printTimedToPlayer(msg, 5, triggerPlayer)
