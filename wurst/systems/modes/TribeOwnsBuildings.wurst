package TribeOwnsBuildings

// Standard library imports:
import RegisterEvents

// Local imports:
import GameConfig
import PlayerExtensions
import Tribe

player array tribePlayers

function onConstruct()
    let building = GetConstructingStructure()
    let tribe = building.getOwner().getTribe()

    if tribe == null
        return

    let tribeId = tribe.getId()
    let tribePlayer = tribePlayers[tribeId]
    building.setOwner(tribePlayer, false)

function initialize()
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_CONSTRUCT_START, function onConstruct)
    for tribe in Tribe.getTribes()
        let members = tribe.getMembers()
        let tribePlayer = players[12 + tribe.getId()]
        tribePlayers[tribe.getId()] = tribePlayer
        for member in members
            SetPlayerAllianceStateBJ(member, tribePlayer, bj_ALLIANCE_ALLIED_VISION)
            SetPlayerAllianceStateBJ(tribePlayer, member, bj_ALLIANCE_ALLIED_ADVUNITS)
            SetPlayerAllianceStateBJ(member, tribePlayer, bj_ALLIANCE_ALLIED_ADVUNITS)

        for a in members
            for b in members
                if a != b
                    a.setPlayerAlliance(b, ALLIANCE_SHARED_ADVANCED_CONTROL, true)

init
    registerTribeInitializationFinishEvent() ->
        if gameConfig.isTribeOwnsBuildingsEnabled()
            initialize()
