package PantherInstinct

// Standard lib Imports:
import ChannelAbilityPreset
import ClosureEvents
import ObjectIdGenerator
import ObjectIds
import Assets
import UpgradeObjEditing
import BuffObjEditing

// Local Imports:
import PlayerExtensions
import LocalAssets
import ToolTipsUtils
import LocalObjectIDs

constant BUFF_ID = compiletime(BUFF_ID_GEN.next())
constant BUFF_ORIGINAL_ID = 'Bpsh' // PhaseShift

constant DUMMY_ABIL_ID = compiletime(ABIL_ID_GEN.next())

constant CAST_RANGE = 0
constant COOLDOWN = 20.
constant MANACOST = 5
constant DURATION = 4.
constant TOOLTIP_NORM = "Phase Shift"
constant TOOLTIP_EXTENDED = "The Conjurer shift between dimension and cannot be harmed." + makeToolTipDuration(DURATION, COOLDOWN)

function createBuff()
    new BuffDefinition(BUFF_ID, BUFF_ORIGINAL_ID)
        ..setIcon(Icons.bTNPhaseShift)
        ..setArtSpecial(1,"")
        ..setTooltipNormal(1, TOOLTIP_NORM)
        ..setTooltipNormalExtended(1, "This troll is shifting between dimension, he doesn't take any damage in this state.")

class DummyPhaseShift extends AbilityDefinitionInnerFire
    construct(int newAbilityId)
        super(newAbilityId)
        this.setName("PhaseShiftDummy")
        this.setBuffs(1, toRawCode(BUFF_ID))
        this.setDurationHero(1, DURATION)
        this.setDurationNormal(1, DURATION)
        this.setCastRange(1, 99999)
        this.setManaCost(1, 0)
        this.setRequirements("")
        this.setDamageIncrease(1, 0)
        this.setDefenseIncrease(1, 0)

class PhaseShift extends AbilityDefinitionWindWalk
    construct(int newAbilityId, string hotkey, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.presetManaCost(lvl -> MANACOST)
        this.presetCooldown(lvl -> COOLDOWN)
        this.setHeroAbility(false)
        this.setItemAbility(false)
        this.setHotkeyNormal(hotkey)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setIconNormal(Icons.bTNPhaseShift)
        this.setIconResearch(Icons.bTNPhaseShift)
        this.setIconTurnOff(Icons.bTNPhaseShift)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setEffectSound("")
        this.setDurationHero(1, 0.01)

@compiletime function createPhaseShift()
    new PhaseShift(ABILITY_PHASE_SHIFT, "E", new Pair(1, 1))
    new DummyPhaseShift(DUMMY_ABIL_ID)
    createBuff()


init
    DamageEvent.addListener() () ->
        let damaged = DamageEvent.getTarget()
        if damaged.hasAbility(BUFF_ID)
            DamageEvent.setAmount(0)
    EventListener.onCast(ABILITY_PHASE_SHIFT) (unit caster) ->
        InstantDummyCaster.castTarget(caster.getOwner(), DUMMY_ABIL_ID, 1, Orders.innerfire, caster)
        doPeriodicallyTimed(0.3, DURATION) cb ->
            if caster.isAlive()
                var fx = addEffect(Abilities.aIilTarget, caster.getPos())
                doAfter(0.5) () ->
                    fx.destr()
