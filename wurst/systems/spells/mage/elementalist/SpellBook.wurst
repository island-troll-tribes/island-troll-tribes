package SpellBook

import AbilityObjEditing
import ClosureForGroups
import GameConfig
import ChatCommands

//With darkgate and runes "XEZS, XEMS, XFPS, ESEF, ESOC, ESMD, A076, A06S, A079, A07A"
constant string array SPELLS = ["XEZS, ESEF", "XEZS, ESFB, ESEF, ESMD", "XEZS, XEMS, ESFB, ESEF, ESOC, ESBR, ESMD"]

@compiletime function createSpellbook()
    new AbilityDefinitionSpellBook('XESB')
    ..setButtonPositionNormalX(1)
    ..setButtonPositionNormalY(1)
    ..setButtonPositionResearchX(0)
    ..setButtonPositionResearchY(0)
    ..setIconNormal("ReplaceableTextures\\CommandButtons\\BTNTomeOfRetraining.blp")
    ..setIconResearch("ReplaceableTextures\\CommandButtons\\BTNTomeOfRetraining.blp")
    ..setIconTurnOff("ReplaceableTextures\\CommandButtons\\BTNTomeOfRetraining.blp")
    ..setHeroAbility(true)
    ..setItemAbility(false)
    ..setLevels(3)
    ..presetBaseOrderID(lvl -> "spellbook")
    ..setHotkeyLearn("Z")
    ..setHotkeyNormal("Z")
    ..setName("New Elementalist")
    ..setTooltipLearn("Learn Spells of The Elementalist")
    ..setTooltipLearnExtended("Harness the elements to do your bidding")
    ..presetMaximumSpells(lvl -> 11)
    ..presetMinimumSpells(lvl -> 11)
    ..presetSpellList(lvl -> SPELLS[lvl-1])
    ..presetCooldown(lvl -> 0)
    ..presetTooltipNormal(lvl -> "Look up elemental spell (|cffffcc00Z|r)")
    ..presetTooltipNormalExtended(lvl -> "Contains various spells n such")
    ..presetSharedSpellCooldown(lvl -> false)
init

    registerCommandAll("spells") (triggerPlayer, args) ->
        if (gameConfig.isTestModeEnabled() == true)
            printTimedToPlayer("Added elementalist test spellbook to any selected heroes", 5., triggerPlayer)
            forUnitsSelected(triggerPlayer) u ->
                if (u.isType(UNIT_TYPE_HERO))
                    u.addAbility('XESB')
        else
            printTimedToPlayer("Requires test mode!", 5., triggerPlayer)
