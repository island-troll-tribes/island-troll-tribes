package UnitExtensions

// Standard library imports:
import ItemObjEditing
import LinkedList
import ObjectIdGenerator

// Third-party imports:
import StackNSplit
import Lodash

// Local imports:
import Brambles
import Construction
import GameConfig
import GroupUtils
import HashMap
import IdListConstant
import LocalObjectIDs
import ManaBurn

public let BUFF_ENSNARE_GROUND = 'Beng'
public let BUFF_ENSNARE_AIR = 'Bena'
public let BUFF_ENTANGLE = 'BEer'


constant DUMMY_ITEM_ID = compiletime(ITEM_ID_GEN.next())
@compiletime function createDummyItem()
    new ItemDefinition(DUMMY_ITEM_ID, 'bspd')

/**
    Adds an item by handle to the given slot.
*/
public function unit.addItemHandleToSlot(item itm, int targetSlot)
    // Compute the list of intervening empty slots.
    let emptySlots = filter(slot -> UnitItemInSlot(this, slot) == null,
                            range(0, targetSlot).toList())
    // Fill each intervening slot.
    for _ in emptySlots
        this.addItemById(DUMMY_ITEM_ID)
    // Add the item to the first available slot, which is now the target slot.
    this.addItemHandle(itm)
    // Remove the dummy items.
    for slot in emptySlots
        UnitItemInSlot(this, slot).remove()

public function unit.getHeroSkillPoints() returns integer
    return GetHeroSkillPoints(this)

public function unit.modifySkillPoints(integer value)
    UnitModifySkillPoints(this, value)

public function unit.freeSlots() returns int
    int result = 0
    for slot = 0 to this.inventorySize() - 1
        if this.itemInSlot(slot) == null
            result++
    return result

public function unit.itemCount(int itemTypeId) returns int
    var count = 0
    for i = 0 to 5
        if this.itemInSlot(i).getTypeId() == itemTypeId
            count++
    return count

public function unit.getInventoryItemCountMap() returns HashMap<int, int>
    let map = new HashMap<int, int>()
    for i = 0 to 5
        let id = this.itemInSlot(i).getTypeId()
        if map.has(id)
            map.put(id, map.get(id) + 1)
        else
            map.put(id, 1)
    return map

public function unit.getInventory() returns LinkedList<item>
    let list = new LinkedList<item>
    let size = this.inventorySize()
    for i = 0 to size - 1
        list.add(this.itemInSlot(i))
    return list

public function unit.addXp(int toAdd)
    AddHeroXP(this, toAdd, true)

public function unit.setXp(int newXpVal)
    this.setXp(newXpVal, true)

public function unit.addUnitToStock(int id, int currentStock, int stockMax)
    AddUnitToStock(this, id, currentStock, stockMax)

public function unit.removeUnitFromStock(int id)
    RemoveUnitFromStock(this, id)

public function unit.addItemToStock(int id, int currentStock, int maxStock)
    AddItemToStock(this, id, currentStock, maxStock)

public function unit.removeItemFromStock(int id)
    RemoveItemFromStock(this, id)

public function unit.shareVision(player whichPlayer, bool share)
    UnitShareVision(this, whichPlayer, share)

public function unit.removeBuffs(bool removePositive, bool removeNegative)
    UnitRemoveBuffs(this, removePositive, removeNegative)

public function unit.isStructure() returns boolean
    return IsUnitType(this, UNIT_TYPE_STRUCTURE)

public function unit.isTroll() returns boolean
    return TROLL_UNIT_LIST.has(this.getTypeId())

public function unit.getHeat() returns real
    return this.getOwner().getGold().toReal()

public function unit.setHeat(int heat)
    this.getOwner().setGold(heat)

public function unit.addHeat(int heat)
    let owner = this.getOwner()
    owner.setGold(min(owner.getGold() + heat, gameConfig.getMaxHeat()))

public function unit.subHeat(int heat)
    let owner = this.getOwner()
    owner.setGold(max(owner.getGold() - heat, 0))

public function unit.getItemMatching(Predicate<item> predicate) returns item
    let inventorySize = this.inventorySize()
    for i = 0 to inventorySize
        let itm = this.itemInSlot(i)
        if predicate.call(itm)
            return itm
    return null

public function unit.moveInventoryDown()
    int array freeSlots
    var bottom = 0
    var top = 0
    for slot = this.inventorySize() - 1 downto 0
        let itm = this.itemInSlot(slot)
        if itm == null
            freeSlots[top] = slot
            top += 1
        else if top != bottom
            this.dropItemSlot(itm, freeSlots[bottom])
            bottom += 1

public function unit.dropItems(int count)
    for slot = 0 to count - 1
        this.removeItemFromSlot(slot)

public function unit.dropItems()
    this.dropItems(this.inventorySize())

public function unit.addSleep(bool value)
    UnitAddSleep(this, value)

public function unit.setCreepGuard(bool value)
    SetUnitCreepGuard(this, value)

public function unit.addOrStackItem(item itm) returns bool
    //Stackables
    var result = false
    let itemTypeId = itm.getTypeId()
    if isItemStackable(itemTypeId)
        this.stackItem(itm)

        if not itm.isAlive()
            return true

        //Add leftover stacks if they were left
        var charges = itm.getCharges()
        let maxStack = getItemMaxStacks(itemTypeId)
        while charges > 0
            if this.isInventoryFull()
                result = false
                break
            if charges > maxStack
                let itemPos = itm.getPos()
                let newItemStack = createItem(itemTypeId, itemPos)
                newItemStack.setCharges(maxStack)
                this.addItemHandle(newItemStack)
                charges -= maxStack
            else
                this.addItemHandle(itm)
                result = true
                break

        itm.setCharges(charges)
    else
        //Non-stackables
        if not this.isInventoryFull()
            this.addItemHandle(itm)
            result = true
    return result

public function unit.isImmobilized() returns bool
    return (
        this.getAbilityLevel(BUFF_ENSNARE_AIR) > 0 or
        this.getAbilityLevel(BUFF_ENSNARE_GROUND) > 0 or
        this.getAbilityLevel(BUFF_ENTANGLE) > 0 or
        this.getAbilityLevel(BUFF_ENTANGLE_BRAMBLES_ID) > 0
    )

//  TO DO : should find a way that works and checks the return
public function unit.makeAbilitiesPermanent(LinkedList<int> abilList, boolean flag)
    abilList.forEach(elem -> this.makeAbilityPermanent(elem, flag))

public function unit.isEnemyBuildingAround(real radius) returns bool
    var nearbyUnits = CreateGroup()..enumUnitsInRange(this.getPos(), radius)
    while nearbyUnits.hasNext()
        let u = nearbyUnits.next()
        if u.isEnemyOf(this.getOwner()) and u.isStructure()
            nearbyUnits.destr()
            return true
    nearbyUnits.destr()
    return false

public function unit.isEnemyDefense() returns bool
    return this.getTypeId() == UNIT_ENSNARE_TRAP
        or this.getTypeId() == UNIT_OMNITOWER

public function unit.isConstructing() returns bool
    return isConstructing(this)

public function unit.burnMana(real damage)
    burnMana(this, damage)

public function unit.registerTroll()
    addTroll(this)

public function unit.deregisterTroll()
    removeTroll(this)

public function unit.isItemInSlot(int slot) returns bool
    return this.itemInSlot(slot) != null

public function unit.isItemInSlot(int slot, int id) returns bool
    let target = this.itemInSlot(slot)
    return target != null and target.getTypeId() == id

public function unit.isBoss() returns bool
    return BOSS_LIST.has(this.getTypeId())
