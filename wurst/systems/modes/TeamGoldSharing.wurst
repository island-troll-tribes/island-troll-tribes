package TeamGoldSharing
import GameConfig
import GameMode
import LegacyColors
import ChatUtils
import Game
import RegisterEvents
import ClosureTimers
import Tribe

function distributeGold(unit whichUnit)
    nullTimer() ->
        let owner = whichUnit.getOwner()
        let tribe = Tribe.ofPlayer(owner)
        let gold = owner.getLumber()
        let members = tribe.getMembers()
        for member in members
            member.setLumber(gold)
        destroy members

function syncTribesToLowestGold()
    nullTimer() ->
        for tribe in Tribe.tribes
            let members = tribe.getMembers()
            members.sortWith((a,b) -> a.getLumber() - b.getLumber())
            let lowestLumber = members.getFirst().getLumber()
            for m in members
                m.setLumber(lowestLumber)
            destroy members

function initializeTeamGold()
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SELL_ITEM) ->
        syncTribesToLowestGold()

    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_PAWN_ITEM) ->
        distributeGold(GetTriggerUnit())

init
    new GameMode("team-gold", "tg", "Players on a team share gold in the same pool") (mode, args) ->
        let enabled = args.size() > 1 ? args.get(0).isConfirmation() : true
        gameConfig.setTeamGoldEnabled(enabled)
        mode.message(GOLD_COLOR + "Team gold|r " + enabled.toOnOff() + "!")

    registerGameStartEvent() ->
        if gameConfig.isTeamGoldEnabled()
            initializeTeamGold()
