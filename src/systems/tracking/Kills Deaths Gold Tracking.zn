
library PlayerStats requires ittEvent {

    ittEvent deaths;
    ittEvent kills;
    ittEvent gold;

    public {
        integer playerKills[];
        integer playerDeaths[];
        integer playerGold[];
    }
    
    public function RegisterEnergyOrHeatKill(unit u) {
        if ( IsUnitTroll( u ) && !udg_NOOB_TIME && !GAME_END )
            kills.addForPlayer( GetOwningPlayer( u ), 1 );
    }
    
    function RegisterDeath() {
        if ( IsUnitTroll( GetTriggerUnit() ) && !udg_NOOB_TIME && !GAME_END ) {
            if ( GetKillingUnit() != null && !IsUnitAlly( GetKillingUnit(), GetOwningPlayer( GetTriggerUnit() ) ) ) {
                kills.addForPlayer( GetOwningPlayer( GetKillingUnit() ), 1 );
                playerKills[GetPlayerId( GetOwningPlayer( GetKillingUnit() ) )] += 1;
            }
            deaths.addForPlayer( GetOwningPlayer( GetTriggerUnit() ), 1 );
            playerDeaths[GetPlayerId( GetOwningPlayer( GetTriggerUnit() ) )] += 1;
        }
    }

    function RegisterGold() {
        integer sellPrice = GetItemSellPrice( GetSoldItem() );
        gold.addForPlayer( GetOwningPlayer( GetTriggerUnit() ), sellPrice );
        playerGold[GetPlayerId( GetOwningPlayer( GetTriggerUnit() ) )] += sellPrice;
    }
    
    function onInit() {
        timer t = CreateTimer();

        TimerStart( t, 1.5, false, function() {
            integer i = 0;
            trigger t = CreateTrigger();
            trigger u = CreateTrigger();

            deaths = ittEvent.create("deaths", TYPE_INT);
            kills = ittEvent.create("kills", TYPE_INT);
            gold = ittEvent.create("gold", TYPE_INT);

            for ( 0 <= i < 12 ) {
                if ( GetPlayerController( Player( i ) ) == MAP_CONTROL_USER && GetPlayerSlotState( Player( i ) ) == PLAYER_SLOT_STATE_PLAYING )
                    TriggerRegisterPlayerUnitEvent( t, Player( i ), EVENT_PLAYER_UNIT_DEATH, null );
            }
            TriggerAddAction ( t, function RegisterDeath );

            TriggerRegisterAnyUnitEventBJ( u, EVENT_PLAYER_UNIT_PAWN_ITEM );
            TriggerAddAction( u, function RegisterGold );
        });
    }
}