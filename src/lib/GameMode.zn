library GameMode requires ChatCommands, GameConfig, Table, PublicLibrary, IDUtils {
  public struct GameMode {
    static integer numGameModes = 0;
    static string modesString = "";
    public static Table modes;

    string description = "";
    string name = "";
    string abbrev = "";
    player modesPlayer = null;
    ArgFunc action;

    public static method create(string name, string abbrev, string description, ArgFunc action) -> thistype {
      thistype self = thistype.allocate();
      self.name = name;
      self.description = description;
      self.abbrev = abbrev;
      self.action = action;
      numGameModes += 1;
      return self;
    }

    static method registerAll(player modesPlayer) {
      integer i;

      for (1 <= i <= numGameModes) {
        thistype(i).register(modesPlayer);
      }
    }

    static method deregisterAll() {
      integer i;

      for (1 <= i <= numGameModes) {
        thistype(i).deregister();
      }
    }

    public method registerFor(string command) {
      ChatCommands.registerArgFunc(this.modesPlayer, command, this.action);
      thistype.modes.integer_s[abbrev] = this;
    }

    public method deregisterFor(string command) {
      ChatCommands.deregisterArgFunc(this.modesPlayer, command);
      thistype.modes.integer_s.remove(abbrev);
    }

    public method register(player modesPlayer) {
      this.modesPlayer = modesPlayer;
      this.registerFor(name);
      this.registerFor(abbrev);
    }

    public method deregister() {
      this.deregisterFor(name);
      this.deregisterFor(abbrev);
    }

    public static method getEnabledModes() -> string {
      return thistype.modesString;
    }

    public static method message(string msg) {
      thistype.modesString += msg + "\n";
      DisplayText(msg);
    }

    public static method error(string msg) {
      DisplayText(RED_COLOR + msg + "|r");
    }

    public static method find(string abbrev) -> thistype {
      return thistype.modes.integer_s[abbrev];
    }

    public method triggerWithArgs(string args) -> thistype {
      ArgsList a = ArgsList.processArgs("-" + this.abbrev + " " + args);
      this.action.evaluate(a);
      a.destroy();
      return this;
    }

    public method trigger() -> thistype {
      return this.triggerWithArgs("");
    }
  }

  function onInit() {
    GameMode.modes = Table.create();

    GameMode.create("players-per-tribe", "ppt", "set the number of players in each tribe, eg: \"-ppt 6\"", function(ArgsList a) {
      integer ppt;
      if (a.length() < 1) {
        GameMode.error("Must supply # of tribes to \"ppt\"");
        return;
      }
      ppt = S2I(a[0]);
      if (ppt <= 0 || 6 < ppt || ModuloInteger(12, ppt) != 0) {
        GameMode.error("Must have 1-6 players per tribe and all slots must belong to a team");
        return;
      }
      GameConfig.getInstance().setNumPlayersPerTribe(ppt);
      GameMode.find("ppt").deregister();
      GameMode.find("6v6").deregister();
      GameMode.find("ffa").deregister();
      GameMode.message(ENERGY_COLOR + "Players per tribe|r set to " + I2S(ppt));
    });

    GameMode.create("6v6", "6s", "set the number of players in each tribe to 6", function(ArgsList a) {
      GameConfig.getInstance().setNumPlayersPerTribe(6);
      GameMode.find("ppt").deregister();
      GameMode.find("6v6").deregister();
      GameMode.find("ffa").deregister();
      GameMode.message(ENERGY_COLOR + "6v6|r enabled");
    });

    GameMode.create("free-for-all", "ffa", "each player is on their own", function(ArgsList a) {
      GameConfig.getInstance().setNumPlayersPerTribe(1);
      GameMode.find("ppt").deregister();
      GameMode.find("6v6").deregister();
      GameMode.find("ffa").deregister();
      GameMode.message(ENERGY_COLOR + "Free for all|r enabled");
    });

    GameMode.create("standard-1v1", "1v1", "enables all standard modes for 1v1", function(ArgsList a) {
      GameMode.message(ENERGY_COLOR + "1v1|r enabled");
      GameConfig.getInstance().setNumPlayersPerTribe(1);
      GameMode.find("fd").triggerWithArgs("2100");
      GameMode.find("sf").trigger();
      GameMode.find("rs").trigger();
    });

    GameMode.create("tournament", "to", "Tournament Modes (-sf, -el, -fd, -rs)", function(ArgsList a) {
      GameMode.message(HIGHLIGHT_COLOR + "Tournament mode enabled:");
      GameMode.find("sf").trigger();
      GameMode.find("rs").trigger();
      GameMode.find("fd").trigger();
      GameMode.find("el").trigger();
      GameMode.find("tournament").deregister();
    });

    GameMode.create("all", "ar", "each player is given the same class or random (ex: \"-all random\"", function(ArgsList a) {
      integer uid = 0;
      if (a[0] != "random") {
        uid = S2ID("UNIT_" + StringReplace(StringCase(a.fullMessage, true), " ", "_"));
        if (UNIT_TROLL_ALL[uid] != TROLL_BASE_CLASS) {
          GameMode.error("Invalid class selection");
          return;
        }
      }
      GameConfig.getInstance().setAllTrollEnabled(uid);
      GameMode.find("all").deregister();
      GameMode.message(ENERGY_COLOR + "All " + StringCase(a.fullMessage, false) + "|r enabled");
    });

    GameMode.create("forced-duel", "fd", "set a forced duel after an amount of minutes, defaults to 1 h, eg: \"-fd 60\"", function(ArgsList a) {
      real amount = 60.0;
      if (a.length() >= 1) {
        amount = S2R(a[0]);
      }
      if (!GameConfig.getInstance().setForcedDuel(amount)) {
        GameMode.error("Force duel must be after end of the grace period");
        return;
      }
      GameMode.message(ENERGY_COLOR + "Forced duel|r after " + R2S(amount) + " min");
    });

    GameMode.create("grace-period-duration", "gp", "change the duration of the grace period, eg: \"-gp 8\"", function(ArgsList a) {
      real amount = 8.0;
      if (a.length() >= 1) {
        amount = S2R(a[0]);
      }
      if (!GameConfig.getInstance().setGracePeriodDuration(amount)) {
        GameMode.error("Grace period must end before forced duel");
        return;
      }
      GameMode.message(ENERGY_COLOR + "Grace period duration|r " + R2S(amount) + " min");
    });

    GameMode.create("hot-mode", "hm", "increase the amount of heat a fire gives per cast", function(ArgsList a) {
      integer amount = 22;
      if (a.length() >= 1) {
        amount = S2I(a[0]);
      }
      udg_HEAT_PER_CAST = amount;
      GameMode.message(ENERGY_COLOR + "Hot mode|r enabled, heat per cast is now " + GOLD_COLOR + I2S(amount) + "|r");
    });

    GameMode.create("enable-boats", "eb", "enables construction of transport boats", function(ArgsList a) {
      udg_DisabledBoats = false;
      GameMode.find("eb").deregister();
      GameMode.message(ENERGY_COLOR + "Transport ships|r enabled");
    });

    GameMode.create("no-horn", "nh", "disables boss prizes", function(ArgsList a) {
      udg_BOSS_PRIZE_MODE = false;
      GameMode.find("nh").deregister();
      GameMode.message(ENERGY_COLOR + "Boss prizes|r like the mammoth horn have been disabled.");
    });

    GameMode.create("random-spawns", "rs", "randomize spawn locations", function(ArgsList a) {
      GameMode.find("rs").deregister();
      GameMode.message(ENERGY_COLOR + "Spawn locations|r have been randomized");
      GameConfig.getInstance().setRandomSpawns(true);
    });

    GameMode.create("start-with-fire", "sf", "every troll starts with a fire", function(ArgsList a) {
      GameMode.find("sf").deregister();
      GameMode.message("Every troll will start with a " + ENERGY_COLOR + "fire kit");
      GameConfig.getInstance().setStartWithFire(true);
    });

    GameMode.create("more-hostiles", "rb", "raise hostile animal spawn rate, eg: \"-rb 300\"", function(ArgsList a) {
      real next;
      if (a.length() >= 1) {
        next = S2R(a[0]) / 100;
      } else {
        next = GameConfig.getInstance().getHostileSpawnRate() + 0.20;
      }
      next = RealMin(next, 3.0);
      GameConfig.getInstance().setHostileSpawnRate(next);

      GameMode.message(ENERGY_COLOR + "Hostile Animal Spawn Rate|r has been increased to "+R2S(next*100)+"% of normal.");
    });

    GameMode.create("old-random", "or", "use the old style of randoming where you can random any class", function(ArgsList a) {
      GameMode.find("or").deregister();
      GameMode.message("Old random enabled, you can now random any class");
      GameConfig.getInstance().setOldRandomEnabled(true);
    });

    GameMode.create("start", "st", "start the game", function(ArgsList a) {
      PauseTimer(MODES_TIMER);
      ExecuteFunc("ModesTimerFinished");
    });

    GameMode.create("quick-pick", "qp", "start the game", function(ArgsList a) {
      integer timeAllowed = 5;
      if (a.length() >= 1) {
        timeAllowed = S2I(a[0]);
      }
      GameMode.message(ENERGY_COLOR + "Selection time|r set to " + I2S(timeAllowed) + "s");
      GameConfig.getInstance().setSelectionTimeAllowed(timeAllowed);
    });

    GameMode.create("elimination", "el", "Elimination mode (no spirit wards)", function(ArgsList a) {
      GameMode.message(HIGHLIGHT_COLOR + "Elimination Mode|r has been enabled. Teams can not build spirit wards now!");
      GameConfig.getInstance().setEliminationEnabled(true);
    });

    // GameMode.create("raise heat", "rh", "increase heat capacity from 100 to 150", function(ArgsList a) {
    //  GameConfig.getInstance().heatMaximum = 150;
    //  GameMode.find("rh").deregister();
    //  GameMode.message(ENERGY_COLOR + "Heat Max|r has been increased. Heat capacity is 150.");
    // });
  }
}
