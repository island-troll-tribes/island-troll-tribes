package ChatCommands

// Standard library imports:
import HashMap
import LinkedList
import StringUtils

// Third-party imports:
import Lodash
import LodashExtensions

public interface CommandHandler
    function run(player triggerPlayer, LinkedList<string> arguments)

let commands = range(bj_MAX_PLAYER_SLOTS)
    .toList()
    .map(
        (_, index) -> new Pair(
            players[index],
            new HashMap<string, CommandHandler>()
        )
    )
    .fromPairs()

public function registerCommand(player thePlayer, string command, CommandHandler handler)
    commands.get(thePlayer).put(command, handler)

public function registerCommandAll(string command, CommandHandler handler)
    for i = 0 to bj_MAX_PLAYER_SLOTS - 1
        registerCommand(players[i], command, handler)

public function deregisterCommand(player thePlayer, string command)
    if commands.get(thePlayer).has(command)
        commands.get(thePlayer).remove(command)

public function deregisterCommandAll(string command)
    for i = 0 to bj_MAX_PLAYER_SLOTS - 1
        deregisterCommand(players[i], command)

init
    let t = CreateTrigger()

    for i = 0 to bj_MAX_PLAYER_SLOTS - 1
        t.registerPlayerChatEvent(players[i], "-", false)

    t.addAction() ->
        let triggerPlayer = GetTriggerPlayer()
        let playerCommands = commands.get(triggerPlayer)
        let tokens = GetEventPlayerChatString().substring(1).split(" ")
        let command = tokens.get(0)
        if playerCommands.has(command)
            playerCommands.get(command).run(triggerPlayer, tokens)

    registerCommandAll("test-wurst-commands") (triggerPlayer, args) ->
        print("player " + triggerPlayer.getId().toString())
        args.forEach(i -> print(i))
