package StupefyField
import RegisterEvents
import LocalObjectIDs
import Icons
import Lodash
import ToolTipsUtils
import ChannelAbilityPreset
import Abilities
import BuffObjEditing

//@configurable constant int ABILITY_STUPEFY = 'A05W' // Dummy Channel ability, Old GUI
//@configurable constant int REAL_AURA_STUPEFY = 'A05V' // Real Aura, Old GUI

// So hum technically it reduces by 300%, I guess it's okay since there is a threshold on minimal as & ms
let AS_REDUCED = 3.0
let MS_REDUCED = 3.0

let TARGET_ALLOWED = "enemies,friend,ground,hero,organic,self,vulnerable"

let DURATION = 10.
let MANACOST = 10
let COOLDOWN = 60.

let ANIMATION_NAME = "stand,channel,spell"
let TOOLTIP_NORM = "Stupefy Field"
let TOOLTIP_EXTENDED = ("The Hypnotist generates a magic field. Anyone, including allies and the hypnotist, that enters this field becomes temporarily stupefied. "+
                       "Stupefied units have attack and movespeed extremely reduced. "+
                       "Last {0} seconds. Has {1} seconds cooldown.").format(DURATION.toToolTipLightBlue(), COOLDOWN.toToolTipLightBlue())

let REAL_AURA_STUPEFY = 'RAST'
let BUFF = compiletime(BUFF_ID_GEN.next())
let ORIGINAL_BUFF_ID = BUFF_STUPEFIED // Barkskin

function createBuff()
    new BuffDefinition(BUFF, ORIGINAL_BUFF_ID)
    ..setTargetAttachmentPoint0(1, "overhead")
    ..setIcon(Icons.bTNPolymorph)
    ..setTooltipNormal(1, "Stupefied")
    ..setTooltipNormalExtended(1, "This unit has been caught in stupefying field. It has extremely lowered attack speed and movement speed.")
    ..setArtTarget(1, Abilities.avengerMissile)

function createStupefyAura()
    new AbilityDefinitionAuraSlow(REAL_AURA_STUPEFY)
    ..setName("Stupefying Aura")
    ..setAttackSpeedFactor(1, -AS_REDUCED)
    ..setMovementSpeedFactor(1, -MS_REDUCED)
    ..setIconNormal(Icons.bTNLoad)
    ..setBuffs(1, toRawCode(BUFF))
    ..setTargetsAllowed(1, TARGET_ALLOWED)
    ..setTooltipNormal(1, "Stupefying Aura")
    ..setTooltipNormalExtended(1, "You shouldn't see this")

class StupefyField extends ChannelAbilityPreset
    construct(int newAbility, string hotkey, Pair<int , int> buttonPos)
        super(newAbility, 1, true)
        this.setName(TOOLTIP_NORM)
        this.setHotkeyNormal(hotkey)
        this.setCooldown(1, COOLDOWN)
        this.setManaCost(1, MANACOST)
        this.setIconNormal(Icons.bTNPolymorph)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setTooltipNormalExtended(1, TOOLTIP_EXTENDED)
        this.setTooltipNormal(1, makeToolTipNorm(hotkey, TOOLTIP_NORM))
        this.setArtDuration(1, 2)
        this.setFollowThroughTime(1, DURATION)
        this.setAnimationNames(ANIMATION_NAME)
        this.setHeroAbility(false)
        this.setArtTarget(Abilities.flameStrikeTarget)
        this.setEffects(1, Abilities.flameStrikeTarget)
        this.setArtCaster(commaList(Abilities.voodooAuraTarget, Abilities.flameStrikeTarget))

@compiletime function createStupefyField()
    new StupefyField(ABILITY_STUPEFY, "F", new Pair(3, 1))
    new StupefyField(ABILITY_DEMENTIA_MASTER_STUPEFY, "X", new Pair(3, 1))
    createStupefyAura()
    createBuff()

function onCast()
    var caster = GetSpellAbilityUnit()
    if not caster.hasAbility(REAL_AURA_STUPEFY)
        caster.addAbility(REAL_AURA_STUPEFY)

function onEndcast()
    var caster = GetSpellAbilityUnit()
    var spell = GetSpellAbilityId()
    if spell == ABILITY_STUPEFY or spell == ABILITY_DEMENTIA_MASTER_STUPEFY and caster.hasAbility(REAL_AURA_STUPEFY)
        caster.removeAbility(REAL_AURA_STUPEFY)
        caster.removeAbility('B00L') // Bams
        caster.removeAbility('B00M') // Bam2

init
    registerSpellEffectEvent(ABILITY_STUPEFY, () -> onCast())
    registerSpellEffectEvent(ABILITY_DEMENTIA_MASTER_STUPEFY, () -> onCast())
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_ENDCAST, () -> onEndcast())
