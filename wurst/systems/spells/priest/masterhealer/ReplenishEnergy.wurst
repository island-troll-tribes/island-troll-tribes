package ReplenishEnergy

import ChannelAbilityPreset
import ID
import Lodash
import Assets
import ToolTipsUtils
import BuffObjEditing
import LegacyColors

let BUFF_ID = compiletime(BUFF_ID_GEN.next())
let BUFF_ORIGINAL_ID = 'Blrl'

let AOE = 800.
let MANA_REGEN = 0.33
let TOOLTIP_NORM = "Replenish Energy"
let TOOLTIP_EXTENDED = "The Master Healer emanates a refreshing aura to himself and his allies, "+
                        "granting {0}|r energy regeneration per seconds.".format(COLOR_LIGHT_BLUE+MANA_REGEN.toString())

function createBuff()
    new BuffDefinition(BUFF_ID, BUFF_ORIGINAL_ID)
        ..setIcon(Icons.bTNTranquility)
        ..setTooltipNormal(1, TOOLTIP_NORM)
        ..setTooltipNormalExtended(1, "This unit is gaining energy from a nearby Master Healer.")

class ReplenishEnergy extends AbilityDefinitionArchMageBrillianceAura
    construct(int newAbilityId, Pair<int, int> buttonPos)
        super(newAbilityId)
        this.setName(TOOLTIP_NORM)
        this.presetTooltipNormal(lvl -> makeToolTipNormAura(TOOLTIP_NORM))
        this.presetTooltipNormalExtended(lvl -> TOOLTIP_EXTENDED)
        this.setButtonPositionNormalX(buttonPos.a)
        this.setButtonPositionNormalY(buttonPos.b)
        this.setIconNormal(Icons.pASBTNRegenerate)
        this.setIconResearch(Icons.pASBTNRegenerate)
        this.setBuffs(1, toRawCode(BUFF_ID))
        this.setCheckDependencies(false)
        this.setHeroAbility(false)
        this.setAreaofEffect(1, AOE)

@compiletime function createReplenishEnergy()
    new ReplenishEnergy(SPELL_REPLENISH_ENERGY, new Pair(2, 1))
    new ReplenishEnergy(SPELL_SAGE_REPLENISH_ENERGY, new Pair(0, 2))
    createBuff()